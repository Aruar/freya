//===== Freya Script =========================================
//= Universal Rental NPC
//===== By: ==================================================
//= eAthena
//===== Current Version: =====================================
//= 1.3
//===== Compatible With: =====================================
//= Freya
//===== Description: =========================================
//= Rent Falcons, Pecos, or Carts
//===== Additional Comments: =================================
// 1.0 First version
// 1.1 Peco Peco option added [Darkchild]
// 1.1a Fixed checkriding/cart/falcon functions [Lupus]
// 1.2 Added support for Baby classes, revised script [Harp]
// 1.3 Made some optimizations and other changes [Tsuyuki]
//============================================================


prontera.gat,122,200,1	script	Rentals	905,{

	mes "[Adelia]";
	mes "Hi there! I rent Carts, Falcons, and Pecos.";
	next;

	if (Class==5 || Class==10 || Class==18 || Class==4006 || Class==4011 || Class==4019 || Class==4041 || Class==4028 || Classes==4033) goto L_Cart;

	if (Class==11 || Class==4012 || Class==4034) goto L_Falcon;

	if (Class == 7 || Class == 14 || Class == 4008 || Class == 4015 || Class == 4030 || Class == 4037) goto L_Peco;

	mes "[Adelia]";
	mes "Unfortunately, I can only rent them to certain classes. You don't qualify for my services..";
	close;

// Rent a Cart
L_Cart:
	set @rentem$, "Cart";
	if (checkcart(0) > 0) goto L_Already_Have;
	set @skillid, 39;
	goto RentItem;

// Rent a Falcon
L_Falcon:
	set @rentem$, "Falcon";
	if (checkfalcon(0) > 0) goto L_Already_Have;
	set @skillid, 127;
	goto RentItem;

// Rent a Peco
L_Peco:
	set @rentem$, "PecoPeco";
	if (checkriding(0)>0) goto L_Already_Have;
	set @skillid, 63;
	goto RentItem;

// Checks if user has needed skill and whether the user has a Cart/Falcon/Peco already or not
// If neither is true, then user gets a cart
RentItem:
	if(getskilllv(@skillid)<1) goto L_Need_Skill;
	mes "[Adelia]";	
	mes strcharinfo(0) + ", Would you like to rent a " + @rentem$ + "?";
	next;
	menu "- Rent a " + @rentem$,-,"- No thanks",L_Quit;

	if (@rentem$=="Cart") setcart;
	if (@rentem$=="Falcon") setfalcon;
	if (@rentem$=="Peco") setriding;

	L_Quit2:
		mes "[Adelia]";
		mes "Thank you for using my services. Come again!";
		close;

	L_Quit:
		mes "[Adelia]";
		mes "Please come back when you are ready to rent a " + @rentem$ + "!";
		close;

	L_Need_Skill:
		mes "[Adelia]";
		mes "I'm sorry, but you don't have the required skill to handle a " + @rentem$ + ".";
		close;

	L_Already_Have:
		mes "[Adelia]";
		mes "Erm.. it seems you already have a ^996633" + @rentem$ +"^000000.";
		mes "Come back when you actually need my services...";
		close;
}
