//===== Freya Script =========================================
//= Kafra Functions
//===== By: ==================================================
//= Lotsa People (1.0)
//= eAthena
//= Darlskies
//= Darkchild
//= Syrus22
//= Lupus
//= kobra_k88
//= Tsuyuki
//===== Current Version: =====================================
//= 2.9
//===== Compatible With: =====================================
//= Freya
//===== Description: =========================================
//= These functions  handle save, storage, cart rental, teleport,
//= and Kafra pass options for all Kafra NPCs.
//===== Additional Comments: =================================
//= 1.0 Added first version [Athena]
//= 1.1 Now using functions :)
//= 2.1 Added Cart Rent for Classes: Whitesmith, Creator [eAthena]
//= Replaced checkoption(x) into checkcart(0) [Lupus]
//= 2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! A -Izlude[4] fix [Lupus]
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option [kobra_k88]
//= 2.3 Rearranged next statements to make menu transitions smoother [kobra_k88]
//= 2.3a Fixed typo >_< [Aria]
//= 2.4 Added new teleport service locations, commented them until I get iRO
//= warpto locations [Tsuyuki]
//= 2.5 Removed some of the new teleport service locations due to them not being
//= in kRO. That includes all of Swartzwald republic (except Yuno) because
//= it uses the Airship system, and the Global Project cities (Ayothaya.. etc) [Tsuyuki]
//= 2.6 Updated with a big haul of official iRO text, and corrected some other things [Tsuyuki]
//= 2.7 Added new Menu functions [Tsuyuki]
//= 2.8 Fixed several bugs as well as fixed the Kafra pass system entirely [Tsuyuki]
//= 2.9 Added special reserve points check [Tsuyuki]
//= 3.0 Fixed Kafra Save System [Tsuyuki]
//============================================================


// Main Function ===========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//==========================================================================


function	script	F_Kafra	{

	if (RESRVPTS < 25000) goto L_Cont01;

	mes "[Kafra Employee]";
	mes "Your special reserve points have exceeded";
	mes "the maximum capacity.";
	mes "Please be advised to spend your";
	mes "special reserved points";
	mes "at the headquarters of Kafra Corporation";
	mes "in Al De Baran before you attempt to use any of Kafra services in the future.";
	close;

L_Cont01:
	set @kafPass,0;
	mes "[Kafra Employee]";
	if (getarg(0)==0) mes "Welcome to the Kafra Corporation. The Kafra services are always on your side. How may I assist you?";
	if (getarg(0)==1) mes "Welcome... Kafra Services.... Will be with you even if you die.....";
	if (getarg(0)==2) mes "Welcome, ^5533FF" + GetGuildName(@GID) + "^000000 members. The Kafra services are always on your side. How may I assist you?";
	next;

	M_Menu:

	if (getarg(0)==2) menu "Use Storage",M_Storage, "Use Guild Storage",M_GStorage, "Rent a Pushcart",M_Cart, "Use Teleport Service",M_Teleport, "Cancel",M_End; // Guild Kafra Employees

	if (getarg(1)==0) menu "Save",M_Save, "Use Storage",M_Storage, "Use Teleport Service",M_Teleport, "Rent a Pushcart",M_Cart, "Kafra Pass",M_Pass,"Check Other Information",M_Info, "Cancel",M_End; // Normal/Complete Kafra

	if (getarg(1)==1) menu "Save",M_Save, "Use Storage",M_Storage, "Rent a Pushcart",M_Cart, "Kafra Pass",M_Pass, "Check Other Information",M_Info,"Cancel",M_End; // No Teleport Service

	if (getarg(1)==2) menu "Use Storage",M_Storage, "Rent a Pushcart",M_Cart, "Kafra Pass",M_Pass, "Check Other Information",M_Info,"Cancel",M_End; // No Save or Teleport Service

	if (getarg(1)==3) menu "Save",M_Save, "Use Storage",M_Storage, "Use Teleport Service",Swz_Teleport, "Rent a Pushcart",M_Cart, "Kafra Pass",M_Pass,"Check Other Information",M_Info, "Cancel",M_End; // Swartzvalt Republic Kafras

	if (getarg(1)==4) menu "Use Storage",M_Storage, "Use Teleport Service",L_Teleport, "Rent a Pushcart",M_Cart, "Kafra Pass",M_Pass, "Check Other Information",M_Info,"Cancel",M_End; // No Save

	if (getarg(1)==5) menu "Use Storage",M_Storage, "Cancel",M_End; // Only Storage

	if (getarg(1)==6) menu "Save",M_Save, "Use Storage",M_Storage, "Cancel",M_End; // Only Save & Storage

	if (getarg(1)==7) menu "Use Storage",M_Storage, "Check Other Information",M_Info,"Cancel",M_End; // Storage & Info

	M_Save:
		mes "[Kafra Employee]";
		mes "Your Respawn Point";
		mes "has been saved. Thank you";
		mes "for using the Kafra Service.";
		close2;
		cutin "", 255;
		return; // Returns back to function calling place, and passes into the M_Save label

		// To-Do: Official iRO Dialogue for Saving:
		// mes "[Kafra Employee]";
		// mes "Your Respawn Point";
		// mes "has been saved in the";
		// mes "*city/town* of *xxxx*. Thank you";
		// mes "for using the Kafra Service.";
		// * Save Respawn Point Here *
		// close2;
		// cutin "", 255;
		// end;

	M_Storage:
		callfunc "F_KafStor",getarg(0);

	M_GStorage:
		callfunc "F_KafStor",3;

	M_Teleport:
		callfunc "F_KafTele",getarg(0);

	M_Cart:
		callfunc "F_KafCart",getarg(0);

	M_Pass:
		callfunc "F_KafPass",getarg(0);
		goto M_Menu;

	M_Info:
		callfunc "F_KafInfo",getarg(2);

	M_End:
		callfunc "F_KafEnd",getarg(0),0;

	Swz_Teleport:
		callfunc "F_KafSwzTele";
}


// Storage Function =======================================================

function	script	F_KafStor	{
	if (getarg(0) == 3) goto L_Guild;
	if ((class ==JOB_NOVICE) && (joblevel<6)) goto sL_JbLvl;
	if (Class == JOB_NOVICE) set @fee, 30;
	if (Class != JOB_NOVICE) set @fee, 60;
	if (@kafPass==1 || getarg(0)==2) set @fee, 0;
	if (Zeny<@fee) goto sL_Zeny;
	set Zeny, Zeny-@fee;
	set RESRVPTS, RESRVPTS + (@fee/5);

	mes "[Kafra Employee]";
	mes "Here let me open";
	mes "your Storage for you.";
	mes "Thank you for using";
	mes "the Kafra Service.";
	close2;
	openstorage;
	cutin "", 255;
	end;

	sL_JbLvl:
		mes "[Kafra Employee]";
		mes "I am sorry but you have to be at least Novice job level 6 if you want to use the storage.";
		close2;
		cutin "", 255;
		end;

	sL_Zeny:
		mes "[Kafra Employee]";
		mes "Dear you don't have enough money. The Storage fee is "+@fee+" Zeny.";
		close2;
		cutin "", 255;
		end;

L_Guild:
	if (guildopenstorage(0) == 1) goto L_InUse;
	close2;
	cutin "", 255;
	end;

	L_InUse:
		mes "[Kafra Employee]";
		mes "I'm sorry but another guild member is using the guild storage";
		mes "right now.  Please wait until that person is finished.";
		close2;
		cutin "", 255;
		end;
}


// Teleport Function ==================================================

function	script	F_KafTele	{
	mes "[Kafra Employee]";
	mes "Please choose";
	mes "your destination.";
	next;

	menu @wrpC$[0],M_Wrp0, @wrpC$[1],M_Wrp1, @wrpC$[2],M_Wrp2, @wrpC$[3],M_Wrp3,
	     @wrpC$[4],M_Wrp4, @wrpC$[5],M_Wrp5, @wrpC$[6],M_Wrp6;

	M_Wrp0:
		set @num, 0;
		goto L_Warp;
	M_Wrp1:
		set @num, 1;
		goto L_Warp;
	M_Wrp2:
		set @num, 2;
		goto L_Warp;
	M_Wrp3:
		set @num, 3;
		goto L_Warp;
	M_Wrp4:
		set @num, 4;
		goto L_Warp;
	M_Wrp5:
		set @num, 5;
		goto L_Warp;
	M_Wrp6:
		set @num, 6;

	L_Warp:
		if (@wrpC$[@num] == "Cancel") goto L_End;
		if (@kafPass == 1) set @wrpP[@num], 0;
		if (Zeny < @wrpP[@num]) goto sL_CantTele;
		set Zeny, Zeny - @wrpP[@num];
		if (@kafPass == 0) set RESRVPTS, RESRVPTS + (@wrpP[@num]/16);

		if (@wrpD$[@num] == "Alberta") warp "alberta.gat", 117, 56;
		if (@wrpD$[@num] == "Al De Baran") warp "aldebaran.gat",143,110;
		if (@wrpD$[@num] == "Comodo") warp "comodo.gat", 207, 144;
		if (@wrpD$[@num] == "Izlude") warp "izlude.gat", 91, 105;
		if (@wrpD$[@num] == "Geffen") warp "geffen.gat", 120, 39;
		if (@wrpD$[@num] == "Morroc") warp "morocc.gat", 156, 46;
		if (@wrpD$[@num] == "Payon") warp "payon.gat", 168, 103;
		if (@wrpD$[@num] == "Prontera") warp "prontera.gat", 116, 72;
		if (@wrpD$[@num] == "Coal Mine(Dead Pit)") warp "mjolnir_02.gat", 82, 347;
		if (@wrpD$[@num] == "Yuno") warp "yuno.gat", 157, 123;
		if (@wrpD$[@num] == "Coal Mine") warp "mjolnir_02.gat", 82, 347;
		if (@wrpD$[@num] == "Comodo Pharos Lighthouse") warp "cmd_fild07.gat", 127, 134;
		if (@wrpD$[@num] == "Orc Dungeon") warp "gef_fild10.gat", 52, 326;
		if (@wrpD$[@num] == "Umbala") warp "umbala.gat", 130, 130;
		close2;		// This part safegaurds against errors/typos
		set Zeny, Zeny + @wrpP[@num];
		cutin "", 255;
		end;

		sL_CantTele:
			mes "[Kafra Employee]";
			mes "Dear you don't have enough money. Please check your funds again.";
			close2;
			cutin "", 255;
			end;

		L_End:
			close2;
			cutin "", 255;
			end;
}


// Cart Function ========================================================

function	script	F_KafCart	{
	if (callfunc("Is_Merc_Class") == 0) goto sL_CantRent;
	if (getskilllv(39) == 0) goto sL_NeedSkill;
	if (checkcart(0) == 1) goto sL_GotCart;
	if (getarg(0) == 2) goto L_Guild;
	mes "[Kafra Employee]";
	if (@kafPass == 0) mes "The Cart Fee is 800 Zeny.  Do you want to Rent a Cart?";
	if (@kafPass == 1) mes "Since you're using a Kafra Pass, you can rent a cart for free!";
	next;
	menu "Rent a Cart.",-, "Cancel.",M_End;

		if (Zeny < 800 && kafPass == 0) goto sL_CartFee;
		if (@kafPass == 0) set Zeny,Zeny-800;
		if (@kafPass == 0) set RESRVPTS, RESRVPTS + 48;
		goto L_Guild;

	L_Guild:
		setcart;
		mes "[Kafra Employee]";
		mes "Here is your cart.";
		close2;
		cutin "", 255;
		end;

	sL_CantRent:
		mes "[Kafra Employee]";
		mes "I'm sorry, but the";
		mes "Pushcart rental service";
		mes "is only available to Merchants,";
		mes "Blacksmiths, Alchemists, White";
		mes "Smiths and Creators.";
		close2;
		cutin "", 255;
		end;

	sL_NeedSkill:
		mes "[Kafra Employee]";
		mes "I'm sorry but you need the skill ^0000FF'Pushcart'^000000 to rent a cart.";
		close2;
		cutin "", 255;
		end;

	sL_GotCart:
		mes "[Kafra Employee]";
		mes "Excuse me... but you already have a cart....";
		emotion 4;
		close2;
		cutin "", 255;
		end;

	sL_CartFee:
		mes "[Kafra Employee]";
		mes "Dear, you don't have enough Money. You need 800 Zeny.";
		close2;
		cutin "", 255;
		end;

	M_End:
		close2;
		cutin "", 255;
		end;
}


// Pass Function ===============================================================

function	script	F_KafPass	{

	sM_Menu:
	menu "Use a Kafra Pass.",-, "What is a Kafra Pass?",sM_PassInfo, "Cancel",sM_End;

		mes "[Kafra Employee]";
		mes "Let me just check your pass.....";
		next;
		if (usedKafPass == 0 && countitem(1084) == 0) goto sL_NeedPass;
		set @kafPass,1;
		set usedKafPass, usedKafPass + 1;
		if (usedKafPass >= 3) goto sL_PassExpire;
		if (usedKafPass > 1) goto L_Cont; // Fixed Lupus
		delitem 1084,1;
		mes "(you hand her your pass)";
		next;
		mes "[Kafra Employee]";
		mes "Great! Everything seems to be in order.  Now that your pass is activated, you may rent a cart or use the teleport services for free.";
		mes "Your pass number has been entered into our database so you no longer need it.";
		next;
		goto L_Cont;

		L_Cont:
			mes "[Kafra Employee]";
			mes "You will be able to use the Cart Rental and Teleport services free of charge ^5533FF"+(3 - usedKafPass)+"^000000 more times with any Kafra service agent you choose.";
			next;
			return;

		sL_NeedPass:
			mes "[Kafra Employee]";
			mes "I'm sorry but you don't have a kafra pass to use....";
			close2;
			cutin "", 255;
			end;

		sL_PassExpire:
			mes "[Kafra Employee]";
			mes "This is going to be the 3rd and final time you use this pass, therefore it is now expired.";
			set usedKafPass,0;
			next;
			mes "[Kafra Employee]";
			mes "You may now use the Teleport and Cart Rental services for free.";
			next;
			return;
	sM_PassInfo:
		mes "[Kafra Employee]";
		mes "A ^5533FFKafra Pass^000000 is a unique voucher that lets you use Kafra services for free!";
		mes "The Kafra services that you may use for free are the ^FF3355Teleport^000000 service and the ^FF3355Cart Rental^000000 service.";
		next;
		mes "[Kafra Employee]";
		mes "Kafra passes can be purchased at the Kafra Corp. Main office in Al De Baran.";
		next;
		mes "[Kafra Employee]";
		mes "To use a Kafra Pass, simply choose the option to 'Use a Kafra Pass', when speaking with a Kafra agent such as myself.";
		mes "Your pass number will be entered into our database, and you will then be able to use the Teleport, and Cart Rental Kafra services free of charge.";
		next;
		mes "[Kafra Employee]";
		mes "Once you have finished using the desired services, and have stoped interacting with the Kafra, your 'free use' session will end.";
		mes "You will have a total of ^5533FF 3 'free use' sessions^000000 available upon activation of your Kafra Pass.";
		next;
		mes "[Kafra Employee]";
		mes "To begin another 'free use' session, simply select the 'Use a Kafra Pass' option when speaking with a Kafra Agent.";
		next;
		mes "[Kafra Employee]";
		mes "Believe me when I say that the Kafra Pass is a great bargain!!";
		mes "With the Kafra Pass, we hope to give players some incentive to use our great services.";
		close2;
		cutin "", 255;
		end;

	sM_End:
		close2;
		cutin "", 255;
		end;
}

// Special Reserve Points Function ===========================================

function	script	F_KafInfo	{

	sM_Menu:
	if (getarg(0) == 0) menu "Check Special Reserve Points",sM_ResChk, "Kafra Employee Locations",sM_KafLoc, "Cancel",sM_End;

	sM_ResChk:
		mes "[Kafra Employee]";
		mes "Lets see...";
		mes ""+strcharinfo(0)+"...";
		mes "Ah, you have a total of";
		mes ""+RESRVPTS+" Special Reserve Points.";
		next;
		mes "[Kafra Employee]";
		mes "You can exchange your";
		mes "Special Reserve Points for";
		mes "rewards at the Kafra Main Office";
		mes "in Al De Baran. Please use our";
		mes "convenient services to see the";
		mes "benefits of our rewards program.";
		close2;
		cutin "", 255;
		end;

	sM_KafLoc:
		viewpoint 1,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 1,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 1,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 1,@viewpX[3],@viewpY[3],4,0xFF00FF;
		close2;
		cutin "", 255;
		end;

	sM_End:
		close2;
		cutin "", 255;
		end;
}


// End Function =====================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is diplayed.
//===================================================================

function	script	F_KafEnd	{
	if (getarg(1) == 1) goto F_KafSaveEnd; // Player Saved Ending Function
	mes "[Kafra Employee]";
	if (getarg(0) != 1) mes "We, here at Kafra Corporation, are always endeavoring to provide you with the best services. We hope that we meet your adventuring needs and standards of excellence.";
	if (getarg(0) == 1) mes "We, Kafra Corporation.... Will be with you.... whenever.... wherever... therefore.... please don't forget us.....";
	close2;
	cutin "", 255;
	end;

F_KafSaveEnd:
	close2;
	cutin "", 255;
	end;
}
