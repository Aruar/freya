//===== eAthena Script =======================================
//= Spawn Survivor de Sapho
//===== By: ==================================================
//= Garion, GAlion, DaVinci, Jondalar, Kheldar
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= Espérons, Nezumi, JAthena, et EAthena
//===== Description: =========================================
//= 1.0.
//= 1.1 - déplacement de la fonction f_monstres au début
//= 1.2 - harmonisation warp warpret -> warprep
//=       changement getmapusers -> getmapaliveusers
//= 1.3 - guild_vs4 -> guild_vs4.gat
//= 1.4 - pvpoff/pvpon/gvgoff/gvgon fixées
//= 1.5 - nombre de monstre! 5xnb de joueurs -> 5x(nb de joueurs + rnd(1,3))
//=       maximum passé de 100 à 150 (c'est une petite carte)
//=       mapannounce placés avant les appels aux fonctions
//=       Suppression des 'removeflag' à la fin de l'event.
//=       Ajout d'un second warp pour sortir.
//=       Respawn de monstre à mi-temps si les joueurs sont trop efficaces.
//= 1.6   Correction chonchon 1063->1011 (spawn mi-temps)
//=       Reset de la variable $@spawn_rnd apèrs usage (spawn mi-temps)
//=       Ajout de poring pour faire patienter les joueurs dans la salle au début
//=       Activation de l'event tous les jours à 01h00.
//=       gain: 1000 zenys au gagnant (en attendant mieux).
//= 1.7   pas de message "Nettoyage" s'il n'y a pas de monstre
//=       Ajout d'un message sur la map dans les spawn des monstres qui font patienter l'event
//=       Démarrage automatique: activation de l'event (enablenpc)
//=       ajout du check du dernier mob spawné.
//=       Ajout d'un test pour savoir quand c'est terminé (tous les mobs tués)
//=       Modification du test pour respawner (respawn lorsqu'il reste moins de 20% des mobs).
//=       Reset de la variable $@var après usage.
//=       Changement de map: guild_vs4.gat -> guild_vs5.gat
//=       Information du nombre de survivant en message serveur (pas uniquement en message map).
//=       Ajout d'un message indiquant le nombre de monstres restant sur les spawns précédents.
//=       Indication du nombre de monstres restants à tuer après le dernier spawn.
//=       Gain: 1000z->5000z en attendant mieux.
//=       Changement de nom: xxx affamé -> xxx carnivore.
//=       Au chargement du NPC, désactiver le NPC Surviv
//=       Au chargement du NPC, tuer tous les monstres
//=       Reset $@nb_part, $@nb_mobs et $@nb_part_en_vie à la fin de l'event
//=       Rappel des règles au début de l'event
//=       Modification de l'heure: 13h50->13h30 (le dernier a duré 3h!)
//=       Check des resurrections.
//=       Installation de 4 sorties.
//===== Additional Comments: =================================
//=Mettre les warp et les bonnes coordonnées pour les NPC.
//============================================================

// les Warps
prontera.gat,178,148,4	warp	warpspawn	2,3,guild_vs5.gat,56,49
guild_vs5.gat,50,76	warp	warprep	2,3,prontera.gat,165,151
guild_vs5.gat,78,49	warp	warprep2	2,3,prontera.gat,165,151
guild_vs5.gat,49,23	warp	warprep3	2,3,prontera.gat,165,151
guild_vs5.gat,20,50	warp	warprep4	2,3,prontera.gat,165,151


// Fin de l'event
function	script	close_spawn	{
// Ne pas permettre ces flags à la fin de l'event.
// S'il y a des survivants, cela leur permettrait de se battre, etc...
//	removemapflag "guild_vs5",mf_nopvp;
//	removemapflag "guild_vs5",mf_nomemo;
//	removemapflag "guild_vs5",mf_nobranch;
//	removemapflag "guild_vs5",mf_pvp_noguild;
//	removemapflag "guild_vs5",mf_nopenalty;
//	removemapflag "guild_vs5",mf_nozenypenalty;
//	pvpon "guild_vs5.gat";
//	gvgon "guild_vs5.gat";
	killmonsterall "guild_vs5.gat";
	set $@Spawn_flag,0; // $@Spawn_flag: 0: event pas en cours, 1: event en cours, 2: dernier mob spawné
	set $@nb_part,0;
	set $@nb_mobs,0;
	set $@nb_part_en_vie,0;
	return;
}


// spawn des monstres
function	script	f_monstres	{
	set $@nb_part,getmapusers2("guild_vs5.gat") - getmapgms2("guild_vs5.gat",20); // alive players less alive GMs
	if ($@nb_part < 1) goto L_stop;
	if (agitcheck(0) != 0) goto L_woe;
	if ($@nb_part > $@nb_part_en_vie) goto L_triche; // quelqu'un a été téléporté ou rescussité
	set $@nb_part_en_vie, $@nb_part;

	// spawn des monstres
	set $@var,getarg(0);
	areamonsteragro "guild_vs5.gat",0,0,200,200,"Survivor",$@var,$@nb_mobs,"Surviv::OnMortMob";
	announce "Garde Royal: Encore " + $@nb_part + " participants en lice pour le titre de Master Survivor!",0;
	if (GetMapMobs("guild_vs5.gat") <= $@nb_mobs) goto Suite_spawn;
	mapannounce "guild_vs5.gat","Il reste encore " + (GetMapMobs("guild_vs5.gat") - $@nb_mobs) + " monstres des spawns précédents...",0;
	goto Suite_spawn;

Suite_spawn:
	mapannounce "guild_vs5.gat","Invasion de '"+ strmobinfo(2,$@var)+"'.",0;
	set $@var,0;
	end;

L_stop:
	doevent "Surviv::L_stoptimer";// tjs en 1ere ligne
	announce "La salle de Spawn Survivor est vide (ou tous les partipants sont morts).",0;
	announce "L'évent s'est arrêté de lui-même.",0;
	goto L_fin;

L_woe:
	doevent "Surviv::L_stoptimer";// tjs en 1ere ligne
	announce "Le Spawn Survivor est arreté pour cause de Woe.",0;
	mapannounce "guild_vs5.gat","Garde Royal: Les joueurs présents dans la salle doivent deco/reco. Merci.",0;
	goto L_fin;

L_triche:
	doevent "Surviv::L_stoptimer";// tjs en 1ere ligne
	announce "Le Spawn Survivor est arreté pour cause de TRICHE!",0;
	announce "Un joueur a été téléporté dans la salle ou rescussité!",0;
	mapannounce "guild_vs5.gat","Garde Royal: Les joueurs présents dans la salle doivent deco/reco. Merci.",0;
	goto L_fin;

L_fin:
	stopnpctimer;
	callfunc "close_spawn";
	end;
}


// fonction testant si les joueurs sont trop efficaces afin de respawner à mi-temps d'autres monstres pour les occuper
function	script	check_monstres	{
	if (GetMapMobs("guild_vs5.gat") >= ($@nb_mobs / 5)) end;
	// respawner des monstres si les joueurs les tuent trop vite
	set $@spawn_rnd, rand(1,5);
	if ($@spawn_rnd == 1) goto rnd1;
	if ($@spawn_rnd == 2) goto rnd2;
	if ($@spawn_rnd == 3 || $@spawn_rnd == 4) goto rnd3;

	// porings
	areamonsteragro "guild_vs5.gat",0,0,200,200,"Pour patienter...",1002,$@nb_mobs,"Surviv::OnMortMob";
	set $@spawn_rnd, 0;
	return;

rnd1:
	// lunatics
	areamonsteragro "guild_vs5.gat",0,0,200,200,"Lapinou carnivore",1063+2000,$@nb_mobs,"Surviv::OnMortMob"; // +2000 pour 'petit' monstre
	set $@spawn_rnd, 0;
	return;

rnd2:
	// ChonChons
	areamonsteragro "guild_vs5.gat",0,0,200,200,"Moustique",1011+2000,$@nb_mobs,"Surviv::OnMortMob"; // +2000 pour 'petit' monstre
	set $@spawn_rnd, 0;
	return;

rnd3:
	// le dernier monstre, répété
	areamonsteragro "guild_vs5.gat",0,0,200,200,"Survivor",getarg(0),$@nb_mobs,"Surviv::OnMortMob";
	set $@spawn_rnd, 0;
	return;
}


guild_vs5.gat,51,49,1	script	Surviv	-1,{
	//mapannounce "guild_vs5.gat","timer: "+ getnpctimer(0)+"     "+getnpctimer(1)+"     "+ getnpctimer(2),0;
	//close;
	end;

OnStart:
	initnpctimer;
	set $@spawn1,1002; // Poring
	set $@spawn2,1063; // Lunatic
	set $@spawn3,1011; // ChonChon
	set $@spawn4,1008; // Pupa
	set $@spawn5,1031; // Poporing
	set $@spawn6,1015; // Zombie
	set $@spawn7,1056; // Smokie
	set $@spawn8,1068; // Hydra
	set $@spawn9,1077; // Poison Spore
	set $@spawn10,1105; // Deniro
	set $@spawn11,1178; // Zerom
	set $@spawn12,1030; // Anacondaq
	set $@spawn13,1145; // Martin
	set $@spawn14,1100; // Argos
	set $@spawn15,1392; // Rotar Zairo
	set $@spawn16,1060; // Bigfoot
	set $@spawn17,1067; // Cornutus
	set $@spawn18,1064; // Megalodon
	set $@spawn19,1106; // Desert Wolf
	set $@spawn20,1258; // Goblin Archer
	set $@spawn21,1119; // Frilldora
	set $@spawn22,1141; // Marina
	set $@spawn23,1127; // Hode
	set $@spawn24,1146; // Matyr
	set $@spawn25,1282; // Kobold Archer
	set $@spawn26,1110; // Dokebi
	set $@spawn27,1248; // Cruiser
	set $@spawn28,1026; // Munak
	set $@spawn29,1471; // Nine Tail
	set $@spawn30,1261; // Wild Rose
	set $@spawn31,1016; // Skeleton Archer
	set $@spawn32,1400; // Karakasa
	set $@spawn33,1138; // Magnolia
	set $@spawn34,1165; // Sandman
	set $@spawn35,1249; // Myst Case
	set $@spawn36,1403; // Firelock Soldier
	set $@spawn37,1040; // Golem
	set $@spawn38,1680; // Hill Wind
	set $@spawn39,1099; // Argiope
	set $@spawn40,1209; // Cramp
	set $@spawn41,1029; // Isis
	set $@spawn42,1144; // Marse
	set $@spawn43,1274; // Megalith
	set $@spawn44,1102; // Bathory
	set $@spawn45,1715; // Novus
	set $@spawn46,1170; // Sohee
	set $@spawn47,1156; // Sky Petite
	set $@spawn48,1215; // Stem Worm
	set $@spawn49,1213; // High Orc
	set $@spawn50,1271; // Alligator
	set $@nb_part,getmapusers2("guild_vs5.gat") - getmapgms2("guild_vs5.gat",20); // alive players less alive GMs
	set $@nb_part_en_vie,$@nb_part; // pour tester les cas de triche

	// ajouter: si un seul participant ou aucun, tout arrêter

	set $@nb_mobs,($@nb_part + rand(1,4)) * 5;
	if ($@nb_mobs <  30) set $@nb_mobs,30;
	if ($@nb_mobs > 150) set $@nb_mobs,150;
	mapannounce "guild_vs5.gat","Garde Royal: 30 secondes entre chaque spawn de monstres différents. Survivez !",0; //Première slave.
	mapannounce "guild_vs5.gat","Garde Royal: Pour devenir le Master Survivor, il vous faut tuer un monstre en étant le dernier survivant!",0;
	mapannounce "guild_vs5.gat","Garde Royal: Règle importante: Il est INTERDIT de téléporter un joueur dans la salle ou de rescussiter un joueur.",0;
	announce "Il y a " + $@nb_part + " participant(s) pour le Spawn Survivor. Bonne chance!",0;
	callfunc "f_monstres", $@spawn1;
	close;

OnTimer30000:
	callfunc "f_monstres", $@spawn2;
	close;

OnTimer60000:
	callfunc "f_monstres", $@spawn3;
	close;

OnTimer90000:
	callfunc "f_monstres", $@spawn4;
	close;

OnTimer120000:
	mapannounce "guild_vs5.gat","Garde Royal: 60 secondes entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres", $@spawn5;
	close;

OnTimer180000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Continuons à monter en puissance...",0;
	callfunc "f_monstres", $@spawn6;
	close;

OnTimer240000:
	callfunc "f_monstres", $@spawn7;
	close;

OnTimer300000:
	callfunc "f_monstres", $@spawn8;
	close;

OnTimer360000:
	callfunc "f_monstres", $@spawn9;
	close;

OnTimer420000:
	callfunc "f_monstres", $@spawn10;
	close;

OnTimer480000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Plus difficile maintenant.",0;
	mapannounce "guild_vs5.gat","Garde Royal: une minute 30 entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres", $@spawn11;
	close;
OnTimer525000: // pour patienter (45 sec après le spawn)
	callfunc "check_monstres", $@spawn11;
	close;

OnTimer570000:
	callfunc "f_monstres", $@spawn12;
	close;
OnTimer615000: // pour patienter (45 sec après le spawn)
	callfunc "check_monstres", $@spawn12;
	close;

OnTimer660000:
	callfunc "f_monstres", $@spawn13;
	close;
OnTimer705000: // pour patienter (45 sec après le spawn)
	callfunc "check_monstres", $@spawn13;
	close;

OnTimer750000:
	callfunc "f_monstres", $@spawn14;
	close;
OnTimer795000: // pour patienter (45 sec après le spawn)
	callfunc "check_monstres", $@spawn14;
	close;

OnTimer840000:
	callfunc "f_monstres", $@spawn15;
	close;
OnTimer885000: // pour patienter (45 sec après le spawn)
	callfunc "check_monstres", $@spawn15;
	close;

OnTimer930000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Plus dur maintenant.",0;
	mapannounce "guild_vs5.gat","Garde Royal: 2 minutes entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres" , $@spawn16;
	close;
OnTimer990000: // pour patienter (1 min après le spawn)
	callfunc "check_monstres", $@spawn16;
	close;

OnTimer1050000:
	callfunc "f_monstres", $@spawn17;
	close;
OnTimer1110000: // pour patienter (1 min après le spawn)
	callfunc "check_monstres", $@spawn17;
	close;

OnTimer1170000:
	callfunc "f_monstres", $@spawn18;
	close;
OnTimer1230000: // pour patienter (1 min après le spawn)
	callfunc "check_monstres", $@spawn18;
	close;

OnTimer1290000:
	callfunc "f_monstres", $@spawn19;
	close;
OnTimer1350000: // pour patienter (1 min après le spawn)
	callfunc "check_monstres", $@spawn19;
	close;

OnTimer1410000:
	callfunc "f_monstres", $@spawn20;
	close;
OnTimer1470000: // pour patienter (1 min après le spawn)
	callfunc "check_monstres", $@spawn20;
	close;

OnTimer1530000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Encore plus dur maintenant.",0;
	mapannounce "guild_vs5.gat","Garde Royal: 2 minutes 30 entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres" , $@spawn21;
	close;
OnTimer1605000: // pour patienter (1 min 15 après le spawn)
	callfunc "check_monstres", $@spawn21;
	close;

OnTimer1680000:
	callfunc "f_monstres", $@spawn22;
	close;
OnTimer1755000: // pour patienter (1 min 15 après le spawn)
	callfunc "check_monstres", $@spawn22;
	close;

OnTimer1830000:
	callfunc "f_monstres", $@spawn23;
	close;
OnTimer1905000: // pour patienter (1 min 15 après le spawn)
	callfunc "check_monstres", $@spawn23;
	close;

OnTimer1980000:
	callfunc "f_monstres", $@spawn24;
	close;
OnTimer2055000: // pour patienter (1 min 15 après le spawn)
	callfunc "check_monstres", $@spawn24;
	close;

OnTimer2130000:
	callfunc "f_monstres", $@spawn25;
	close;
OnTimer2205000: // pour patienter (1 min 15 après le spawn)
	callfunc "check_monstres", $@spawn25;
	close;

OnTimer2280000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Encore plus difficile maintenant.",0;
	mapannounce "guild_vs5.gat","Garde Royal: 180 secondes entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres", $@spawn26;
	close;
OnTimer2370000: // pour patienter (1 min 30 après le spawn)
	callfunc "check_monstres", $@spawn26;
	close;

OnTimer2460000:
	callfunc "f_monstres", $@spawn27;
	close;
OnTimer2550000: // pour patienter (1 min 30 après le spawn)
	callfunc "check_monstres", $@spawn27;
	close;

OnTimer2640000:
	callfunc "f_monstres", $@spawn28;
	close;
OnTimer2730000: // pour patienter (1 min 30 après le spawn)
	callfunc "check_monstres", $@spawn28;
	close;

OnTimer2820000:
	callfunc "f_monstres", $@spawn29;
	close;
OnTimer2810000: // pour patienter (1 min 30 après le spawn)
	callfunc "check_monstres", $@spawn29;
	close;

OnTimer3000000:
	callfunc "f_monstres", $@spawn30;
	close;
OnTimer3090000: // pour patienter (1 min 30 après le spawn)
	callfunc "check_monstres", $@spawn30;
	close;

OnTimer3180000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Passons aux choses sérieuses...",0;
	mapannounce "guild_vs5.gat","Garde Royal: 3 minutes 30 entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres", $@spawn31;
	close;
OnTimer3285000: // pour patienter (1 min 45 après le spawn)
	callfunc "check_monstres", $@spawn31;
	close;

OnTimer3390000:
	callfunc "f_monstres", $@spawn32;
	close;
OnTimer3495000: // pour patienter (1 min 45 après le spawn)
	callfunc "check_monstres", $@spawn32;
	close;

OnTimer3600000:
	callfunc "f_monstres", $@spawn33;
	close;
OnTimer3705000: // pour patienter (1 min 45 après le spawn)
	callfunc "check_monstres", $@spawn33;
	close;

OnTimer3810000:
	callfunc "f_monstres", $@spawn34;
	close;
OnTimer3915000: // pour patienter (1 min 45 après le spawn)
	callfunc "check_monstres", $@spawn34;
	close;

OnTimer4020000:
	callfunc "f_monstres", $@spawn35;
	close;
OnTimer4125000: // pour patienter (1 min 45 après le spawn)
	callfunc "check_monstres", $@spawn35;
	close;

OnTimer4230000:
	callfunc "f_monstres", $@spawn36;
	close;
OnTimer4335000: // pour patienter (1 min 45 après le spawn)
	callfunc "check_monstres", $@spawn36;
	close;

OnTimer4440000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Après ces mises en bouches... le plat principal.",0;
	mapannounce "guild_vs5.gat","Garde Royal: 4 minutes entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres", $@spawn37;
	close;
OnTimer4560000: // pour patienter (2 min après le spawn)
	callfunc "check_monstres", $@spawn37;
	close;

OnTimer4680000:
	callfunc "f_monstres", $@spawn38;
	close;
OnTimer4800000: // pour patienter (2 min après le spawn)
	callfunc "check_monstres", $@spawn38;
	close;

OnTimer4920000:
	callfunc "f_monstres", $@spawn39;
	close;
OnTimer5040000: // pour patienter (2 min après le spawn)
	callfunc "check_monstres", $@spawn39;
	close;

OnTimer5160000:
	callfunc "f_monstres", $@spawn40;
	close;
OnTimer5280000: // pour patienter (2 min après le spawn)
	callfunc "check_monstres", $@spawn40;
	close;

OnTimer5400000:
	callfunc "f_monstres", $@spawn41;
	close;
OnTimer5520000: // pour patienter (2 min après le spawn)
	callfunc "check_monstres", $@spawn41;
	close;

OnTimer5640000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Vous avez demandez plus fort...",0;
	mapannounce "guild_vs5.gat","Garde Royal: 4 minutes 30 entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres", $@spawn42;
	close;
OnTimer5775000: // pour patienter (2 min 15 après le spawn)
	callfunc "check_monstres", $@spawn42;
	close;

OnTimer5910000:
	callfunc "f_monstres", $@spawn43;
	close;
OnTimer6045000: // pour patienter (2 min 15 après le spawn)
	callfunc "check_monstres", $@spawn43;
	close;

OnTimer6180000:
	callfunc "f_monstres", $@spawn44;
	close;
OnTimer6315000: // pour patienter (2 min 15 après le spawn)
	callfunc "check_monstres", $@spawn44;
	close;

OnTimer6450000:
	callfunc "f_monstres", $@spawn45;
	close;
OnTimer6585000: // pour patienter (2 min 15 après le spawn)
	callfunc "check_monstres", $@spawn45;
	close;

OnTimer6720000:
	callfunc "f_monstres", $@spawn46;
	close;
OnTimer6855000: // pour patienter (2 min 15 après le spawn)
	callfunc "check_monstres", $@spawn46;
	close;

OnTimer6990000:
	mapannounce "guild_vs5.gat","Garde Royal: Bravo, aux survivants ! Ok, vous allez voir maintenant...",0;
	mapannounce "guild_vs5.gat","Garde Royal: 5 minutes entre chaque spawn de monstres différents.",0;
	callfunc "f_monstres", $@spawn47;
	close;
OnTimer7140000: // pour patienter (2 min 30 après le spawn)
	callfunc "check_monstres", $@spawn47;
	close;

OnTimer7290000:
	callfunc "f_monstres", $@spawn48;
	close;
OnTimer7440000: // pour patienter (2 min 30 après le spawn)
	callfunc "check_monstres", $@spawn48;
	close;

OnTimer7590000:
	mapannounce "guild_vs5.gat","Garde Royal: Avant dernier spawn, ensuite, nous vous laissons 10 minutes avant le dernier...",0;
	callfunc "f_monstres", $@spawn49;
	close;
// pas de monstres pour patienter

OnTimer8190000:
	mapannounce "guild_vs5.gat","Garde Royal: Attention, dernier spawn ! Serez-vous les Masters Survivors ?!",0;
	set $@Spawn_flag,2; // $@Spawn_flag: 0: event pas en cours, 1: event en cours, 2: dernier mob spawné
	callfunc "f_monstres", $@spawn50;
	close;

OnMortMob: // à chaque mort de mob
	// un seul survivant
	if ((getmapusers2("guild_vs5.gat") - getmapgms2("guild_vs5.gat",20)) == 1) goto Le_Gagnant;
	// note: pour tuer un monstre, il faut un joueur vivant, donc le test == 0 est inutile

	// ce n'est pas le dernier spawn
	if ($@Spawn_flag != 2) end; // $@Spawn_flag: 0: event pas en cours, 1: event en cours, 2: dernier mob spawné

	set $@Mob_a_tuer, GetMapMobs("guild_vs5.gat");
	if ($@Mob_a_tuer == 500 || $@Mob_a_tuer == 400 || $@Mob_a_tuer == 300 || $@Mob_a_tuer == 200 || $@Mob_a_tuer == 100 || $@Mob_a_tuer == 50 || $@Mob_a_tuer == 10 || $@Mob_a_tuer == 5) goto OnMortMob_sans_quantity;
	mapannounce "guild_vs5.gat","Garde Royal: Encore " + $@Mob_a_tuer +" monstres à tuer.",0;
	goto OnMortMob_sans_quantity;

OnMortMob_sans_quantity:
	set $@Mob_a_tuer, 0;

	// les joueurs sont-il à la fin et ont-il tués tous les monstres.
	if (GetMapMobs("guild_vs5.gat") > 0) end;

	announce "Nous avons plusieur Masters Survivors dans le Spawn Survivor!",0;
	// annonces pour savoir comment quitter la carte
	mapannounce "guild_vs5.gat","Garde Royal: Vous devez deco/reco pour sortir. Merci.",0;
	goto L_OnMortMob_suite;

// 1 gagnant!
Le_Gagnant:
	announce "Nous avons un ultime survivant dans le Spawn Survivor!",0;
	announce "Le Master Survivor est: "+ strcharinfo(0) + ".",0;

	// le prix du gagnant
	announce strcharinfo(0) + " gagne 5.000 zenys.",0;
	set Zeny,Zeny + 5000;

	// annonces pour savoir comment quitter la carte
	mapannounce "guild_vs5.gat","Garde Royal: "+ strcharinfo(0) + " doit deco/reco pour sortir. Merci.",0;
	goto L_OnMortMob_suite;

L_OnMortMob_suite:
	if ((getmapusers3("guild_vs5.gat") - getmapgms3("guild_vs5.gat",20)) < 1) goto L_OnMortMob_suite2; // pas de mort sur la carte
	mapannounce "guild_vs5.gat","Garde Royal: Les autres, vous pouvez deco/reco pour aller à Prontera",0;
	mapannounce "guild_vs5.gat","Garde Royal: ou retourner à votre point de sauvegarde. Merci.",0;
	goto L_OnMortMob_suite2;

L_OnMortMob_suite2:
	stopnpctimer;
	callfunc "close_spawn";
	goto L_stoptimer;

L_stoptimer:
	disablenpc "Surviv";
	end;
}


// NPC extérieur + avant l'event --------------------------------------------


// fonction spawnant des monstres pour faire patienter les joueurs avant l'event
function	script	before_event	{
	set $@spawn_rnd, (rand(51,100) - GetMapMobs("guild_vs5.gat")) / 2; // divisé par 2, car 2 spawns (un normal, un agressif)
	if ($@spawn_rnd < 0) end;

	mapannounce "guild_vs5.gat","Garde Royal: Voici de quoi vous entraîner en attendant...",0;
	areamonster "guild_vs5.gat",0,0,200,200,"Poring",1002,$@spawn_rnd,"";
	areamonsteragro "guild_vs5.gat",0,0,200,200,"Poring carnivore",1002,$@spawn_rnd,"";
	set $@spawn_rnd, 0;
	return;
}


// NPC extérieur (à Prontéra) permettant d'activer l'événement
prontera.gat,174,154,3	script	Spawner	743,{
	if (getgmlevel(0) < 20) goto L_JoueurMenu;
	menu "Menu spécial GM",L_GmMenu,"Menu Joueur",L_JoueurMenu;

L_JoueurMenu:
	mes "[Spawner]";
	mes "Le Spawn Survivor ne peut être activé que par un GM ou de manière automatisée tous les jours à 01h00 et le Samedi à 13h30.";
	mes "Une fois activé, vous avez 10 minutes pour venir ici et joindre la partie.";
	mes "Si vous arrivez en retard, le jeu se déroulera sans vous.";
	next;
	mes "[Spawner]";
	mes "Quelques règles:";
	mes "- Il est interdit de rescussiter un joueur durant le Spawn Survivor.";
	mes "- Pour gagner le Spawn Survivor, il faut être l'unique survivant et tuer un monstre.";
	mes "Au revoir.";
	close;

OnInit:
	disablenpc "warpspawn";
	disablenpc "warprep";
	disablenpc "warprep2";
	disablenpc "warprep3";
	disablenpc "warprep4";
	disablenpc "Surviv";
	killmonsterall "guild_vs5.gat";
	end;

OnSat1330:
	Announce "Garde Royal: Le Spawn Survivor est ouvert.",8;
	enablenpc "Surviv";
	goto L_creawarp;

OnHour01:
	Announce "Garde Royal: Le Spawn Survivor est ouvert.",8;
	enablenpc "Surviv";
	goto L_creawarp;

L_GmMenu:
	if (agitcheck(0) != 0) goto L_Gmwoe;
	if ($@Spawn_flag > 0) goto L_GmArreter; // $@Spawn_flag: 0: event pas en cours, 1: event en cours, 2: dernier mob spawné
	mes "[Spawner]";
	mes "Voulez-vous ^FFBBBBactiver^000000 le Spawn Survivor?";
	menu "Oui",L_GMlauch,"Non",L_GmFin;
	close;

L_Gmwoe:
	mes "[Spawner]";
	mes "Vous ne pouvez pas activer maintenant le Spawn Survivor.";
	mes "En effet, la Guerre de l'Emperium est en cours...";
	mes "Désolé.";
	close;

L_GmArreter:
	mes "[Spawner]";
	mes "Voulez-vous ^FFBBBBarrêter^000000 le Spawn Survivor?";
	menu "Oui",L_GMclose,"Non",L_GmFin;
	close;

L_GMclose:
	if ($@Spawn_flag == 0) goto L_GMdejaclose; // $@Spawn_flag: 0: event pas en cours, 1: event en cours, 2: dernier mob spawné
	close2; // le script continue
	doevent "Surviv::L_stoptimer";//tjs en 1ere ligne
	Announce "Garde Royal: " +strcharinfo(0)+ " a stoppé le Spawn Survivor.",8;
	mapannounce "guild_vs5.gat","Garde Royal: Les joueurs présents dans la salle doivent deco/reco. Merci.",0;
	stopnpctimer;
	callfunc "close_spawn";
	end;

L_GMdejaclose:
	mes "[Spawner]";
	mes "Le Spawn Survivor a été arrêté entre temps.";
	mes "Au revoir.";
	close;

L_GmFin:
	mes "[Spawner]";
	mes "Dans ce cas, au revoir.";
	close;

L_GMlauch:
	if ($@Spawn_flag > 0) goto L_GMdejaLauch; // $@Spawn_flag: 0: event pas en cours, 1: event en cours, 2: dernier mob spawné
	enablenpc "Surviv";
	Announce "Garde Royal: " +strcharinfo(0)+ " a activé le Spawn Survivor.",8;
	close2; // le script continue
	goto L_creawarp;

L_GMdejaLauch:
	mes "[Spawner]";
	mes "Le Spawn Survivor a été activé entre temps.";
	mes "Au revoir.";
	close;

L_creawarp:
	set $@Spawn_flag,1; // $@Spawn_flag: 0: event pas en cours, 1: event en cours, 2: dernier mob spawné
	Announce "Garde Royal: L'évent commence dans 10 minutes.",8;
	Announce "Garde Royal: Rendez-vous dans l'allée au sud de la fontaine de Prontera.",8;
	killmonsterall "guild_vs5.gat";
	enablenpc "warpspawn";
	enablenpc "warprep";
	enablenpc "warprep2";
	enablenpc "warprep3";
	enablenpc "warprep4";
	initnpctimer;
	pvpoff "guild_vs5.gat";
	gvgoff "guild_vs5.gat";
	setmapflag "guild_vs5.gat",mf_nosave;
	setmapflag "guild_vs5.gat",mf_nopvp;
	setmapflag "guild_vs5.gat",mf_nomemo;
	setmapflag "guild_vs5.gat",mf_nobranch;
	setmapflag "guild_vs5.gat",mf_pvp_noguild;
	setmapflag "guild_vs5.gat",mf_nopenalty;
	setmapflag "guild_vs5.gat",mf_nozenypenalty;
	end;

OnTimer60000:
	callfunc "before_event"; // 1 minute, spawn de montres pour patienter.
	close;

OnTimer120000:
	callfunc "before_event"; // 2 minutes, spawn de montres pour patienter.
	close;

OnTimer180000:
	callfunc "before_event"; // 3 minutes, spawn de montres pour patienter.
	close;

OnTimer240000:
	callfunc "before_event"; // 4 minutes, spawn de montres pour patienter.
	close;

OnTimer300000: // 5 minutes après je fais un rappel.
//OnTimer10000: // pour les tests
	Announce "Garde Royal: Plus que 5 min. avant le Spawn Survivor, dépéchez-vous !",8;
	Announce "Garde Royal: Rendez-vous dans l'allée au sud de la fontaine de Prontera.",8;
	callfunc "before_event"; // 5 minutes, spawn de montres pour patienter.
	end;

OnTimer360000:
	callfunc "before_event"; // 6 minutes, spawn de montres pour patienter.
	close;

OnTimer420000:
	callfunc "before_event"; // 7 minutes, spawn de montres pour patienter.
	close;

OnTimer480000:
	callfunc "before_event"; // 8 minutes, spawn de montres pour patienter.
	close;

//OnTimer15000: // pour les tests...
OnTimer540000: // 9 minute après je refais une annonce.
	Announce "Garde Royal: Le Spawn Survivor commence dans 1 minute, dépéchez-vous !",8;
	Announce "Garde Royal: Rendez-vous dans l'allée au sud de la fontaine de Prontera.",8;
	mapannounce "guild_vs5.gat","Garde Royal: Participants, préparez-vous au Spawn Survivor...",0;
	// nettoyage de la place
	if (GetMapMobs("guild_vs5.gat") < 1) end;
	mapannounce "guild_vs5.gat","Garde Royal: *Nettoyage de l'arène...*",0;
	killmonsterall "guild_vs5.gat"; // destruction des monstres pour laisser une minute tranquille
	end;

OnTimer600000: // 10 minutes après je coupe l'accès.
//OnTimer20000: // pour les tests
	if ((getmapusers2("guild_vs5.gat") - getmapgms2("guild_vs5.gat",20)) > 1) goto L_LancementEvent; // au moins 2 joueurs...
	// ici l'arrêt en cas d'un joueur (+ message pour se deco/reco) ou aucun joueur

L_LancementEvent:
	Announce "Garde Royal: Le Spawn Survivor commence, l'accès en est coupé.",8;
	disablenpc "warpspawn";
	disablenpc "warprep";
	disablenpc "warprep2";
	disablenpc "warprep3";
	disablenpc "warprep4";
	doevent "Surviv::OnStart";
	stopnpctimer;
	end;
}
