//===== Freya Script =========================================
//= Kunai "Trader" @ que_ng.gat
//===== By: ==================================================
//= erKURITA
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= Freya
//===== Description: =========================================
//= NPC that trades you a few shurikens + ninja stones for 
//= elemental kunais.
//===== Additional Comments: =================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=	  id of the 2 required items plus the amount. Can trade
//= 	  up to 500 units (5,000 kunai) at once. [erKURITA]
//= 1.1 Converted to Freya [Tsuyuki]
//============================================================

que_ng.gat,72,29,3	script	Kunai Seller	83,{

	mes "[Jin]";
	mes "Hi, I sell elemental enchanted kunai. I'll trade you some elemental stones and one kind of shuriken for a determined elemental Kunai";
	next;
	mes "[Jin]";
	mes "What would you like to trade some? It's free";
	menu "10 Poison Kunai",KS1,"10 Icycle Kunai",KS2,"10 Rough Wind Kunai",KS3,"10 Black Soil Kunai",KS4,"10 Explosion Kunai",KS5,"Nothing at the Moment",KS6;

//Callfunc usage: callfunc "Kunai_Trade",itemreqid1,itemreqct1,itemreqid2,itemreqct2,itemidtrade;
	KS1:
		callfunc "Kunai_Trade",13250,20,7524,1,13259;
		goto L_Bye;

	KS2:
		callfunc "Kunai_Trade",13251,8,7522,2,13255;
		goto L_Bye;

	KS3:
		callfunc "Kunai_Trade",13252,4,7523,2,13257;
		goto L_Bye;

	KS4:
		callfunc "Kunai_Trade",13253,2,7524,1,13256;
		goto L_Bye;

	KS5:
		callfunc "Kunai_Trade",13254,1,7521,2,13258;
		goto L_Bye;

	KS6:
		goto L_Bye;

L_Bye:
	next;
	mes "[Jin]";
	mes "Good bye and hope to see you again then";
	close;
}

function	script	Kunai_Trade	{
	next;
	mes "[Jin]";
	if (MaxWeight*50/100 < Weight) goto KT1;
	goto KT4;

	KT1:
		if (MaxWeight*90/100 < Weight) goto KT2;
		goto KT3;

		KT2:
			mes "Sorry but you have more than 90% weight. Your Kunai might drop. I can't give you anything.";
			return;

	KT3:
		mes "You have 50% or more weight, do you still want to continue?";
		menu "Yes",-,"No, thanks",L_Return;

KT4:
	mes "Would you like to trade my 10 "+getitemname(getarg(4))+" for your "+getarg(1)+" "+getitemname(getarg(0))+" and "+getarg(3)+" "+getitemname(getarg(2))+" ?";
	menu "Yes",KTMenu1,"I'll think about it",KTMenu2;

		KTMenu1:
			next; 
			mes "[Jin]";
			if (countitem(getarg(0)) < getarg(1) || countitem(getarg(2)) < getarg(3)) goto KT5;
			goto KT6;

			KT5:
				mes "Sorry but you're missing one of the required items. Please get them";
				close;

		KT6:
			mes "How many do you want to trade? I can only trade you ^FF0000500^000000 units at once.";
			mes "^00FF001 Unit^000000 ^FF0000=^000000 ^0000FF10 Kunai^000000 of your desire";
			input @trade;
			next;
			mes "[Jin]";
			if (@trade > 500) goto KT7;
			if (@trade < 1) goto KT8;
			if ((countitem(getarg(0)) < @trade*getarg(1)) || (countitem(getarg(2)) < @trade*getarg(3))) goto KT9;
			goto KT10;

			KT7:
				mes "Sorry, but I told you I could only trade you 500 units at once. Try again";
				close;

			KT8:
				mes "Sorry, but 1 unit is the minimun I can trade. Try again please.";
				close;

			KT9:
 				mes "Sorry, but you don't have enough items to trade "+@trade+" unit(s). Get more please";
				close;

		KT10:
			mes "Here you go, enjoy them";
			delitem getarg(0),getarg(1)*@trade;
			delitem getarg(2),getarg(3)*@trade;
			getitem getarg(4),10*@trade;
			next;
			return;

		KTMenu2:
			return;

L_Return:
	return;
}
