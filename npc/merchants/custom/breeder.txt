//=======================================
// 	"Universal Rental NPC"
//----------------------------------------------------------
// 4/17: Script Revised [Harp]. 
// Inclusion of baby classes [harp].
//FIXED checkriding/cart/falcon funcs [Lupus]
//Peco Peco breeder for Knight by Darkchild v1.1
//=======================================
prontera.gat,122,200,1	script	Universal Rental NPC	726,{
	mes "[Universal Renter]";
	mes "Hi, I can help you can rent a Cart, a Falcon, or a Pecopeco.";
	next;
	if (Class==5 || Class==10 || Class==18 || Class==4006 || Class==4011 || Class==4019 || Class==4041 || Class==4028 || Classes==4033) goto L_Cart;
	if (Class==11 || Class==4012 || Class==4034) goto L_Falcon;
	if (Class == 7 || Class == 14 || Class == 4008 || Class == 4015 || Class == 4030 || Class == 4037) goto L_Peco;
	mes "[Universal Renter]";
	mes "Sorry, but it doesn't look like you can rent a falcon, cart, or pecopeco!";
	close;

// Rent a cart/falcon/pecopeco
L_Cart:
	set @rentem$, "Cart";
	if (checkcart(0) > 0) goto L_Already_Have;
	set @skillid, 39;
	goto RentItem;

L_Falcon:
	set @rentem$, "Falcon";
	if (checkfalcon(0) > 0) goto L_Already_Have;
	set @skillid, 127;
	goto RentItem;

L_Peco:
	set @rentem$, "PecoPeco";
	if (checkriding(0)>0) goto L_Already_Have;
	set @skillid, 63;
	goto RentItem;

// Checks if user has needed skill and whether the user has a cart/falcon/peopeco already or not
// If neither is true, then user gets a cart.
RentItem:
	if(getskilllv(@skillid)<1) goto L_Need_Skill;
	mes "[Universal Renter]";	
	mes strcharinfo(0) + ", Would you like to rent a " + @rentem$ + "?";
	next;
	menu "- Rent a " + @rentem$,-,"- No thanks",L_Quit;
	if (@rentem$=="Cart") setcart;
	if (@rentem$=="Falcon") setfalcon;
	if (@rentem$=="PecoPeco") setriding;
	L_Quit2:
		mes "[Universal Renter]";
		mes strcharinfo(0) + ", thank you for using the universal rental service.";
		close;

	L_Quit:
		mes "[Universal Renter]";
		mes strcharinfo(0) + ", please come back when you are ready to rent a " + @rentem$ + ".";
		close;

	L_Need_Skill:
		mes "[Universal Renter]";
		mes "Sorry, but you don't have the required skill to handle a " + @rentem$ + ".";
		close;

	L_Already_Have:
		mes "[Universal Renter]";
		mes "Erm.. it seems you already have a ^996633" + @rentem$ +"^000000. I'll see you again when you actually need me...";
		close;
}
