//--------------------------------------------------------------
//               (c)2004-2007 Freya Team Presents:              
//                 ___   ___    ___   _  _   __                 
//                (  _) (  ,)  (  _) ( \/ ) (  )                
//                (  _)  )  \   ) _)  \  /  /__\                
//                (_)   (_)\_) (___) (__/  (_)(_)               
//                    http://www.ro-freya.net                   
//--------------------------------------------------------------
// Script Title: Marriage	Author: A Lot of People
//--------------------------------------------------------------
// Revision History: v2.7
//--------------------------------------------------------------
//	1.0 First version [AppleGirl/Evera]
//	1.1 Tested and debugged [whocares]
//	1.2 Same sex weddings, now the script (NPCs) tell the players how they must perform exactly [ShadowLady]
//	1.3 Added some code ideas. Thanks to Jbain for help me debbuging [ShadowLady]
//	1.4 Added @outfit variable, to check if you already bought a tux/dress [ShadowLady]
//	1.5 Fixed "part" of the divorce npc, the code is bugged by itself, need both chars to be online yet [ShadowLady]
//	1.6 Fixed some minor bugs. Same-sex is disabled by default now [ShadowLady]
//	1.7 Change $progress variable to $@progress, so it doesn't mess up the ceremony on a server crash [ShadowLady]
//	1.8 Added a Special GM Menu to enable/disable same-sex weddings and reset most variables [ShadowLady]
//	1.9 Added the $progress to $@progress variable change that ShadowLady put in notes, but not in code [Aria]
//	2.0 Complete rewrite [Skotlex]
//	2.1 Removed Divorce System from this script as the official Divorce system is now in [Tsuyuki]
//	2.2 Converted to be fully compatible with jAthena [Tsuyuki]
//	2.3 Corrected NPC coordinates/view ID/facing positions and added cutins [Tsuyuki]
//	2.4 Added base level 45 and under failure check [Tsuyuki]
//	2.5 Added in misc NPCs from Aegis/jAthena [Tsuyuki]
//	2.6 Removed Wedding Veil requirement entirely [Tsuyuki]
//	2.7 Script no longer requires players to have Tuxedo/Wedding Dress equiped, as per Aegis [Tsuyuki]
//	 - Instead the two players must have the items in their inventories
//--------------------------------------------------------------
// Additional Notes: N/A
//--------------------------------------------------------------


//--------------------------------------------------------------
// Configuration Variables:
//--------------------------------------------------------------

-	script	marriage_init	-1,{

OnInit:
	set $@wed_allow, 0;	// If 1, allows same sex marriages.

	// ID of the item that is traded for the Wedding Ring (use 0 to disable):
	set $@wed_ring, 2613;

	set $@wed_groom_reg, 1300000;	// Registration cost for the Groom
	set $@wed_bride_reg, 1200000; // Registration cost for the Bride
	set $@wedding_effect, 1; // On who to display the FX: 0: Priest, 1: Bride, 2: Groom
	end;
}


//--------------------------------------------------------------
// Documentation
//--------------------------------------------------------------

// Other Configuration:
// Line 61,62: Priest location, sprite and name.
// Line 437,438: Registration location, sprite and name. 
// Line 813,814: Divorcing location, sprite and name.

// Variable Notes:
// $wed_progress	Signals that there is a wedding in progress
// $wed_groom$ - Groom's name storage
// $wed_groom_sex - Groom's gender (for same marriage ring giving)
// $wed_bride$ - Bride's name storage
// $wed_bride_sex - Groom's gender (for same marriage ring giving)
// $wed_groom_progress - Notes the progress on the groom's part
// $wed_bride_progress - Notes the progress on the bride's part
// 0: Not registered. 1: Registered. 2: Accepted the partner. 3: Ready to
// Retrieve the ring. 4: Retrieved the ring. 5: All set to be wed. 6: Already
// a couple.
// ceremony.
// $wedding_effect_id - When wedding_effect is enabled, sets the ID of the
// player to show the effect on.


//--------------------------------------------------------------
// Frederick Second (The Priest)
//--------------------------------------------------------------

prt_church.gat,99,125,4	script	Frederick Second	108,{

	set @name$,"Frederick Second";
	
	if (PartnerId) {
		mes "["+@name$+"]";
		mes "You have my blessings, have a wonderful married life.";
		close;
	}
	if ($wed_progress == 0) {
		mes "["+@name$+"]";
		mes "Hello child. How is life treating you?";
		close;
	}
	
	if (strcharinfo(0) == $wed_groom$) {
		callfunc "F_Groom";
		end;
	}
	if (strcharinfo(0) == $wed_bride$) {
		callfunc "F_Bride";
		end;
	}
	mes "["+@name$+"]";
	if ($wed_groom_progress == 0 || $wed_bride_progress == 0) {
		mes "There is a wedding being planned. I would appreciate it if you would not interrupt me.";
		close;
	}
	if ($wed_groom_progress == 6) {
		mes "I am wedding "+$wed_groom$+" and "+$wed_bride$+", and it's already too late to object. Please let me continue.";
		close;
	}
	mes "I am going to wed "+$wed_groom$+" and "+$wed_bride$+", do you have an objection to it?";
	if (select("Sorry, please go on.","Yes, I actually do.") == 2) {
		//Abort
		announce "Ladies and gentlemen, "+strcharinfo(0)+" has an objection to the wedding!",9;
		callfunc "F_wed_end";
		mes "Why should they not be wed?";
		input $@msg$;
		announce strcharinfo(0)+"'s objection is: "+$@msg$,9;
		emotion e_sob;
		mes "I see...";
	} else
		mes "Very well, go sit and enjoy the ceremony.";
	close;

OnInit:
	if ($wed_groom_progress == 6) {
		callfunc "F_wed_end";
	}
	end;
}


//--------------------------------------------------------------
// Sister Mary
//--------------------------------------------------------------

// Registration & Status
prt_church.gat,94,100,4	script	Sister Mary	71,{

	cutin "wedding_marry01",2;
	set @name$,"Mary";
	if (PartnerId) {
		mes "["+@name$+"]";
		mes "Isn't marriage beautiful?";
		close2;
		cutin "wedding_marry01",255;
		end;
	}

	if ($wed_progress) {
		callfunc "F_WedProgress";
		close2;
		cutin "wedding_marry01",255;
		end;
	}
	
	do {
		mes "["+@name$+"]";
		mes "Marriage... is such a beautiful thing.";
		mes "Would you like to get married with someone?";
		next;
		set @menu, select(
			"I'll be single forever!",
			"Explain the principles of marriage.",
			"Explain the marriage procedure.",
			"I want to get married with someone."
		);
		switch (@menu) {
		case 1: //Quit
			mes "["+@name$+"]";
			mes "In that case, enjoy your bachelor's life.";
			close2;
			cutin "wedding_marry01",255;
			end;
		case 2: //Principles
			callfunc "F_Principles";
			break;
		case 3: //Procedure
			callfunc "F_Procedure";
			break;
		case 4: //Register
			callfunc "F_Register";
			break;
		}
	} while (@menu > 1);
	close2;
	cutin "wedding_marry01",255;
	end;

OnInit:
	if ($wed_groom_progress + $wed_bride_progress == 1)
		initnpctimer;
	end;

OnTimer60000:
	//Registration failed.
	if ($wed_bride_progress == 1)
		set $@msg$, $wed_bride$;
	else
		set $@msg$, $wed_groom$;

	announce "Registration timed out. Is it that no one wants to marry "+$@msg$+"..?",9;
	emotion e_hmm;
	
	set $wed_groom$,"";
	set $wed_groom_sex, 0;
	set $wed_bride$,"";
	set $wed_bride_sex, 0;
	set $wed_groom_progress,0;
	set $wed_bride_progress,0;
	set $wed_progress,0;
	stopnpctimer;
	end;
}


//--------------------------------------------------------------
// Marriage Functions
//--------------------------------------------------------------

function	script	F_Groom	{

	if ($wed_bride_progress == 0) {
		mes "["+@name$+"]";
		mes "Looks like your bride has yet to arrive and register.";
		close;
	}

	switch($wed_groom_progress) {
	case 1:
		callfunc "F_AcceptBride";
		break;
	case 2:
		mes "["+@name$+"]";
		mes "I am waiting for your partner to accept you to start the ceremony.";
		close;
	case 3:
		callfunc "F_RetrieveRingM";
		break;
	case 4:
		mes "["+@name$+"]";
		mes "Your partner's wedding ring hasn't been retrieved yet. The ceremony will start as soon as you both have claimed your rings.";
		close;
	case 5:
		mes "["+@name$+"]";
		callfunc "F_StartCeremony";
		break;
	default:
		mes "["+@name$+"]";
		mes "Please don't interrupt me now.";
		close;
	}
	return;
}

function	script	F_Bride	{

	if ($wed_groom_progress == 0) {
		mes "["+@name$+"]";
		mes "Looks like your groom has yet to arrive and register.";
		close;
	}

	switch ($wed_bride_progress) {
	case 1:
		callfunc "F_AcceptGroom";
		break;
	case 2:
		mes "["+@name$+"]";
		mes "I am waiting for your partner to accept you to start the ceremony.";
		close;
	case 3:
		callfunc "F_RetrieveRingF";
		break;
	case 4:
		mes "["+@name$+"]";
		mes "Your partner's wedding ring hasn't been retrieved yet. The ceremony will start as soon as you both have claimed your rings.";
		close;
	case 5:
		mes "["+@name$+"]";
		callfunc "F_StartCeremony";
		break;
	default:
		mes "["+@name$+"]";
		mes "Please don't interrupt me now.";
		close;
	}
	return;
}

function	script	F_AcceptGroom	{

	mes "["+@name$+"]";
	mes $wed_bride$+", "+$wed_groom$+" has requested to be your husband for the rest of your life. Do you accept?";
	next;
	switch(select("I need time to think about it.","No, I don't!","Yes, I do!")) {
	case 1:
		mes "["+@name$+"]";
		mes "You what!?";
		mes "err.. *cough* *cough* very well... come back after you've made up your mind.";
		emotion e_ag;
		close;
	case 2:
		mes "["+@name$+"]";
		mes "!!";
		mes "Ah... err... ehm... okay. You two seem to have some differences to settle first.";
		close2;
		emotion e_omg;
		announce "Ladies and gentlemen, "+$wed_bride$+" has rejected to marry "+$wed_groom$+"!",9;
		callfunc "F_wed_end";
		break;
	case 3:
		set $wed_bride_progress,2;
		if ($wed_groom_progress == 2) {
		callfunc "F_RingsAccepted";
			break;
		}
		emotion e_ok;
		mes "["+@name$+"]";
		mes "After your groom approves, you will be given your rings, the ceremony will begin and you will be officially married.";
		close;
	}
	return;
}

function	script	F_AcceptBride	{

	mes "["+@name$+"]";
	mes $wed_groom$+", "+$wed_bride$+" has requested to be your wife for the rest of your life. Do you accept?";
	next;
	switch(select("I need time to think about it.","No, I don't!","Yes, I do!")) {
	case 1:
		mes "["+@name$+"]";
		mes "You what!?";
		mes "err.. *cough* *cough* very well... come back after you've made up your mind.";
		emotion e_ag;
		close;
	case 2:
		mes "["+@name$+"]";
		mes "!!";
		mes "Ah... err... ehm... okay. You two seem to have some differences to settle first.";
		emotion e_omg;
		close2;
		announce "Ladies and gentlemen, "+$wed_groom$+" has rejected to marry "+$wed_bride$+"!",9;
		callfunc "F_wed_end";
		break;
	case 3:
		set $wed_groom_progress,2;
		if ($wed_bride_progress == 2) {
			callfunc "F_RingsAccepted";
			break;
		}
		emotion e_ok;
		mes "["+@name$+"]";
		mes "After your bride approves, you will be given your rings, the ceremony will begin and you will be officially married.";
		close;
	}
	return;
}

function	script	F_RingsAccepted	{

	mes "["+@name$+"]";
	mes "Now that you both have accepted, the wedding will begin. Please come forth, you and your partner, to retrieve your rings.";
	set $wed_bride_progress,3;
	set $wed_groom_progress,3;
	announce $wed_groom$+" and "+$wed_bride$+"'s wedding ceremony will be held at the church!",8;
	close2;
	emotion e_lv;
	announce "May the groom and bride please step forward and retrieve their rings?",9;
}

function	script	F_RetrieveRingM	{

	mes "["+@name$+"]";
	if ($@wed_ring && countitem($@wed_ring) < 1) {
		mes "What happened to your "+getitemname($@wed_ring)+"? You didn't lose it... did you? We need it to continue with the ceremony!";
		close;
	}
	if ($wed_bride_sex)
		set @item, 2634;	//Groom's wedding ring
	else
		set @item, 2635;	//Bride's wedding ring

	getitem @item,1,0,getcharid(3,$wed_groom$);

	mes "Here's the wedding ring for your bride.";
	if ($@wed_ring) delitem $@wed_ring,1;
	set $wed_groom_progress,4;
	
	if ($wed_bride_progress == 4)
		callfunc "F_StartCeremony";
	else {
		mes "Once your bride retrieves the ring, the ceremony will begin.";
		close;
	}
	return;
}

function	script	F_RetrieveRingF	{

	mes "["+@name$+"]";
	if ($@wed_ring && countitem($@wed_ring) < 1) {
		mes "What happened to your "+getitemname($@wed_ring)+"? You didn't lose it... did you? We need it to continue with the ceremony!";
		close;
	}
	if ($wed_groom_sex)
		set @item, 2634;	//Groom's wedding ring
	else
		set @item, 2635;	//Bride's wedding ring

	getitem @item,1,0,getcharid(3,$wed_bride$);

	mes "Here's the wedding ring for your groom.";
	if ($@wed_ring) delitem $@wed_ring,1;
	set $wed_bride_progress,4;

	if ($wed_groom_progress == 4)
		callfunc "F_StartCeremony";
	else {
		mes "Once your groom retrieves the ring, the ceremony will begin.";
		close;
	}
	return;
}

function	script	F_StartCeremony	{

	mes "I will now start the wedding ceremony, and you will be declared forth husband and wife.";
	set $wed_bride_progress,5;
	set $wed_groom_progress,5;
	set $@msg$,$wed_groom$;
	if (strcharinfo(0) == $wed_groom$)
		set $@msg$,$wed_bride$;
	if (marriage($@msg$) == 0) {
		next;
		mes "["+@name$+"]";
		mes "Where is "+$@msg$+"?? I can't marry you both if one is missing...";
		close;
	}
	set $wed_bride_progress,6;
	set $wed_groom_progress,6;
	close2;
	sleep 1000;
	announce "Ladies and Gentlemen, We will now join in holy matrimony these two lovers.",9;
	sleep 4000;
	announce "Now more than ever, will both of your lives be entwined together as so will be your souls.",9;
	sleep 5000;
	announce "You will both honor and cherish each other through the best and worst of times.",9;
	sleep 5000;
	announce "The safety and well being of your other will now also be your responsibility.",9;
	sleep 5000;
	announce "May in sickness or good health, your love burn bright like no force can extinguish it.",9;
	sleep 5000;
	announce "Those here stand witness to these vows bestowed upon you, you must act accordingly to them.",9;
	sleep 5000;
	announce "Understanding that, we are nothing more but mortals on this earth, but this is our triumph.",9;
	sleep 5000;
	announce "We here will now join these two mortal entities, and create an immortal love.",9;
	sleep 5000;
	announce $wed_groom$+", you have accepted to take "+$wed_bride$+" as your lawfully wedded wife,",9;
	sleep 5000;
	announce "and you, "+$wed_bride$+", have accepted take "+$wed_groom$+" as your lawfully wedded husband.",9;
	sleep 5000;
	announce "And as such, now, by the powers vested in me...",9;
	sleep 5000;
	announce "I pronounce you Husband and Wife, you may kiss the bride and exchange rings.",9;
	if ($wedding_effect_id && isloggedin($wedding_effect_id))
	{
		attachrid($wedding_effect_id);
		wedding;
		detachrid;
	} else
		wedding;
		callfunc "F_wed_end";
	stopnpctimer;
	end;
	return;
}

// Function: Resets wedding variables.
function	script	F_wed_end	{

	set $wed_groom$,"";
	set $wed_groom_sex, 0;
	set $wed_bride$,"";
	set $wed_bride_sex, 0;
	set $wed_groom_progress,0;
	set $wed_bride_progress,0;
	set $wed_progress,0;
	set $wedding_effect_id,0;
}

function	script	F_Register	{

	if ($@wed_allow) { // Role select
		mes "["+@name$+"]";
		mes "Very well, whom would you like to register as?";
		next;
		set @submenu, select("Groom","Bride","Cancel");
	} else if (Sex == Male) { // Groom
		mes "["+@name$+"]";
		mes "Very well, will you register as the Groom?";
		next;
		if (select("Yes","I've changed my mind.")==1)
			set @submenu, 1;
		else
			set @submenu, 3;
	} else { // Bride
		mes "["+@name$+"]";
		mes "Very well, will you register as the Bride?";
		next;
		if (select("Yes","I've changed my mind.")==1)
			set @submenu, 2;
		else
			set @submenu, 3;
	}
	switch (@submenu) {
	case 1: // Groom
		callfunc "F_TryRegister",0;
		set $wed_progress,1;
		mes "["+@name$+"]";
		mes "You are now registered as the groom.";
		mes "Tell your bride to register as soon as possible.";	
		emotion e_hmm;
		initnpctimer;
		close2;
		cutin "wedding_marry01",255;
		end;
	case 2: // Bride
		callfunc "F_TryRegister",1;
		set $wed_progress,1;
		mes "["+@name$+"]";
		mes "You are now registered as the bride.";
		mes "Tell your groom to register as soon as possible.";
		emotion e_hmm;
		initnpctimer;
		close2;
		cutin "wedding_marry01",255;
		end;
	default: // Cancel
		mes "["+@name$+"]";
		mes "Come back when you are ready.";
		close2;
		cutin "wedding_marry01",255;
		end;
	}
	return;
}

function	script	F_WedProgress	{

	if (strcharinfo(0) == $wed_groom$) {
		mes "["+@name$+"]";
		if ($wed_bride_progress > 0)
			mes "The Priest will handle the rest of the ceremony.";
		else
			mes "Tell your bride to register, what is taking so long? Time is running out.";
		close2;
		cutin "wedding_marry01",255;
		end;
	}
	if (strcharinfo(0) == $wed_bride$) {
		mes "["+@name$+"]";
		if ($wed_groom_progress > 0) 
			mes "The Priest will handle the rest of the ceremony.";
		else
			mes "Tell your groom to register, what is taking so long? Time is running out.";
		close2;
		cutin "wedding_marry01",255;
		end;
	}
	if (($wed_groom_progress == 0) && (Sex == Male || $@wed_allow == 1)) {
		mes "["+@name$+"]";
		mes $wed_bride$+" is waiting for the groom to register. Are you the one who came to register as groom?";
		next;
		if (select("Yes, I am.","Sorry, you got the wrong person.") == 1) {
			callfunc "F_TryRegister",0;
			stopnpctimer;
			set $wed_groom_progress,1;
			mes "["+@name$+"]";
			mes "Very well, now go to the Priest to reaffirm your vows and the ceremony will begin."; 
			emotion e_no1;
			close2;
			cutin "wedding_marry01",255;
			announce "Registration finished. "+$wed_groom$+" and "+$wed_bride$+", please reaffirm your vows with the Priest.",9;
			emotion e_no1;
			end;
		} else {
			mes "["+@name$+"]";
			mes "I see. Sorry to have bothered you then.";
			close2;
			cutin "wedding_marry01",255;
			end;
		}

	}
	if (($wed_bride_progress == 0) && (Sex == Female || $@wed_allow == 1)) {
		mes "["+@name$+"]";
		mes $wed_groom$+" is waiting for the bride to register. Are you the one who came to register as the bride?";
		next;
		if(select("Yes, I am.","Sorry, you got the wrong person.") == 1) {
			callfunc "F_TryRegister",1;
			stopnpctimer;
			mes "["+@name$+"]";
			mes "Very well, now go to the Priest to reaffirm your vows and the ceremony will begin."; 
			emotion e_no1;
			close2;
			cutin "wedding_marry01",255;
			announce "Registration finished. "+$wed_groom$+" and "+$wed_bride$+", please reaffirm your vows with the Priest.",9;
			emotion e_no1;
			end;
		} else {
			mes "["+@name$+"]";
			mes "I see. Sorry to have bothered you then.";
			close2;
			cutin "wedding_marry01",255;
			end;
		}
	}
	mes "["+@name$+"]";
	mes "There is a wedding in progress.";
	mes "Would you like to know the progress of said wedding?";
	next;
	if (select("Yes","No") != 1) {
		mes "["+@name$+"]";
		mes "Enjoy the wedding.";
		close2;
		cutin "wedding_marry01",255;
		end;
	}
	//Display Progress
	mes "["+@name$+"]";
	switch ($wed_groom_progress) {
	case 0:
		mes "The groom has not registered yet.";
		break;
	case 1:
		mes "The groom, "+$wed_groom$+", has yet to accept the bride.";
		break;
	case 2:
		mes "The groom, "+$wed_groom$+", is waiting for the bride's acceptance.";
		break;
	case 3:
		mes "The groom, "+$wed_groom$+", has yet to retrieve the ring.";
		break;
	case 4:
		mes "The groom, "+$wed_groom$+", is waiting for the bride to retrieve the ring.";
		break;
	}
	switch ($wed_bride_progress) {
	case 0:
		mes "The bride has not registered yet.";
		break;
	case 1:
		mes "The bride, "+$wed_bride$+", has yet to confirm the groom.";
		break;
	case 2:
		mes "The bride, "+$wed_bride$+", is waiting for the groom's acceptance.";
		break;
	case 3:
		mes "The bride, "+$wed_bride$+", has yet to retrieve the ring.";
		break;
	case 4:
		mes "The bride, "+$wed_bride$+", is waiting for the groom to retrieve the ring.";	
		break;
	case 5:
		mes "We are just waiting for both "+$wed_groom$+" and "+$wed_bride$+" to be together to marry them.";
		break;
	case 6:
		mes $wed_groom$+" and "+$wed_bride$+"'s wedding ceremony is already well on it's way.";
		break;
	}
	mes "Enjoy the remaining of the wedding.";
	close2;
	cutin "wedding_marry01",255;
	end;
	return;
}

// Function F_TryRegister (int bride)
function	script	F_TryRegister	{

	set @bride, getarg(0);
	set @type$, "groom";
	if (@bride)
		set @type$, "bride";
		
	mes "["+@name$+"]";
	mes "Before registering as "+@type$+", let me check if you meet all the requirements...";
	next;
	if (Upper == 2) {
		mes "["+@name$+"]";
		mes "Oh dear, you are too young to be thinking of marriage!";
		emotion e_gasp;
		close2;
		cutin "wedding_marry01",255;
		end;
	}
	if (BaseLevel < 45) {
		mes "["+@name$+"]";
		mes "I'm sorry, but you must be";
		mes "at least base level 45 or higher";
		mes "to get married. Please come back";
		mes "when you've become stronger.";
		close2;
		cutin "wedding_marry02",255;
		end;
	}
	if (Sex == Male)
		set @item,7170;
	else
		set @item,2338;
	if ((Sex == Female && countitem(@item) < 1) || (Sex == Male && countitem(@item) < 1)) {
		mes "["+@name$+"]";
		if (Sex == Female) {
			mes "I'm sorry, but you must provide us with a Wedding Dress";
			mes "in your inventory for the marriage process.";
		}
		else {
			mes "I'm sorry, but you must provide us with a Tuxedo";
			mes "in your inventory for the marriage process.";
		}
		mes "Please get one and come back.";
		close2;
		cutin "wedding_marry02",255;
		end;
	}
	if ($@wed_ring && countitem($@wed_ring) < 1) {
		mes "["+@name$+"]";
		mes "Where's the ring? You need a "+getitemname($@wed_ring)+" for the ring exchange, dear.";
		close2;
		cutin "wedding_marry01",255;
		end;
	}
	if (@bride)
		set @cost, $@wed_bride_reg;
	else 
		set @cost, $@wed_groom_reg;

	if (Zeny < @cost) {
		mes "["+@name$+"]";
		mes "I am sorry, but you don't have enough to pay for the registration fee.";
		mes "Come back once you have collected "+@cost+"z."; 
		close2;
		cutin "wedding_marry01",255;
		end;
	}
	set Zeny,Zeny-@cost;
	if (Sex == Female) {
		delitem 2338,1;
	}
	else {
		delitem 7170,1;
	}
	sc_start SC_Wedding,3600000000,1; //Start Wedding Effect (SC_WEDDING)
	if (@bride) {
		set $wed_bride_progress,1;
		set $wed_bride$,strcharinfo(0);
		set $wed_bride_sex, sex;
		if ($@wedding_effect == 1) //Store account id for effect.
			set $wedding_effect_id, getcharid(3);
	} else {
		set $wed_groom_progress,1;
		set $wed_groom$,strcharinfo(0);
		set $wed_groom_sex, sex;
		if ($@wedding_effect == 2) //Store account id for effect.
			set $wedding_effect_id, getcharid(3);
	}
	return;
}

// Explain wedding principles...
function	script	F_Principles	{

	mes "["+@name$+"]";
	mes "Weddings are performed by our local Priest with the intent of promoting love and peace among the loving couples.";
	next;
	mes "["+@name$+"]";
	mes "The proposal must be done with prudence and courtesy, once the wedlocks have been made, they cannot be undone.";
	next;
	mes "["+@name$+"]";
	mes "The two who have been joined by marriage must remain together forever until the day death do them apart.";
	next;
	mes "["+@name$+"]";
	if ($@wed_allow == 1)
		mes "Although normally only males can wed females (and viceversa), our local Priest is more open-minded than that and he permits all pairings regardless of gender.";
	else
		mes "Males may only wed with females, and females only with males, the church will not consent any other kind of partnerships.";
	next;
	mes "["+@name$+"]";
	mes "If there is a significant other with whom you want to spend the rest of your life with, then don't be shy to propose.";
	next;
	mes "["+@name$+"]";
   mes "I wish for many blessings upon couples who wish to live happily ever after...";
	next;
	return;
}

//Explain the wedding procedure...
function	script	F_Procedure	{

	mes "["+@name$+"]";
	mes "First of all, both groom and bride must register with me.";
	next;
	if ($@wed_allow == 1) {
		mes "["+@name$+"]";
		mes "The registration requirements are:";
		mes "- Males must have a "+getitemname(7170)+". in their inventory.";
		mes "- Females must have a "+getitemname(2338)+". in their inventory";
		if ($@wed_ring) mes "- Both must own a "+getitemname($@wed_ring)+" each.";
		if ($@wed_groom_reg > 0) mes "- The registration fee for the groom is "+$@wed_groom_reg+"z.";
		if ($@wed_bride_reg > 0) mes "- The registration fee for the bride is "+$@wed_bride_reg+"z.";
	} else {
		mes "["+@name$+"]";
		mes "The registration requirements for the groom are:";
		mes "- To have a "+getitemname(7170)+". in their inventory";
		if ($@wed_ring) mes "- To own a "+getitemname($@wed_ring)+".";
		if ($@wed_groom_reg > 0) mes "- Pay a Registration fee of "+$@wed_groom_reg+"z.";
		next;
		mes "["+@name$+"]";
		mes "The registration requirements for the bride are:";
		mes "- To have a "+getitemname(2338)+". in their inventory.";
		if ($@wed_ring) mes "- To own a "+getitemname($@wed_ring)+".";
		if ($@wed_bride_reg > 0) mes "- Pay a Registration fee of "+$@wed_bride_reg+"z.";
	}
	next;
	mes "["+@name$+"]";
	mes "I shouldn't need to mention this, but adopted kids are too young to get married.";
	mes "Both groom and bride must register within a minute of each other, or the wedding will be cancelled. So be sure you both are ready and meet the registration requirements beforehand.";
	next;
	mes "["+@name$+"]";
	mes "After both have registered with me, you have to go pledge your vows to the Priest and accept your registered partner. If for some reason you reject your registered partner, the wedding will be cancelled...";
	next;
	mes "["+@name$+"]";
	mes "If you both accept each other, then the wedding has been decided and the ceremony will begin.";
	if ($@wed_ring) {
		mes "But first, you need to get your rings ready.";
		next;
		mes "["+@name$+"]";
		mes "Talk to the priest once more, and he will exchange your "+getitemname($@wed_ring)+" for a wedding ring. After you both have claimed the rings for exchanging, the ceremony will begin.";
	}
	next;
	mes "["+@name$+"]";
	mes "If there are various couples who desire to marry, you should keep in order, for the Priest can only handle one wedding at a time.";
	next;
	return;
}


//--------------------------------------------------------------
// Miscellaneous Marriage NPCs
//--------------------------------------------------------------

prt_church.gat,97,102,0	script	Single Army#Prontera	105,{

	mes "[Single Army]";
	mes "You have to refine things yourself to produce great items!";
	close;
}

prt_church.gat,98,102,0	script	Single Army#Geffen	705,{

	mes "[Single Army]";
	mes "It is a waste to form a party in a dungeon!";
	close;
}

prt_church.gat,99,102,0	script	Single Army#Morroc	707,{

	mes "[Single Army]";
	mes "I can become a heart-shaped NPC!";
	close;
}

prt_church.gat,100,102,0	script	Single Army#Payon	708,{

	mes "[Single Army]";
	mes "I train by myself from my creation to my job change!";
	close;
}

prt_church.gat,101,102,0	script	Single Army#Amatsu	767,{

	mes "[Single Army]";
	mes "During Christmas, I'll cut grass outside the house!";
	close;
}

prt_church.gat,102,102,0	script	Single Army#Kunlun	780,{

	mes "[Single Army]";
	mes "...We are the invincible Single Army!";
	close;
}
