//--------------------------------------------------------------
//               (c)2004-2007 Aurora Team Presents:             
//                  ___                                         
//                 /   |_   _ _ __ ___  _ __ __ _               
//                / /| | | | | '__/ _ \| '__/ _` |              
//               / ___ | |_| | | | (_) | | | (_| |              
//              /_/  |_|\____|_|  \___/|_|  \__,_|              
//                 http://aurora.deltaanime.net                 
//--------------------------------------------------------------
// Script Title: Rentals	Author: L0ne_W0lf/Tsuyuki
//--------------------------------------------------------------
// Revision History: v1.1
//--------------------------------------------------------------
//	1.0 First version, converted from Aegis [L0ne_W0lf]
//	1.1 Merged in the Super Novice Cart Renter, converted from Aegis [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: * Official-certified / Aegis-certified *
//--------------------------------------------------------------


// Knight Peco Peco Breeder
prontera.gat,55,350,5	script	Peco Peco Breeder	105,{

	if (Upper==0) set @price,2500; // Normal Peco - Default price
	if (Upper==1) set @price,2500; // Armored Peco
	if (Upper==2) set @price,2500; // Baby Peco

	mes "[Peco Peco Breeder]";
	if (BaseJob == Job_Knight) {
		mes "Welcome.";
		mes "Honorable Knight,";
		mes "would you like to rent";
		mes "a Peco Peco? The rental";
		mes "fee is "+@price+" zeny.";
		next;
		if (select("Rent Peco Peco","Cancel") == 1) {
			if (zeny < @price) {
				mes "[Peco Peco Breeder]";
				mes "You do not";
				mes "have enough zeny.";
				mes "Are you...";
				mes "bankrupt?";
				close;
			}
			else if(getskilllv(63) == 0) {
				mes "[Peco Peco Breeder]";
				mes "I'm sorry, but you're";
				mes "not eligible for this";
				mes "service. Please go learn";
				mes "the Peco Peco Ride skill first.";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "You're already";
				mes "mounted on a";
				mes "Peco Peco.";
				close;
			}
			set zeny,zeny-@price;
			setriding;
		}

		mes "[Peco Peco Breeder]";
		mes "I see.";
		mes "Well then,";
		mes "have a good day.";
		close;
	}
	mes "I'm sorry, but these";
	mes "Peco Pecos are only";
	mes "available for Knights";
	mes "and Lord Knights.";
	close;
}

// Crusader Grand Peco Peco Breeder
prontera.gat,232,318,3	script	Peco Peco Breeder	105,{

	if (Upper==0) set @price,3500; // Normal Peco - Default price
	if (Upper==1) set @price,3500; // Armored Peco
	if (Upper==2) set @price,3500; // Baby Peco

	mes "[Peco Peco Breeder]";
	if (BaseJob == Job_Crusader) {
		if (Upper == 0)
			mes "Welcome, Crusader.";
		else 
			mes "Welcome, Paladin.";
		mes "We have a special";
		mes "Peco Peco prepared";
		mes "for you. To rent one";
		mes "will cost "+@price+" zeny.";
		next;
		if (select("Rent a Peco Peco","Quit") ==1) {
			if (zeny < @price) {
				mes "[Peco Peco Breeder]";
				mes "You do not";
				mes "have enough zeny.";
				mes "If you would like";
				mes "a Peco Peco please";
				mes "bring "+@price+" zeny...";
				close;
			}

			else if(getskilllv(63) == 0) {
				mes "[Peco Peco Breeder]";
				mes "You must first learn";
				mes "to ride a Peco Peco before";
				mes "I can rent one to you.";
				close;
			}
			else if(checkriding()) {
				mes "[Peco Peco Breeder]";
				mes "You are already";
				mes "mounted on a Peco Peco.";
				close;
			}
			set zeny,zeny-@price;
			setriding;
		}
		mes "[Peco Peco Breeder]";
		mes "See you around.";
		close;
	}
	mes "What can I do for you?";
	mes "Please be aware that";
	mes "this Peco Peco rental";
	mes "service is strictly for";
	mes "Crusaders and Paladins.";
	close;
}

// Hunter Falcon Breeder
hu_in01.gat,381,304,5	script	Falcon Breeder	105,{

	if (Upper==0) set @price,2500; // Normal Falcon - Default price
	if (Upper==1) set @price,2500; // Scarf Falcon
	if (Upper==2) set @price,2500; // Baby Falcon

	mes "[Falcon Breeder]";
	if (BaseJob == Job_Hunter) {
		mes "Do you need a Falcon?";
		mes "You can rent your own";
		mes "trusty bird of prey for a";
		mes "fee of just "+@price+" zeny~";
		next;
		if (select("Rent Falcon","Cancel") == 1) {
			if (zeny < @price) {
				mes "[Falcon Breeder]";
				mes "What is this?";
				mes "You don't have";
				mes "enough zeny?!";
				mes "You better start";
				mes "hunting money";
				mes "instead of monsters~";
				close;
			}
			else if(getskilllv(127) == 0) {
				mes "[Falcon Breeder]";
				mes "Gosh~";
				mes "Go learn how to";
				mes "manage a Falcon";
				mes "first! I can't rent one";
				mes "to you if you can't";
				mes "handle it, you know.";
				close;
			}
			else if(checkfalcon()) {
				mes "[Falcon Breeder]";
				mes "Um...";
				mes "You already have";
				mes "a Falcon. It's right";
				mes "there, can't you see it?";
				close;
			}
			set zeny,zeny-@price;
			setfalcon;
			close;
		}
		mes "[Falcon Breeder]";
		mes "W-wait, where're";
		mes "you goin'? These";
		mes "Falcons are top notch,";
		mes "I guarantee it! C'mon, yo~";
		close;
	}
	mes "Young fool!";
	mes "Falcons can only";
	mes "be used by Hunters";
	mes "and Snipers, capish?";
	mes "...Heh heh, jealous?";
	close;
}

// Super Novice Cart Renter
aldebaran.gat,54,238,5	script	Kafra Employee	117,{

	if (Basejob == Job_Super_Novice) {
		mes "[Kafra Employee]";
		if (Sex == Male) {
			mes "Hello, Mister Super Novice~";
		} else {
			mes "Hello, Super Novice, Ma'am.";
		}
		mes "You must have been really";
		mes "dissapointed that the other";
		mes "Kafra Employees wouldn't let";
		mes "you rent a cart from them.";
		mes "But don't you worry now...";
		next;
		mes "[Kafra Employee]";
		mes "I'm here to support you guys";
		mes "by providing carts...";
		next;
		mes "[Kafra Employee]";
		mes "^3355FF*whispers*^000000";
		mes "^555555I am not supposed to do this";
		mes "because it's against our";
		mes "company policy. But I felt";
		mes "really sorry for Super Novices";
		mes "...so here I am.^000000";
		next;
		mes "[Kafra Employee]";
		mes "Anyway, would you like to rent a cart? The service fee is 1,900 zeny.";
		next;
		switch(select("Rent a Cart.","Cancel.")) {
		case 1:
			mes "[Kafra Employee]";
			mes "Oh, here's the thing...";
			mes "Have you learned the 'Push Cart'";
			mes "skill? I can just rent you a";
			mes "cart, but if you haven't";
			mes "learned to push it, you'll";
			mes "just be wasting your zeny.";
			next;
			mes "[Kafra Employee]";
			mes "So make sure that you have";
			mes "the 'Push Cart' skill already.";
			next;
			switch(select("Rent a Cart.","Cancel.")) {
			case 1:
				if (Cart) {
					mes "[Kafra Employee]";
					mes "Oh, you've already equipped a cart.";
					close;
				}
				if (Zeny > 1899) {
					set KAFRA_PIT,KAFRA_PIT+10;
					set Zeny,Zeny-1900;
					setcart;
					mes "[Kafra Employee]";
					mes "Thank you for using my service.";
					mes "Although what I am doing might";
					mes "not be legitimate to other";
					mes "Kafra Employees, I strongly";
					mes "believe I am doing what's right for the customers.";
					close;
				} else {
					mes "[Kafra Employee]";
					mes "I am sorry, but you do not have enough zeny with you. The service fee is 1,900 zeny.";
					close;
				}
			case 2:
				mes "[Kafra Employee]";
				mes "Please come again.";
				mes "Thank you for using my services.";
				close;
		}
		case 2:
			mes "[Kafra Employee]";
			mes "Please come again.";
			mes "Thank you for using my services.";
			close;
		}
	} else {
		mes "[Kafra Employee]";
		mes "Good da--Oops...!";
		mes "I don't think I can provide you";
		mes "with the services you want...";
		mes "Please go talk to another";
		mes "Kafra employee. I apologize";
		mes "for such inconvenience...";
		close;
	}
}
