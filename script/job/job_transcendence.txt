//--------------------------------------------------------------
//               (c)2004-2007 Aurora Team Presents:             
//                  ___                                         
//                 /   |_   _ _ __ ___  _ __ __ _               
//                / /| | | | | '__/ _ \| '__/ _` |              
//               / ___ | |_| | | | (_) | | | (_| |              
//              /_/  |_|\____|_|  \___/|_|  \__,_|              
//                 http://aurora.deltaanime.net                 
//--------------------------------------------------------------
// Script Title: Transcendence Job Change Quest	Author: Blaze/Tsuyuki
//--------------------------------------------------------------
// Revision History: v1.5
//--------------------------------------------------------------
//	1.0 First version [Blaze]
//	1.1 Translated from Japanese into English based on Aegis euRO scripts [Tsuyuki]
//	1.2 Made some small modifications for Aurora compatibility [Tsuyuki]
//	1.3 Added 1st class job change function (For use with original job quests) [Tsuyuki]
//	1.4 Replaced some dialogue with Aegis iRO 10.3 dialogue (Work in progress) [Tsuyuki]
//	1.5 Officialized a few things, now 100% matches Aegis [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: * Official-certified / Aegis-certified *
//--------------------------------------------------------------


//--------------------------------------------------------------
// Metheus Sylphe
//--------------------------------------------------------------

yuno_in02.gat,88,164,5	script	Metheus Sylphe#Library	742,{

	if (TRANSMIGRATE == 0) {
		if (BaseLevel < 99 || JobLevel < 50 || Basejob < 7 || Basejob > 21 || Upper != 0) {
			mes "[Metheus Sylphe]";
			mes "Welcome to the Library of the Schweicherbil Magic Academy.";
			mes "Here, we have a countless number of books. Please take your time and look around.";
			close;
		}
		mes "[Metheus Sylphe]";
		mes "Welcome to the Library of the Schweicherbil Magic Academy.";
		mes "I assume you have come here";
		mes "to read the 'Book of Ymir.'";
		next;
		mes "[Metheus Sylphe]";
		mes "Unfortunately, the original copy of the book has been damaged over time. We currently only allow the public to view a copy of the book.";
		next;
		mes "[Metheus Sylphe]";
		mes "Also, in order to preserve the original 'Book of Ymir,' we have decided to accept donations from people who wish to read the copy we have provided.";
		next;
		mes "[Metheus Sylphe]";
		mes "The suggested";
		mes "donation amount is";
		mes "1,285,000 zeny.";
		next;
		if (select("Donate.","Cancel.")==2) {
			mes "[Metheus Sylphe]";
			mes "Take your time, and";
			mes "enjoy your travels.";
			close;
		}
		if (Zeny < 1285000) {
			mes "[Metheus Sylphe]";
			mes "Unfortunately, you don't seem to possess enough zeny at the moment. Please check your funds and come back again.";
			close;
		}
		set Zeny,Zeny-1285000;
		set TRANSMIGRATE,1;
		mes "[Metheus Sylphe]";
		mes "Thank you, your donation will be used for a good cause. You may";
		mes "now go in and read the book.";
		close;
	}
	mes "[Metheus Sylphe]";
	mes "Once again, thank you for your generous donation. Feel free to read a carbon copy of the 'Book of Ymir' at your leisure.";
	close;
}


//--------------------------------------------------------------
// Book of Ymir
//--------------------------------------------------------------

yuno_in02.gat,93,207,1	script	Book of Ymir	111,{

	if (Upper == 1) {
		mes "[The Book of Ymir]";
		mes "...The entrance to the Hall of Honor is open to anyone who has moved forward into their next life. It is there to help heroes decide what they want to do, and can lead them to anywhere in this world.";
		next;
		mes "[The Book of Ymir]";
		mes "In the Hall of Honor, everything is prepared for heroes. It is rumored that any wish that cannot be fulfilled in our reality can be realized in the Hall of Honor.";
		next;
		if (select("Stop reading.","Continue reading.")==1) {
			mes "[The Book of Ymir]";
			mes "...";
			close;
		}
		mes "[The Book of Ymir]";
		mes "There is a forgotten path which leads to the Hall of Honor, the closest place to the heavens. The ordinary will never discover this place...";
		close2;
		warp "valkyrie.gat",48,8;
		end;
	}
	if (TRANSMIGRATE == 0) {
		mes "[The Book of Ymir]";
		mes "...";
		close;
	}
	mes "[The Book of Ymir]";
	mes "...Therefore, ancient heroes were";
	mes "always in anguish, knowing that";
	mes "eventually, they were mortal and";
	mes "would pass from this realm...";
	next;
	mes "[The Book of Ymir]";
	mes "There were no documents,";
	mes "songs, or remaining folklore that had any information on life after death. However, I recently uncovered an old scroll";
	mes "about Valkyrie...";
	next;
	mes "[The Book of Ymir]";
	mes "Valkyrie...";
	mes "The legendary";
	mes "guardian angel.";
	mes "Angel of Ragnarok.";
	next;
	mes "[The Book of Ymir]";
	mes "Adventurers of great strength";
	mes "and bravery will be lead by";
	mes "Valkyrie to Valhalla, the Hall";
	mes "of Honor. There, they will be";
	mes "given a new life.";
	next;
	mes "[The Book of Ymir]";
	mes "Reborn, they will live again as";
	mes "even greater heroes that will";
	mes "brighten the world. Bodies that";
	mes "were exhausted will be filled";
	mes "with energy...";
	next;
	mes "[The Book of Ymir]";
	mes "And their souls will be given abilities with the heart of Ymir. However, the heart of Ymir was totally destroyed and scattered all over the world after the battle for Rune-Midgarts.";
	next;
	mes "[The Book of Ymir]";
	mes "I have found a small amount of";
	mes "Ymir heart pieces over a long";
	mes "long period of time. But I can't";
	mes "confirm if the story of Valkyrie";
	mes "and Valhalla is true just";
	mes "through scientific tests.";
	next;
	mes "[The Book of Ymir]";
	mes "So, I am leaving this record in hope that someone in the future";
	mes "can confirm that Valkyrie and Valhalla actually exist...";
	next;
	set TRANSMIGRATE,2;
	mes "[The book of Ymir]";
	mes "Let the heroes live new lives";
	mes "so they can protect the world";
	mes "from danger. And then...";
	close;
}


//--------------------------------------------------------------
// Heart of Ymir
//--------------------------------------------------------------

yuno_in05.gat,49,43,1	script	Heart of Ymir	111,{

	if (TRANSMIGRATE == 2) {
		warp "valkyrie.gat",48,8;
	}
	end;
}


//--------------------------------------------------------------
// Valkyrie
//--------------------------------------------------------------

valkyrie.gat,48,86,4	script	Valkyrie	811,{

	if (Upper == 1) {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "Please make";
		mes "yourself comfortable";
		mes "while you are here.";
		mes "Honor to the warriors!";
		close;
	}
	if (BaseLevel < 99 || JobLevel < 50 || Basejob < 7 || Basejob > 21 || Upper != 0) {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "Unfortunately, you have not yet been invited here. I ask you to leave immediately. Honor to the warriors!";
		close2;
		warp "yuno_in02.gat",93,205;
		end;
	}
	mes "[Valkyrie]";
	mes "Welcome";
	mes "to Valhalla,";
	mes "the Hall of Honor.";
	next;
	mes "[Valkyrie]";
	mes "You will now end";
	mes "your present life and";
	mes "begin an entirely new life.";
	mes "Honor to the warriors!";
	next;
	if (Weight || Zeny || checkcart() || checkfalcon() || checkriding()) {
		mes "[Valkyrie]";
		mes "There are a few things you must";
		mes "do before we start. You must";
		mes "first empty your mind and body.";
		mes "Honor comes when you abandon";
		mes "all your selfish desires...";
		next;
		mes "[Valkyrie]";
		mes "You cannot take anything with you to the next life. Your items, zeny, pets and Pushcart all have to be left behind.";
		next;
		mes "[Valkyrie]";
		mes "When you are ready";
		mes "please return to me,";
		mes "brave adventurer.";
		close2;
		warp "yuno_in02.gat",93,205;
		end;
	}
	mes "[Valkyrie]";
	mes "I see you've already";
	mes "released yourself from";
	mes "all worldy attachments,";
	mes ""+strcharinfo(0)+".";
	next;
	mes "[Valkyrie]";
	mes "That's an admirable attitude for an adventurer such as yourself. Honor comes when you abandon all personal desires for the sake of mankind.";
	next;
	if (SkillPoint) {
		mes "[Valkyrie]";
		mes "Hmm... I sense that you have";
		mes "some lingering attachment or";
		mes "unfinished business in your";
		mes "current life. Take care of that,";
		mes "and bring closure to your present life.";
		close2;
		warp "yuno_in02.gat",93,205;
		end;
	}
	mes "[Valkyrie]";
	mes "Now, let me remove all";
	mes "of your present memories...";
	mes "However, you will be able to";
	mes "remember the most honorable";
	mes "moments of this life.";
	next;
	mes "[Valkyrie]";
	mes "With one,";
	mes "I will ask the";
	mes "goddess Urd to remove";
	mes "all of your present";
	mes "memories.";
	next;
	mes "[Valkyrie]";
	mes "With two,";
	mes "I will ask the";
	mes "goddess Verdani to keep";
	mes "and record the most honorable moments of your present life.";
	next;
	mes "[Valkyrie]";
	mes "With three,";
	mes "I will ask the";
	mes "goddess Skuld to";
	mes "guide you to your";
	mes "next life.";
	next;
	mes "[Valkyrie]";
	mes "One...";
	next;
	mes "[Valkyrie]";
	mes "One...";
	mes "Two......";
	next;
	mes "[Valkyrie]";
	mes "One...";
	mes "Two......";
	mes "And Three.";
	set OLD_CLASS,Basejob;
	unequip;
	jobchange 0,1;
	set BaseLevel,1;
	resetstatus;
	resetskill;
	setoption 0x0000;
	skill 143,1,0;
	skill 142,1,0;
	set SkillPoint,0;
	set StatusPoint,100;
	set TRANSMIGRATE,0;
	callfunc "F_ClearJobVariables";
	next;
	mes "[Valkyrie]";
	mes "Congratulations.";
	mes "You are now reborn";
	mes "into a brand new life.";
	mes "Please take these small gifts";
	mes "in preparation for your new adventures.";
	getitem 1202,1;
	getitem 2302,1;
	next;
	mes "[Valkyrie]";
	mes "I wish that the release the goddess Urd has granted you proves to be a blessing. I hope that the memories Verdandi has recorded will always honor you.";
	next;
	mes "[Valkyrie]";
	mes "And I pray that the new life to which the goddess Skuld will guide you will be even more honorable than your last.";
	close2;
	switch(OLD_CLASS) {
		case 7:
		case 14:
			warp "izlude.gat",94,103;
			break;
		case 8:
		case 15:
			warp "prontera.gat",273,354;
			break;
		case 9:
		case 16:
			warp "geffen.gat",120,60;
			break;
		case 10:
		case 18:
			warp "alberta.gat",116,57;
			break;
		case 11:
		case 19:
		case 20:
			warp "payon.gat",69,100;
			break;
		case 12:
		case 17:
			warp "morocc.gat",154,50;
			break;
		default:
			warp "yuno_in02.gat",93,205;
			break;
	}
	end;
}


//--------------------------------------------------------------
// Teleporter
//--------------------------------------------------------------

valkyrie.gat,44,33,5	script	Teleporter	124,{

	if (Upper != 1) {
		set '@karma_d,rand(1,10);
		if ('@karma_d > 4) {
			mes "[Teleporter]";
			mes "Congratulations.";
			mes "Honors to the warriors!";
		}
		else {
			mes "[Teleporter]";
			mes "Please refrain";
			mes "from touching any";
			mes "of the exhibitions.";
			mes "..........";
		}
		close;
	}
	mes "[Teleporter]";
	mes "Honorable one,";
	mes "which place do you wish to go?";
	next;
	switch (select("Prontera","Morroc","Payon","Geffen","Alberta","Izlude","Al De Baran","Comodo","Juno")) {
		case 1: savepoint "prontera.gat",116,72; 	break;
		case 2: savepoint "morocc.gat",156,46; 		break;
		case 3: savepoint "payon.gat",69,100; 		break;
		case 4: savepoint "geffen.gat",120,39; 		break;
		case 5: savepoint "alberta.gat",117,56; 	break;
		case 6: savepoint "izlude.gat",94,103; 		break;
		case 7: savepoint "aldebaran.gat",91,105; 	break;
		case 8: savepoint "comodo.gat",209,143; 	break;
		case 9: savepoint "yuno.gat",328,101; 		break;
	}
	mes "[Teleporter]";
	mes "Have a nice trip.";
	close2;
	warp "SavePoint",0,0;
	end;
}


//--------------------------------------------------------------
// Lord Knight
//--------------------------------------------------------------

valkyrie.gat,44,39,5	script	Lord Knight	56,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Lord Knight]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Lord Knight]";
			mes "Everyone has agreed.... And none is objecting.";
			mes "Then I shall give my judgement too.";
			mes "My opinion is.....";
			next;
			mes "[Lord Knight]";
			mes "Agree!";
		}
		close;
	}
	callfunc "F_JobChanger","Lord Knight",1,7;
	mes "[Lord Knight]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// High Priest
//--------------------------------------------------------------

valkyrie.gat,44,42,5	script	High Priest	60,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[High Priest]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[High Priest]";
			mes "Oh God, please use your power to help this human,";
			mes "release your wisdom to gift us with strength and glory.";
			mes ".........";
		}
		close;
	}
	callfunc "F_JobChanger","High Priest",4,8;
	mes "[High Priest]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// High Wizard
//--------------------------------------------------------------

valkyrie.gat,44,47,5	script	High Wizard	735,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[High Wizard]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[High Wizard]";
			mes "You're a Wizard, so act like one, understand?";
			mes "We possess powerful magic, so be careful with your action.";
			mes ".........";
		}
		close;
	}
	callfunc "F_JobChanger","High Wizard",2,9;
	mes "[High Wizard]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Whitesmith
//--------------------------------------------------------------

valkyrie.gat,44,50,5	script	Whitesmith	731,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Whitesmith]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Whitesmith]";
			mes "This is the receipt. It must be a hard task for you.";
			mes " given by "+strcharinfo(0)+"..., thank you!";
			mes ".........";
		}
		close;
	}
	callfunc "F_JobChanger","Whitesmith",5,10;
	mes "[Whitesmith]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Sniper
//--------------------------------------------------------------

valkyrie.gat,44,55,5	script	Sniper	727,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Sniper]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Sniper]";
			mes "^6B8C21So, the job upgrade trial begins now,^000000";
			mes "^6B8C21Among the monsters, hunt the monster with the name 'Sample for job change test'.^000000";
			mes "^6B8C21***** Beware of the trap in the process *****^000000";
			mes ".........";
		}
		close;
	}
	callfunc "F_JobChanger","Sniper",3,11;
	mes "[Sniper]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Assassin Cross
//--------------------------------------------------------------

valkyrie.gat,44,58,5	script	Assassin Cross	725,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Assassin Cross]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Assassin Cross]";
			mes "Don't bother others calling Assassin as the 'Grimtooh of Desert'.";
			mes "We shouldn't be provoked easily.";
		}
		close;
	}
	callfunc "F_JobChanger","Assassin Cross",6,12;
	mes "[Assassin Cross]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Paladin
//--------------------------------------------------------------

valkyrie.gat,53,39,3	script	Paladin	752,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Paladin]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Paladin]";
			mes "Do not forget that the Holy War is approaching. Don't stop training!!";
			mes "May Odin protect you!";
			mes "Go now!.........";
		}
		close;
	}
	callfunc "F_JobChanger","Paladin",1,14;
	mes "[Paladin]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Champion
//--------------------------------------------------------------

valkyrie.gat,53,42,3	script	Champion	52,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Champion]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Champion]";
			mes "We will perform the ceremony to change your chi now..";
			next;
			mes "[Champion]";
			mes "....You have performed this before...";
			mes ".........";
		}
		close;
	}
	callfunc "F_JobChanger","Champion",4,15;
	mes "[Champion]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Professor
//--------------------------------------------------------------

valkyrie.gat,53,47,3	script	Professor	743,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Professor]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Professor]";
			mes "Please take good care of your thesis because it is the only book you've written.";
			mes "There will be something useful in it. And it keeps your past memory.";
			mes ".........";
		}
		close;
	}
	callfunc "F_JobChanger","Professor",2,16;
	mes "[Professor]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Stalker
//--------------------------------------------------------------

valkyrie.gat,53,58,3	script	Stalker	747,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Stalker]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Stalker]";
			mes "The password is ^0000FF'Aragham didn't move the refine item to another place'^000000..";
			mes "..........";
		}
		close;
	}
	callfunc "F_JobChanger","Stalker",6,17;
	mes "[Stalker]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Creator
//--------------------------------------------------------------

valkyrie.gat,53,50,3	script	Creator	122,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Creator]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Creator]";
			mes "When you buy 5 Iron Helms with a 24% discount,";
			mes "and sell them with a 20% overcharge,";
			mes "how much zeny do you earn?";
			mes ".........";
		}
		close;
	}
	callfunc "F_JobChanger","Creator",5,18;
	mes "[Creator]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Clown
//--------------------------------------------------------------

valkyrie.gat,53,54,3	script	Clown	741,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Clown]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Clown]";
			mes "So, let's sing, shall we...?";
			mes "I'll sing one verse first...";
			mes "........";
		}
		close;
	}
	callfunc "F_JobChanger","Clown",3,19;
	mes "[Clown]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Gypsy
//--------------------------------------------------------------

valkyrie.gat,53,56,3	script	Gypsy	101,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Gypsy]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Gypsy]";
			mes "Step left once then step right~";
			mes "......";
		}
		close;
	}
	callfunc "F_JobChanger","Gypsy",3,20;
	mes "[Gypsy]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// 2nd Class Job Changer Function
// -> callfunc "F_JobChanger","Name",1stClass,2ndClass
//--------------------------------------------------------------

function	script	F_JobChanger	{

	if (JobLevel < 40 || Basejob != getarg(1) || OLD_CLASS != getarg(2))
		return;
	mes "["+getarg(0)+"]";
	mes "Look for your glory,";
	mes "as it's time to shine in the new era.";
	mes "Walk the path of Heroes,";
	mes "in your new appearance.";
	next;
	mes "["+getarg(0)+"]";
	mes "Do you want to perform the job upgrade to "+getarg(0)+"?";
	next;
	if (select("No","Yes")==1) {
		mes "["+getarg(0)+"]";
		mes "Then find me when you're fully prepared!";
		mes "May the honor be with the warriors!";
		close;
	}
	if (SkillPoint) {
		mes "["+getarg(0)+"]";
		mes "You haven't learned enough.";
		mes "Come back after you've learned all the skills.";
		close;
	}
	unequip;
	jobchange getarg(2),1;
	if (Basejob==18 && SKILL_BIOETHICS==13) {
		skill 238,1,0;
		set SKILL_BIOETHICS,0;
	}
	mes "["+getarg(0)+"]";
	mes "Congratulations.";
	mes "After you've gone back to your world,";
	mes "may you lead everyone with your ability.";
	close;
}


//--------------------------------------------------------------
// 1st Class Job Changer Function
// -> callfunc "F_HighFirstClass",ChangeToJobID,"ChangeToJobName",2-1Class,2-2Class,ExtraParam,"NPCName";
//--------------------------------------------------------------

function	script	F_HighFirstClass	{

	if (Upper == 1 && Basejob == Job_Novice) {
		if (OLD_CLASS != getarg(2) && OLD_CLASS != getarg(3) && OLD_CLASS != getarg(4)) {
			mes "["+getarg(5)+"]";
			mes "After a long journey, you've come to this place again.";
			mes "You must have seen, listened and experienced a lot of things.";
			next;
			mes "["+getarg(5)+"]";
			mes "However, the path of an "+getarg(1)+" is not what you're destined to be.";
			mes "Recall your lost memory and embrace your own destiny!";
			close;
		}
		mes "["+getarg(5)+"]";
		mes "After a long journey, you've come to this place again.";
		mes "You must have seen, listened and experienced a lot of things.";
		next;
		if (getskilllv(1) < 9 || SkillPoint) {
			mes "["+getarg(5)+"]";
			mes "I will transfer you to "+getarg(1)+" again.";
			mes "However, you must learn the basic skills before I do that";
			next;
			mes "["+getarg(5)+"]";
			mes "Well... I will be waiting for your return.";
			mes "Farewell then.";
			close;
		}
		mes "["+getarg(5)+"]";
		mes "Come, set out for your pilgrimage again.";
		mes "May your future be blessed with happiness and light.";
		next;
		unequip;
		jobchange getarg(0),1;
		switch(Basejob) {
		case Job_Swordsman:
			skill 144,1,0;
			skill 145,1,0;
			skill 146,1,0;
			break;
		case Job_Mage:
			skill 157,1,0;
			break;
		case Job_Archer:
			skill 147,1,0;
			skill 148,1,0;
			break;
		case Job_Acolyte:
			skill 156,1,0;
			break;
		case Job_Merchant:
			skill 153,1,0;
			skill 154,1,0;
			skill 155,1,0;
			break;
		case Job_Thief:
			skill 149,1,0;
			skill 150,1,0;
			skill 151,1,0;
			skill 152,1,0;
			break;
		}
		close;
	}
	else if (Upper == 1 && Basejob == Job_Acolyte) {
		mes "["+getarg(5)+"]";
		mes "Come, set out for your pilgrimage again.";
		mes "May your future be blessed with happiness and light.";
		close;
	} else {
		return;
	}
}
