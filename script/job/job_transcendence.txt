//--------------------------------------------------------------
//               (c)2004-2007 Freya Team Presents:              
//                 ___   ___    ___   _  _   __                 
//                (  _) (  ,)  (  _) ( \/ ) (  )                
//                (  _)  )  \   ) _)  \  /  /__\                
//                (_)   (_)\_) (___) (__/  (_)(_)               
//                    http://www.ro-freya.net                   
//--------------------------------------------------------------
// Script Title: Transcendence Job Change Quest	Author: Blaze/Tsuyuki
//--------------------------------------------------------------
// Revision History: v1.3
//--------------------------------------------------------------
//	1.0 First version [Blaze]
//	1.1 Translated from Japanese into English [Tsuyuki]
//	1.2 Made some small modifications for Phaeton compatibility [Tsuyuki]
//	1.3 Added 1st class job change function (For use with original job quests) [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: * Aegis-certified *
//--------------------------------------------------------------


//--------------------------------------------------------------
// Metavis Seith
//--------------------------------------------------------------

yuno_in02.gat,88,164,5	script	Metavis Seith	742,{

	if (TRANSMIGRATE == 0) {
		if (BaseLevel < 99 || JobLevel < 50 || Basejob < 7 || Basejob > 20 || Upper != 0) {
			mes "[Metavis Seith]";
			mes "Welcome, this is the Suychervile Magic Academy.";
			mes "Enjoy your visit.";
			mes "There are a lot of books here.";
			close;
		}
		mes "[Metavis Seith]";
		mes "Welcome, this is the library of";
		mes "Suychervile Magic Academy.";
		mes "Do you come to learn 'The Book of Ymir'?";
		next;
		mes "[Metavis Seith]";
		mes "Although the content of Book of Ymir";
		mes "has been opened to public.";
		mes "Some might want to read it themselves.";
		mes "But since the condition of the book is not that good,";
		mes "we would hope to receive some donation";
		mes "from those who wanted to read it.";
		next;
		mes "[Metavis Seith]";
		mes "The amount of donation should be 1,285,000 zeny";
		mes "This is used for the protection and";
		mes "research of ancient script.";
		mes "If you are willing to pay it, you can enter the venue now.";
		mes "Do you want to pay it?";
		next;
		if (select("Yes","Cancel")==2) {
			mes "[Metavis Seith]";
			mes "You may proceed.";
			mes "Have a nice visit.";
			close;
		}
		if (Zeny < 1285000) {
			mes "[Metavis Seith]";
			mes "Looks like you do not have enough zeny.";
			mes "If you want to pay and go in,";
			mes "please make sure that you have enough zeny.";
			close;
		}
		set Zeny,Zeny-1285000;
		set TRANSMIGRATE,1;
	}
	mes "[Metavis Seith]";
	mes "Thank you,";
	mes "We will use this well in the research for the academy.";
	mes "You can proceed now.";
	close;
}


//--------------------------------------------------------------
// Book of Ymir
//--------------------------------------------------------------

yuno_in02.gat,93,207,0	script	Book of Ymir	111,{

	if (Upper == 1) {
		mes "[Book of Ymir]";
		mes "...The entrance to the Hall of Fame,";
		mes "has opened to those who have reborn.";
		mes "Those who will walk the path of Heroes, ";
		mes "all the places in this world shall be opened for you.";
		next;
		mes "[Book of Ymir]";
		mes "This place,";
		mes "has prepared for them too.";
		mes "According to the rumors,";
		mes "anything can possibly happen in the Hall.";
		next;
		if (select("Stop reading","Continue")==1) {
			mes "[Book of Ymir]";
			mes ". . . . .";
			close;
		}
		mes "[Book of Ymir]";
		mes "There is a passageway to the temple of Valkyrie";
		mes "at the place nearest to the sky.....";
		mes "The commoners will never know.";
		close2;
		warp "valkyrie.gat",48,8;
		end;
	}
	if (TRANSMIGRATE == 0) {
		mes "[Book of Ymir]";
		mes ". . . . .";
		close;
	}
	mes "[Book of Ymir]";
	mes "...Although the elders have done different great things,";
	mes "there is one matter";
	mes "that none can escape - Death.";
	mes "Even those who lived with the title of Heroes,";
	mes "will meet this moment in their lives.";
	next;
	mes "[Book of Ymir]";
	mes "Alas, none of the records or songs of the Heroes,";
	mes "or even the tales that were passed for generations,";
	mes "is able to tell the final moment of the Heroes.";
	mes "Be acknowledge that this moment,";
	mes "is only stated in the content of Valkyrie";
	mes "in the world of Ragnarok.";
	next;
	mes "[Book of Ymir]";
	mes "If a man has the honorable and greatest qualities,";
	mes "that makes him a figther for the final war,";
	mes "His slain soul shall be leady by the Valkyries";
	mes "to the Hall of Valhalla.";
	mes "His last moment is different from the commoners,";
	mes "for this is the reincarnation of a Hero.";
	next;
	mes "[Book of Ymir]";
	mes "He will be reborn as a real Hero,";
	mes "to shine the people of new era.";
	mes "The fallen body has rised,";
	mes "and the soul shall call upon the new power.";
	mes "Unfortunately, the Heart of Ymir,";
	mes "was lose in this land during the chaotic war.";
	next;
	mes "[Book of Ymir]";
	mes "But this discovery will not be able to support us for the research.";
	mes "Without experiment,";
	mes "we cannot try anything.";
	mes "Therefore, I leave this record,";
	mes "in hope that there'll be someone to find this record";
	mes "together with my will and hope.";
	next;
	mes "[Book of Ymir]";
	mes "Reborn of the Hero shall light the flame of an era.";
	mes "And the long wait shall ends,";
	mes "for the chosen one shall appear,";
	mes "leading the heroes from the hall of Valkyries. And then...";
	mes "...........";
	set TRANSMIGRATE,2;
	close;
}


//--------------------------------------------------------------
// Heart of Ymir
//--------------------------------------------------------------

yuno_in05.gat,49,43,0	script	Heart of Ymir	111,{

	if (TRANSMIGRATE == 2)
		warp "valkyrie.gat",48,8;
	end;
}

//--------------------------------------------------------------
// Valkyrie
//--------------------------------------------------------------

valkyrie.gat,48,86,4	script	Valkyrie	811,{

	if (Upper != 0) {
		mes "[Valkyrie]";
		mes "Welcome to the great Valhalla.";
		mes "Alas, it is still not your time.";
		mes "Please leave now and do not corrupt this place of glory.";
		mes "May the honour be with the warriors.!";
		close;
	}
	mes "[Valkyrie]";
	mes "Welcome to the great Valhalla.";
	mes "From now onwards, your present life ends here.";
	mes "You shall be given another new path!";
	mes "May the honor be with the warriors!";
	next;
	if (Weight || Zeny || SkillPoint || checkcart() || checkfalcon() || checkriding()) {
		mes "[Valkyrie]";
		mes "Good..You need to prepare a few items now.";
		mes "First, cleanse your body and soul.";
		mes "The honorable spirit of yours will only be";
		mes "shown if your soul and body are cleansed.";
		next;
		mes "[Valkyrie]";
		mes "All the items and zeny you're carrying has to be cleansed,";
		mes "including the pets, push cart and other stuffs...";
		mes "None can be brought away with you,";
		mes "Now...be prepared and come back later.";
		close2;
		warp "yuno_in02.gat",93,205;
		end;
	}
	mes "[Valkyrie]";
	mes "You've have cleansed your body and soul.";
	mes "A great will you have there. The honorable spirit of yours will shine once your body and soul are cleansed.";
	next;
	mes "[Valkyrie]";
	mes "Good...The memories shall be removed from you.";
	mes "Only the memory of the glorious moment right now shall stay inside of you.";
	next;
	mes "[Valkyrie]";
	mes "Good...Now,";
	mes "One, your pass memory shall be left in Urd.";
	mes "Two, you will only remember you glorious moment in Verdande.";
	mes "Three, You will live your new life in Skuld";
	next;
	mes "[Valkyrie]";
	mes "One";
	next;
	mes "[Valkyrie]";
	mes "Two....";
	next;
	mes "[Valkyrie]";
	mes "Three....";
	set OLD_CLASS,Basejob;
	jobchange 0,1;
	set BaseLevel,1;
	resetstatus;
	resetskill;
	setoption 0x0000;
	skill 143,1,0;
	skill 142,1,0;
	set SkillPoint,0;
	set StatusPoint,100;
	set TRANSMIGRATE,0;
	callfunc "F_ClearJobVariables";
	next;
	getitem 1202,1;
	getitem 2302,1;
	mes "[Valkyrie]";
	mes "Good. Congratulations. The process is complete.";
	mes "This is, a gift that I want you to have in your life as a new warrior!";
	next;
	mes "[Valkyrie]";
	mes "May the memory of your life in Urd will not be wasted.";
	mes "May the glorious moment in Verdande be reborn.";
	mes "May the new life in Skuld bring glory again.";
	close2;
	switch(OLD_CLASS) {
		case 7:
		case 14:
			savepoint "izlude.gat",94,103;
			break;
		case 8:
		case 15:
			savepoint "prontera.gat",273,354;
			break;
		case 9:
		case 16:
			savepoint "geffen.gat",120,100;
			break;
		case 10:
		case 18:
			savepoint "alberta.gat",116,57;
			break;
		case 11:
		case 19:
		case 20:
			savepoint "payon.gat",160,58;
			break;
		case 12:
		case 17:
			savepoint "morocc.gat",160,94;
			break;
	}
	warp "SavePoint",0,0;
	end;
}


//--------------------------------------------------------------
// Transporter
//--------------------------------------------------------------

valkyrie.gat,44,33,5	script	Transporter	124,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Transporter]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Transporter]";
			mes "Please do not touch the items on display.";
			mes "..........";
		}
		close;
	}
	mes "[Transporter]";
	mes "Please remember your moment of glory.";
	mes "Where do you want to go now?";
	next;
	switch (select("Prontera","Morroc","Payon","Geffen","Alberta","Izlude","Aldebaran","Comodo","Juno")) {
		case 1: savepoint "prontera.gat",116,72; 	break;
		case 2: savepoint "morocc.gat",156,46; 		break;
		case 3: savepoint "payon.gat",160,58; 		break;
		case 4: savepoint "geffen.gat",120,39; 		break;
		case 5: savepoint "alberta.gat",117,56; 	break;
		case 6: savepoint "izlude.gat",91,105; 		break;
		case 7: savepoint "aldebaran.gat",160,109; 	break;
		case 8: savepoint "comodo.gat",209,143; 	break;
		case 9: savepoint "yuno.gat",328,101; 		break;
	}
	warp "SavePoint",0,0;
	end;
}


//--------------------------------------------------------------
// Lord Knight
//--------------------------------------------------------------

valkyrie.gat,44,39,5	script	Lord Knight	56,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Lord Knight]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Lord Knight]";
			mes "Everyone has agreed.... And none is objecting.";
			mes "Then I shall give my judgement too.";
			mes "My opinion is.....";
			next;
			mes "[Lord Knight]";
			mes "Agree!";
		}
		close;
	}
	callfunc "JobChanger","Lord Knight",1,7;
	mes "[Lord Knight]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// High Priest
//--------------------------------------------------------------

valkyrie.gat,44,42,5	script	High Priest	60,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[High Priest]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[High Priest]";
			mes "Oh God, please use your power to help this human,";
			mes "release your wisdom to gift us with strength and glory.";
			mes ".........";
		}
		close;
	}
	callfunc "JobChanger","High Priest",4,8;
	mes "[High Priest]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// High Wizard
//--------------------------------------------------------------

valkyrie.gat,44,47,5	script	High Wizard	735,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[High Wizard]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[High Wizard]";
			mes "You're a Wizard, so act like one, understand?";
			mes "We possess powerful magic, so be careful with your action.";
			mes ".........";
		}
		close;
	}
	callfunc "JobChanger","High Wizard",2,9;
	mes "[High Wizard]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Whitesmith
//--------------------------------------------------------------

valkyrie.gat,44,50,5	script	Whitesmith	731,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Whitesmith]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Whitesmith]";
			mes "This is the receipt. It must be a hard task for you.";
			mes " given by "+strcharinfo(0)+"..., thank you!";
			mes ".........";
		}
		close;
	}
	callfunc "JobChanger","Whitesmith",5,10;
	mes "[Whitesmith]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Sniper
//--------------------------------------------------------------

valkyrie.gat,44,55,5	script	Sniper	727,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Sniper]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Sniper]";
			mes "^6B8C21So, the job upgrade trial begins now,^000000";
			mes "^6B8C21Among the monsters, hunt the monster with the name 'Sample for job change test'.^000000";
			mes "^6B8C21***** Beware of the trap in the process *****^000000";
			mes ".........";
		}
		close;
	}
	callfunc "JobChanger","Sniper",3,11;
	mes "[Sniper]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Assassin Cross
//--------------------------------------------------------------

valkyrie.gat,44,58,5	script	Assassin Cross	725,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Assassin Cross]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Assassin Cross]";
			mes "Don't bother others calling Assassin as the 'Grimtooh of Desert'.";
			mes "We shouldn't be provoked easily.";
		}
		close;
	}
	callfunc "JobChanger","Assassin Cross",6,12;
	mes "[Assassin Cross]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Paladin
//--------------------------------------------------------------

valkyrie.gat,53,39,3	script	Paladin	752,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Paladin]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Paladin]";
			mes "Do not forget that the Holy War is approaching. Don't stop training!!";
			mes "May Odin protect you!";
			mes "Go now!.........";
		}
		close;
	}
	callfunc "JobChanger","Paladin",1,14;
	mes "[Paladin]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Champion
//--------------------------------------------------------------

valkyrie.gat,53,42,3	script	Champion	52,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Champion]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Champion]";
			mes "We will perform the ceremony to change your chi now..";
			next;
			mes "[Champion]";
			mes "....You have performed this before...";
			mes ".........";
		}
		close;
	}
	callfunc "JobChanger","Champion",4,15;
	mes "[Champion]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Professor
//--------------------------------------------------------------

valkyrie.gat,53,47,3	script	Professor	743,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Professor]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Professor]";
			mes "Please take good care of your thesis because it is the only book you've written.";
			mes "There will be something useful in it. And it keeps your past memory.";
			mes ".........";
		}
		close;
	}
	callfunc "JobChanger","Professor",2,16;
	mes "[Professor]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Stalker
//--------------------------------------------------------------

valkyrie.gat,53,58,3	script	Stalker	747,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Stalker]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Stalker]";
			mes "The password is ^0000FF'Aragham didn't move the refine item to another place'^000000..";
			mes "..........";
		}
		close;
	}
	callfunc "JobChanger","Stalker",6,17;
	mes "[Stalker]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Creator
//--------------------------------------------------------------

valkyrie.gat,53,50,3	script	Creator	122,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Creator]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Creator]";
			mes "When you buy 5 Iron Helms with a 24% discount,";
			mes "and sell them with a 20% overcharge,";
			mes "how much zeny do you earn?";
			mes ".........";
		}
		close;
	}
	callfunc "JobChanger","Creator",5,18;
	mes "[Creator]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Clown
//--------------------------------------------------------------

valkyrie.gat,53,54,3	script	Clown	741,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Clown]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Clown]";
			mes "So, let's sing, shall we...?";
			mes "I'll sing one verse first...";
			mes "........";
		}
		close;
	}
	callfunc "JobChanger","Clown",3,19;
	mes "[Clown]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// Gypsy
//--------------------------------------------------------------

valkyrie.gat,53,56,3	script	Gypsy	101,{

	if (Upper != 1) {
		if (rand(3)) {
			mes "[Gypsy]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
		}
		else {
			mes "[Gypsy]";
			mes "Step left once then step right~";
			mes "......";
		}
		close;
	}
	callfunc "JobChanger","Gypsy",3,20;
	mes "[Gypsy]";
	mes "Welcome to the great Valhalla.";
	mes "Rest well before you depart.";
	mes "May the honor be with the warriors!";
	close;
}


//--------------------------------------------------------------
// 2nd Class Job Changer Function
// -> callfunc "JobChanger","Name",1stClass,2ndClass
//--------------------------------------------------------------

function	script	JobChanger	{

	if (JobLevel < 40 || Basejob != getarg(1) || OLD_CLASS != getarg(2))
		return;
	mes "["+getarg(0)+"]";
	mes "Look for your glory,";
	mes "as it's time to shine in the new era.";
	mes "Walk the path of Heroes,";
	mes "in your new appearance.";
	next;
	mes "["+getarg(0)+"]";
	mes "Do you want to perform the job upgrade to "+getarg(0)+"?";
	next;
	if (select("No","Yes")==1) {
		mes "["+getarg(0)+"]";
		mes "Then find me when you're fully prepared!";
		mes "May the honor be with the warriors!";
		close;
	}
	if (SkillPoint) {
		mes "["+getarg(0)+"]";
		mes "You haven't learned enough.";
		mes "Come back after you've learned all the skills.";
		close;
	}
	unequip;
	jobchange getarg(2),1;
	if (Basejob==18 && SKILL_BIOETHICS==13) {
		skill 238,1,0;
		set SKILL_BIOETHICS,0;
	}
	mes "["+getarg(0)+"]";
	mes "Congratulations.";
	mes "After you've gone back to your world,";
	mes "may you lead everyone with your ability.";
	close;
}


//--------------------------------------------------------------
// 1st Class Job Changer Function
// -> callfunc "F_HighFirstClass",ChangeToJobID,"ChangeToJobName",2-1Class,2-2Class,ExtraParam,"NPCName";
//--------------------------------------------------------------

function	script	F_HighFirstClass	{

	if (Upper == 1 && Basejob == 0) {
		if (OLD_CLASS != getarg(2) && OLD_CLASS != getarg(3) && OLD_CLASS != getarg(4)) {
			mes "["+getarg(5)+"]";
			mes "After a long journey, you've come to this place again.";
			mes "You must have seen, listened and experienced a lot of things.";
			next;
			mes "["+getarg(5)+"]";
			mes "However, the path of an "+getarg(1)+" is not what you're destined to be.";
			mes "Recall your lost memory and embrace your own destiny!";
			close;
		}
		mes "["+getarg(5)+"]";
		mes "After a long journey, you've come to this place again.";
		mes "You must have seen, listened and experienced a lot of things.";
		next;
		if (getskilllv(1) < 9 || SkillPoint) {
			mes "["+getarg(5)+"]";
			mes "I will transfer you to "+getarg(1)+" again.";
			mes "However, you must learn the basic skills before I do that";
			next;
			mes "["+getarg(5)+"]";
			mes "Well... I will be waiting for your return.";
			mes "Farewell then.";
			close;
		}
		mes "["+getarg(5)+"]";
		mes "Come, set out for your pilgrimage again.";
		mes "May your future be blessed with happiness and light.";
		next;
		unequip;
		jobchange getarg(0),1;
		switch(Basejob) {
		case Job_Swordsman:
			skill 144,1,0;
			skill 145,1,0;
			skill 146,1,0;
			break;
		case Job_Mage:
			skill 157,1,0;
			break;
		case Job_Archer:
			skill 147,1,0;
			skill 148,1,0;
			break;
		case Job_Acolyte:
			skill 156,1,0;
			break;
		case Job_Merchant:
			skill 153,1,0;
			skill 154,1,0;
			skill 155,1,0;
			break;
		case Job_Thief:
			skill 149,1,0;
			skill 150,1,0;
			skill 151,1,0;
			skill 152,1,0;
			break;
		}
		close;
	}
	if (Upper == 1 && Basejob == 4) {
		mes "["+getarg(5)+"]";
		mes "Come, set out for your pilgrimage again.";
		mes "May your future be blessed with happiness and light.";
		close;
	}
	return;
}
