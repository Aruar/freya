//--------------------------------------------------------------
//               (c)2004-2007 Freya Team Presents:              
//                  ___                                         
//                 /   |_   _ _ __ ___  _ __ __ _               
//                / /| | | | | '__/ _ \| '__/ _` |              
//               / ___ | |_| | | | (_) | | | (_| |              
//              /_/  |_|\____|_|  \___/|_|  \__,_|              
//                    http://www.ro-freya.net                   
//--------------------------------------------------------------
// Script Title: Archer Job Change Quest	Author: A Lot of People
//--------------------------------------------------------------
// Revision History: v1.8
//--------------------------------------------------------------
//	1.0 First version [kobra_k88]
//	1.1 Added instant job change for High Novice [Lupus]
//	1.2 Added Baby Class support [Lupus]
//	1.3 Optimized and moved first checker to Jfunc1-1 [massdriller]
//	1.5 Now saves/restores all quest skills [Lupus]
//	1.6 Merged back JFunc [Lupus]
//	1.7 Updated dialogue to match Aegis [Brainstorm]
//	1.8 Converted to Aurora/jAthena format and fixed a bunch of typos/bad grammar [voidreality]
//--------------------------------------------------------------
// Additional Notes: * Aegis-certified *
//--------------------------------------------------------------


//--------------------------------------------------------------
// Archer Guildsman
//--------------------------------------------------------------

payon_in02.gat,64,71,4	script	Archer Guildsman	59,{

	callfunc "F_HighFirstClass",Job_Archer,"Archer",Job_Hunter,Job_Bard,Job_Dancer,"Archer Guildsman";
	mes "[Archer Guildsman]";
	mes "Good day. How may I help you?";
	next;
	menu "I want to be an Archer.",M_0,"Need the Requirements Tip.",M_1,"No thanks.",M_End;

M_0:
	mes "[Archer Guildsman]";
	if(BaseJob==Job_Archer) {
		mes "You've occupied with Archer Job already.";
		close;
	}
	if(BaseJob!=Job_Novice) {
		mes "Hmm...You don't look like Novice at all..";
		mes "Anyway whatever you are,you cannot choose a job as an Archer any more because you have a Job already.";
		close;
	}
	if(BaseJob==Job_Novice && job_archer_q==1) goto L_Check;
	mes "Do you want to be an Archer?";
	mes "If so, you'd better fill this application form.";
	next;
	switch(select("Apply.","Cancel")) {
	case 1:
		set job_archer_q,1;
		mes "[Archer Guildsman]";
		mes "Ok, sign here.. Alright... Umm I will promote you when you meet the requirements.";
		mes "If you qualified already, we can make it now.";
		mes "Continue?";
		next;
		switch(select("Yes,Sure.","No,not yet.")) {
		case 1:
			mes "[Archer Guildsman]";
			mes "Let me check.";
			next;
			goto L_Check;
			break;
		case 2:
			mes "[Archer Guildsman]";
			mes "I understand.";
			mes "Be my guest if you want to know the requirements.";
			close;
		}
		break;
	case 2:
		mes "[Archer Guildsman]";
		mes "Yes.. See you next time.";
		close;
	}
	close;

M_1:
	if(BaseJob==Job_Archer) {
		mes "[Archer Guildsman]";
		mes "However, you've already occupied with Archer Job...";
		mes "....Yeah, whatever.. Who cares..";
		next;
	}
	if(BaseJob!=Job_Novice) {
		mes "[Archer Guildsman]";
		mes "But, you have another job...";
		mes "....Yeah, whatever.. Who cares..";
		next;
	}
	mes "[Archer Guildsman]";
	mes "First of all, you have to reach at Novice Job level with entire Basic Skills.";
	mes "Archer needs a high-concentration due to the specialness, we don't accept people without attention.";
	next;
	mes "[Archer Guildsman]";
	mes "You have to gather ^FF0000Trunk^000000 as many as you can.";
	mes "Those Trunks you bring into will be separated into 4 kinds by their each quality, and you'll be given marks with them.";
	mes "Lastly we will grant you to become an Archer when you receive grades at least ^0000FF25 ^000000 in 40 total.";
	next;
	mes "[Archer Guildsman]";
	mes "You can get 'Trunk' from 'Willow' the Tree. Although I can't say they are tough monsters, be careful.";
	close;

M_End:
	close;

L_Check:
	mes "[Archer Guildsman]";
	mes "Are you ... " + strcharinfo(0) + "?";
	next;
	mes "[Archer Guildsman]";
	if(getskilllv(1)<9) {
		mes "Well, you do not have enough of Job Level.. or Basic Skills might be not qualified.";
		mes "Please check the Requirements again. And level up at least Job Level 10.";
		mes "Don't forget you should learn entire Basic Skills. You may come back after that.";
		close;
	}
	mes "Excellent. Well-done, dear.";
	mes "Ok from now on I will appraise the value of articles,`Trunk' needed for producing a 'Bow'.";
	mes "....Er~~";
	next;
	mes "[Archer Guildsman]";
	if((countitem(1066)==0) && (countitem(1067)==0) && (countitem(1068)==0) && (countitem(1019)==0)) {
		mes "..... Unfortunately you didn't bring required items at all!!!";
		close;
	}
	if(SkillPoint > 0) {
		mes "You need to use up all of your skill points before I can make you an Archer.";
		close;
	}
	set @Trunk1, countitem(1066)*5;
	set @Trunk2, countitem(1067)*3;
	set @Trunk3, countitem(1068)*2;
	set @Trunk4, countitem(1019);
	set @TrunkTotal, @Trunk1 + @Trunk2 + @Trunk3 + @Trunk4;
	set @ArrowTotal, (@Trunk1 + @Trunk3)*2 + @Trunk4;

	mes "Lets see.....";
	next;
	mes "[Archer Guildsman]";
	mes "You have:";

	if(countitem(1066)!=0) mes " Grade S : " + countitem(1066) + " ea, Grade: " + @Trunk1 + " . ";
	if(countitem(1067)!=0) mes " Grade A : " + countitem(1067) + " ea, Grade : " + @Trunk2 + " . ";
	if(countitem(1068)!=0) mes " Grade B : " + countitem(1068) + " ea, Grade : " + @Trunk3 + " . ";
	if(countitem(1019)!=0) mes " Grade C : " + countitem(1019) + " ea, Grade : " + @Trunk4 + " . ";

	next;
	mes "[Archer Guildsman]";
	mes "Total Grade: ^FF0000" +@TrunkTotal+"^000000 / 40";
	next;
	mes "[Archer Guildsman]";
	if(@TrunkTotal < 25) {
		mes "Below 25 !? You have to gain at least 25! Try harder!!";
		close;
	}
	else if(@TrunkTotal > 40) mes "Wow!! Above 40! Excellent! Congratulations!";
	else if(@TrunkTotal > 30) mes "30 Above! Excellent! Congratulations!";
	else if(@TrunkTotal > 25) mes "Sigh~ You barely passed. Anyway well-done.";

	mes "I will transmit them to our Bow Produce Department.";
	next;

	delitem 1066, countitem(1066); // Items: Fine-grained_Trunk
	delitem 1067, countitem(1067); // Items: Solid_Trunk
	delitem 1068, countitem(1068); // Items: Barren_Trunk
	delitem 1019, countitem(1019); // Items: Trunk

	mes "[Archer Guildsman]";
	mes "Finally you achieved the Goal. I will promote you right away.";
	next;

	callfunc "F_JobChange",Job_Archer;
	callfunc "F_ClearJobVariables";

	mes "[Archer Guildsman]";
	mes "Congratulations! Besides we expect you will cooperate us for the future of the Archer Guild.";
	mes "Ah, your bow has arrived from Bow Produce Department. Please take it.";
	mes "Now you become a real Archer with this bow of your own.";

	getitem 1702,1; // Items: Bow_
	getitem 1750,@ArrowTotal; // Items: Arrow

	next;
	mes "[Archer Guildsman]";
	mes "Then farewell. I wish you will be the greatest Archer in the World.";
	close;
}
