//--------------------------------------------------------------
//               (c)2004-2007 Aurora Team Presents:             
//                  ___                                         
//                 /   |_   _ _ __ ___  _ __ __ _               
//                / /| | | | | '__/ _ \| '__/ _` |              
//               / ___ | |_| | | | (_) | | | (_| |              
//              /_/  |_|\____|_|  \___/|_|  \__,_|              
//                 http://aurora.deltaanime.net                 
//--------------------------------------------------------------
// Script Title: Kafra Corp System	Author: jAthena/Yor/Tsuyuki
//--------------------------------------------------------------
// Revision History: v1.5
//--------------------------------------------------------------
//	1.0 First version [jAthena]
//	1.1 Translated to English [Yor]
//	1.2 Imported script from fAthena [Tsuyuki]
//	1.3 Officialized some dialogue/function [Tsuyuki]
//	1.4 Updated Merchant Job Change Quest Kafras [Tsuyuki]
//	1.5 Merged in the Kafra Corporation Headquarters [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: The Izlude Byalan Island, and Prontera
// Old Swordsman Association Kafras are required for the Merchant
// Job Change Quest.
//--------------------------------------------------------------


//--------------------------------------------------------------
// Documentation:
//--------------------------------------------------------------
// Kafra package functions
//	callfunc "F_KafraMain",Speech,SelectNum,"OutSave",OutX,OutY,"InSave",InX,InY,
//				Storage,Cart,MapCodeArray,PriceArray;
// * Details of arguments:
//	Speech -> What says the Kafra?
//			0: A greeting of 3 lines.
//			1: A greeting of 4 lines.
//			2: For Comodo.
//			3: For items, no greeting's word.
//			4: For einbroch and einbech (only for teleport service: no teleport service in theses cities)
//	SelectNum -> The bit flag to choose among the 7 services (0x7f to select all 7 services)
//			+1:  Respawn point.
//			+2:  Warehouse (storage).
//			+4:  Teleport Service.
//			+8:  Cart.
//			+16: Kafra Points displaying.
//			+32: Kafra location (on mini-map).
//			+64: End.
//	"OutSave",OutX,OutY -> Respawn point outside the town when player want to recall ouside.
//	                       Or unique Respawn point (no choice, only 1 possible location).
//	"InSave",InX,InY    -> Respawn point inside the town when player want to save inside.
//	                       If "InSave" is set to NULL,
//	                       inside point is completly ignored and outside point is the only one used.
//	Storage -> Warehouse charge.
//	Cart -> Cart charge.
//	MapCodeArray -> The array variable which stored the possible MAP codes for the Teleport Service.
//	PriceArray -> The array variable which stored the required charge for each teleport.
//
// * MAP codes for teleport service:
//	1	Prontera
//	2	Izlude
//	3	Geffen
//	4	Payon
//	5	Morroc
//	6	Albelta
//	7	Orc dungeon
//	8	Aldebaran
//	9	Coal Mine(Dead Pit)
//	10	Comodo
//	11	Pharos Lighthouse island
//	12	Comodo Pharos Lighthouse (another display name of 11)
//	13	Yuno
//	14	Prontera (hideout)
//	15	Geffen (hideout)
//	16	Payon (hideout)
//	17	Aldebaran (hideout)
//	18	Reserved 1
//	19	Reserved 2
//	20	Reserved 3
//
// * parse end processing:
//	When there is the "Kafra location (on mini-map)", viewpoint is added, cutin is turned off.
//	If you want, you can change script to not finish and continue the conversation.
//
// * Character permanent variable:
//	Kafra_PIT -> Kafra points.
//--------------------------------------------------------------

function	script	F_KafraMain	{
	// Greeting section
	function sF_KafraSpeech {
		if (getarg(0)!=3) {
			mes "[Kafra Employee]";
			mes "Welcome to the";
			mes "Kafra Corporartion.";
			mes "The Kafra services";
			mes "are always on your side.";
			mes "How may I assist you?";
			switch(getarg(0)) {
			case 0:
				break;
			case 1:
			case 4:	// Einbroch/Einbech
				break;
			case 2:	// Comodo
				break;
			}
			next;
		}
		return;
	}

	// Service of Respawn point.
	function sF_KafraSave {
		if (getarg(5) == "NULL")
			savepoint getarg(2),getarg(3),getarg(4);
		else if (select("Save outside city.","Save inside city.")==1)
			savepoint getarg(2),getarg(3),getarg(4);	// Outside Save
		else
			savepoint getarg(5),getarg(6),getarg(7);	// Inside Save
		mes "[Kafra Employee]";
		mes "Your Respawn Point";
		mes "has been saved. Thank you";
		mes "for using the Kafra Service.";
		return;
	}

	// Teleport Service
	function sF_KafraTrans {
		if (getarg(0)==4) {	// Einbroch/Einbech
			mes "[Kafra Employee]";
			mes "Because of the ^FF0000Limited^000000";
			mes "^FF0000Transport Agreement^000000, the";
			mes "Kafra Corporation cannot";
			mes "provide Teleport Services";
			mes "in the Schwartzwald Republic.";
			next;
			mes "[Kafra Employee]";
			mes "We ask that you please";
			mes "use the Airship Service";
			mes "instead, Thank you for your";
			mes "understanding and cooperation.";
			return;
		}
		copyarray '@price,getarg(11),getarraysize(getarg(11));	// Received Price Array is developed in another array.
		// Only when the function calls for the first time, setarray is done.
		if ('placename$[0] == "") {
			setarray 'placename$,	"Prontera","Izlude","Geffen","Payon","Morroc","Albelta",
						"Orc dungeon","Aldebaran","Coal Mine (Dead Pit)","Comodo","Pharos Lighthouse island",
						"Comodo Pharos Lighthouse","Yuno","Prontera","Geffen","Payon","Aldebaran",
						"Reserved 1","Reserved 2","Reserved 3";
		}
		for(set '@i,0; '@i<getarraysize('@price); set '@i,'@i+1)
			set '@place$['@i],'placename$[ getelementofarray(getarg(10),'@i)-1 ] +" -> "+ '@price['@i] +" z";
		mes "[Kafra Employee]";
		mes "Please choice your destination.";
		next;
		set '@j,select(		'@place$[0],'@place$[1],'@place$[2],'@place$[3],'@place$[4],'@place$[5],'@place$[6],'@place$[7],
					'@place$[8],'@place$[9],'@place$[10],'@place$[11],'@place$[12],'@place$[13],'@place$[14],'@place$[15],
					'@place$[16],'@place$[17],'@place$[18],'@place$[19],"Cancel.")-1;
		if ('@j==20) {
			return;
		}
		if (Zeny<'@price['@j]) {
			mes "[Kafra Employee]";
			mes "I'm sorry, but you don't have enough money.";
			mes "Please check your funds again.";
			return;
		}
		set Zeny,Zeny-'@price['@j];
		set Kafra_PIT,Kafra_PIT+('@price['@j]/10);
		switch(getelementofarray(getarg(10),'@j)) {
			case 1: warp "prontera.gat",116,72; 	break;
			case 2: warp "izlude.gat",91,105; 	break;
			case 3: warp "geffen.gat",120,39; 	break;
			case 4: warp "payon.gat",160,58; 	break;
			case 5: warp "morocc.gat",156,46; 	break;
			case 6: warp "alberta.gat",117,56; 	break;
			case 7: warp "gef_fild10.gat",52,326; 	break;
			case 8: warp "aldebaran.gat",167,112; 	break;
			case 9: warp "mjolnir_02.gat",99,351; 	break;
			case 10: warp "comodo.gat",209,143; 	break;
			case 11:
			case 12:
				 warp "cmd_fild07.gat",127,134; break;
			case 13: warp "yuno.gat",158,124; 	break;
			// Guild Kafra special teleport points
			case 14: warp "prontera.gat",278,211; 	break;
			case 15: warp "geffen.gat",120,39; 	break;
			case 16: warp "payon.gat",158,88; 	break;
			case 17: warp "aldebaran.gat",132,103;	break;
			// Reserved
			case 18: warp "SavePoint",0,0;		break;
			case 19: warp "SavePoint",0,0;		break;
			case 20: warp "SavePoint",0,0;		break;
		}
		end;
	}

	// Cart service
	function sF_KafraCart {
		if (BaseClass != Job_Merchant) {
			mes "[Kafra Employee]";
			mes "I'm sorry dear.";
			mes "The Cart service is only provided for the Merchants and its higher jobs.";
			//mes "";
			return;
		}
		if (Cart) {
			mes "[Kafra Employee]";
			mes "Excuse me...";
			mes "But you already have a Cart...";
			return;
		}
		mes "[Kafra Employee]";
		mes "The Cart Fee is " +getarg(9)+ " Zenys.";
		mes "In addition, 'Pushcart' skill is required to use a Cart.";
		//mes "";
		mes "The Cart cannot be used if you don't have this skill and the money is nevertheless taken.";
		mes "So, be careful to your answer.";
		mes "Do you want to rent a Cart?";
		next;
		if (select("Rent a Cart.","Cancel.")==2) {
			mes "[Kafra Employee]";
			mes "Ok. No problem.";
			mes "It will be for a next time. ";
			return;
		}
		if (Zeny<getarg(9)) {
			mes "[Kafra Employee]";
			mes "Sorry, but you don't have enough Money.";
			mes "The Fee to use a Cart is " +getarg(9)+ " Zenys.";
			return;
		}
		set Zeny,Zeny-getarg(9);
		setcart;
		return;
	}

	// Information about Kafra points.
	function sF_KafraPoint {
		mes "[Kafra Employee]";
		mes strcharinfo(0)+ ", you have acquired"+Kafra_PIT+ " points actually.";
		//mes "";
		next;
		mes "[Kafra Employee]";
		mes "The Kafra points can be exchanged for the prize at head office of Kafra services.";
		//mes "";
		mes "Also in the future you ask Kafra services to have more Kafra points.";
		//mes "";
		return;
	}

	// End
	function sF_KafraEnd {
		mes "[Kafra Employee]";
		mes "Some of the rules of the Kafra Service are:";
		mes "- we aim toward a more perfect service and administration.";
		mes "- the customer must feel at ease.";
		mes "- the Kafra Services are always doing their best.";
		mes "Thank you for using Kafra Services.";
		mes "We hope to see you again soon.";
		return;
	}

	// From here main section
	if (Kafra_PIT >= 25000) {
		mes "[Kafra Employee]";
		mes "Your Special Reserve Points";
		mes "have exceeded the maximum";
		mes "capacity. Please redeem these";
		mes "points at the Kafra Corportation Headquarters in Al De Baran";
		mes "before using the Kafra Service.";
		close;
	}
	sF_KafraSpeech getarg(0);
	setarray '@serv$,"Set your respawn point","Use storage","Use Teleport Service",
				"Rent a Cart","Kafra Points information.","Kafra location on map";
	for(set '@i,0; '@i<7; set '@i,'@i+1) {
		if (getarg(1)&(1<<'@i) == 0)
			set '@serv$['@i],"";	// If the bit flag of SelectNum is not set, we remove the menu
	}
	switch(select('@serv$[0],'@serv$[1],'@serv$[2],'@serv$[3],'@serv$[4],'@serv$[5],'@serv$[6])) {
		case 1:
			// Respawn point service (values 0 and 1 are not used)
			sF_KafraSave 0,1,getarg(2),getarg(3),getarg(4),getarg(5),getarg(6),getarg(7);
			break;
		case 2:
			// Storage service (use a separated function)
			callfunc "F_KafraStorage",getarg(8);
			end;
		case 3:
			// Values from 1 to 9 are not used.
			if (getarg(0) != 4)
				sF_KafraTrans getarg(0),1,2,3,4,5,6,7,8,9,getarg(10),getarg(11);
			else
				sF_KafraTrans getarg(0);
			break;
		case 4:
			// Values from 0 to 8 are not used.
			sF_KafraCart 0,1,2,3,4,5,6,7,8,getarg(9);
			break;
		case 5:
			sF_KafraPoint;
			break;
		case 6:
			mes "[Kafra Employee]";
			mes "The flashing signals on your mini-map point to the locations of all of the Kafra Agents in this city.";
			//mes "";
			return;	// Only case 6 do a 'return', it's a better for user.
		case 7:
			sF_KafraEnd;
			break;
	}
	close2;
	cutin "Kafra_01",255;
	end;
}

// Only Storage service uses a separated function.
function	script	F_KafraStorage	{
	if (basicskillcheck() && getskilllv(1)<6) {
		mes "[Kafra Employee]";
		mes "I am sorry but you must have at least basic skill level 6 if you want to use the Storage.";
		//mes "";
	}
	else if (Zeny<getarg(0)) {
		mes "[Kafra Employee]";
		mes "Sorry, but you don't have enough Money.";
		mes "The Fee to use a Storage is " +getarg(0)+ " Zenys.";
	}
	else {
		set Zeny,Zeny-getarg(0);
		openstorage;
		cutin "Kafra_01",255;
		close;
	}
	close2;
	cutin "Kafra_01",255;
	return;
}


//--------------------------------------------------------------
// < Prontera - Center >
//--------------------------------------------------------------

prontera.gat,146,89,6	script	Kafra Employee	117,{

	cutin "Kafra_01",2;
	setarray '@code,2,3,4,5,6,7;
	setarray '@price,600,1200,1200,1200,1800,1700;
	callfunc "F_KafraMain",1,0x7f,"prontera.gat",116,72,"NULL",0,0,40,800,'@code,'@price;
	close2;
	viewpoint 1,146,89,1,0x0000FF;
	viewpoint 1,282,200,2,0x0000FF;
	viewpoint 1,151,29,3,0x0000FF;
	viewpoint 1,29,207,4,0x0000FF;
	cutin "Kafra_01",255;
	end;
}


//--------------------------------------------------------------
// < Prontera - East >
//--------------------------------------------------------------

prontera.gat,282,200,2	script	Kafra Employee	114,{

	cutin "Kafra_04",2;
	setarray '@code,2,3,4,5,6,7;
	setarray '@price,600,1200,1200,1200,1800,1700;
	callfunc "F_KafraMain",1,0x7f,"prt_fild06.gat",33,192,"prontera.gat",279,200,40,800,'@code,'@price;
	close2;
	viewpoint 1,146,89,1,0x0000FF;
	viewpoint 1,282,200,2,0x0000FF;
	viewpoint 1,151,29,3,0x0000FF;
	viewpoint 1,29,207,4,0x0000FF;
	cutin "Kafra_04",255;
	end;
}


//--------------------------------------------------------------
// < Prontera - South >
//--------------------------------------------------------------

prontera.gat,151,29,0	script	Kafra Employee	115,{

	cutin "Kafra_03",2;
	setarray '@code,2,3,4,5,6,7;
	setarray '@price,600,1200,1200,1200,1800,1700;
	callfunc "F_KafraMain",1,0x7f,"prt_fild08.gat",170,368,"prontera.gat",116,72,40,800,'@code,'@price;
	close2;
	viewpoint 1,146,89,1,0x0000FF;
	viewpoint 1,282,200,2,0x0000FF;
	viewpoint 1,151,29,3,0x0000FF;
	viewpoint 1,29,207,4,0x0000FF;
	cutin "Kafra_03",255;
	end;
}


//--------------------------------------------------------------
// < Prontera - West >
//--------------------------------------------------------------

prontera.gat,29,207,6	script	Kafra Employee	113,{

	cutin "Kafra_05",2;
	setarray '@code,2,3,4,5,6,7;
	setarray '@price,600,1200,1200,1200,1800,1700;
	callfunc "F_KafraMain",1,0x7f,"prt_fild05.gat",368,205,"prontera.gat",33,207,40,800,'@code,'@price;
	close2;
	viewpoint 1,146,89,1,0x0000FF;
	viewpoint 1,282,200,2,0x0000FF;
	viewpoint 1,151,29,3,0x0000FF;
	viewpoint 1,29,207,4,0x0000FF;
	cutin "Kafra_05",255;
	end;
}


//--------------------------------------------------------------
// < Prontera - North >
//--------------------------------------------------------------

prt_fild01.gat,198,47,0	script	Kafra Employee	112,{

	cutin "Kafra_06",2;
	setarray '@code,2,3,4,5,6,7;
	setarray '@price,600,1200,1200,1200,1800,1700;
	callfunc "F_KafraMain",1,0x5f,"prt_fild01.gat",198,51,"NULL",0,0,40,800,'@code,'@price;
	end;
}


//--------------------------------------------------------------
// < Prontera - In Front Of River (Outside) >
//--------------------------------------------------------------

prt_fild05.gat,290,224,4	script	Kafra Employee	114,{

	cutin "Kafra_04",2;
	callfunc "F_KafraMain",0,0x5b,"prt_fild05.gat",274,244,"NULL",0,0,30,800;
	end;
}


//--------------------------------------------------------------
// < Izlude >
//--------------------------------------------------------------

izlude.gat,134,88,4	script	Kafra Employee	115,{

	cutin "Kafra_03",2;
	setarray '@code,5,4,3,8;
	setarray '@price,1200,1200,1200,1800;
	callfunc "F_KafraMain",1,0x7f,"prt_fild08.gat",350,202,"izlude.gat",94,103,30,800,'@code,'@price;
	close2;
	viewpoint 1,136,88,1,0x0000FF;
	cutin "Kafra_03",255;
	end;
}


//--------------------------------------------------------------
// < Geffen - South Fountain >
//--------------------------------------------------------------

geffen.gat,120,62,0	script	Kafra Employee	115,{

	cutin "Kafra_03",2;
	setarray '@code,1,8,7,9;
	setarray '@price,1200,1200,1700,1700;
	callfunc "F_KafraMain",0,0x7f,"geffen.gat",120,38,"NULL",0,0,30,800,'@code,'@price;
	close2;
	viewpoint 1,120,62,1,0x0000FF;
	viewpoint 1,203,123,2,0x0000FF;
	cutin "Kafra_03",255;
	end;
}


//--------------------------------------------------------------
// < Geffen - East Entrance >
//--------------------------------------------------------------

geffen.gat,203,123,4	script	Kafra Employee	114,{

	cutin "Kafra_04",2;
	setarray '@code,1,8,7,9;
	setarray '@price,1200,1200,1700,1700;
	callfunc "F_KafraMain",0,0x7f,"gef_fild00.gat",55,222,"geffen.gat",120,38,30,800,'@code,'@price;
	close2;
	viewpoint 1,120,62,1,0x0000FF;
	viewpoint 1,203,123,2,0x0000FF;
	cutin "Kafra_04",255;
	end;
}


//--------------------------------------------------------------
// < Morroc - South >
//--------------------------------------------------------------

morocc.gat,156,97,4	script	Kafra Employee	113,{

	cutin "Kafra_05",2;
	setarray '@code,1,4,6,10,11;
	setarray '@price,1200,1200,1800,1800,1200;
	callfunc "F_KafraMain",1,0x7f,"morocc.gat",156,46,"NULL",0,0,40,800,'@code,'@price;
	close2;
	viewpoint 1,160,258,1,0x0000FF;
	viewpoint 1,159,97,2,0x0000FF;
	cutin "Kafra_05",255;
	end;
}


//--------------------------------------------------------------
// < Morroc - North Entrance >
//--------------------------------------------------------------

morocc.gat,160,258,4	script	Kafra Employee	114,{

	cutin "Kafra_04",2;
	setarray '@code,1,4,6,10,11;
	setarray '@price,1200,1200,1800,1800,1200;
	callfunc "F_KafraMain",1,0x7f,"moc_fild07.gat",211,29,"morocc.gat",160,283,40,800,'@code,'@price;
	close2;
	viewpoint 1,160,258,1,0x0000FF;
	viewpoint 1,159,97,2,0x0000FF;
	cutin "Kafra_04",255;
	end;
}


//--------------------------------------------------------------
// < Morroc - Pyramid Entrance >
//--------------------------------------------------------------

moc_ruins.gat,59,157,6	script	Kafra Employee	117,{
	cutin "Kafra_01",2;
	callfunc "F_KafraMain",1,0x5a,0,0,0,0,0,0,50,800;
	end;
}


//--------------------------------------------------------------
// < Albelta - North Entrance >
//--------------------------------------------------------------

alberta.gat,28,229,0	script	Kafra Employee	116,{

	cutin "Kafra_02",2;
	setarray '@code,4,1,5;
	setarray '@price,1200,1800,1800;
	callfunc "F_KafraMain",1,0x7f,"pay_fild03.gat",387,76,"alberta.gat",31,231,50,800,'@code,'@price;
	close2;
	viewpoint 1,28,229,1,0x0000FF;
	viewpoint 1,113,60,2,0x0000FF;
	cutin "Kafra_02",255;
	end;
}


//--------------------------------------------------------------
// < Alberta - South Fountain >
//--------------------------------------------------------------

alberta.gat,113,60,6	script	Kafra Employee#alberta2	112,{

	cutin "Kafra_06",2;
	setarray '@code,4,1,5;
	setarray '@price,1200,1800,1800;
	callfunc "F_KafraMain",1,0x7f,"alberta.gat",117,56,"NULL",0,0,50,800,'@code,'@price;
	close2;
	viewpoint 1,28,229,1,0x0000FF;
	viewpoint 1,113,60,2,0x0000FF;
	cutin "Kafra_06",255;
	end;
}


//--------------------------------------------------------------
// < Alberta - Island >
//--------------------------------------------------------------

alb2trea.gat,59,69,6	script	Kafra Employee	117,{

	cutin "Kafra_01",2;
	callfunc "F_KafraMain",0,0x5a,0,0,0,0,0,0,50,800;
	end;
}


//--------------------------------------------------------------
// < Payon - North >
//--------------------------------------------------------------

payon.gat,175,226,4	script	Kafra Employee	116,{

	cutin "Kafra_02",2;
	setarray '@code,6,1,5;
	setarray '@price,1200,1200,1200;
	callfunc "F_KafraMain",1,0x5f,"payon.gat",207,113,"NULL",0,0,40,800,'@code,'@price;
	end;
}


//--------------------------------------------------------------
// < Payon - South >
//--------------------------------------------------------------

payon.gat,181,104,4	script	Kafra Employee	113,{

	cutin "Kafra_05",2;
	setarray '@code,6,1,5;
	setarray '@price,1200,1200,1200;
	callfunc "F_KafraMain",1,0x5f,"payon.gat",160,58,"NULL",0,0,40,800,'@code,'@price;
	end;
}


//--------------------------------------------------------------
// < Payon - Archer's Village >
//--------------------------------------------------------------

pay_arche.gat,55,123,4	script	Kafra Employee	114,{

	cutin "Kafra_04",2;
	callfunc "F_KafraMain",1,0x5b,"pay_arche.gat",50,145,"NULL",0,0,40,800;
	end;
}

//--------------------------------------------------------------
// < Aldebaran >
//--------------------------------------------------------------

aldebaran.gat,143,119,4	script	Kafra Employee	113,{

	cutin "Kafra_05",2;
	setarray '@code,3,13,2,9;
	setarray '@price,1200,1200,1800,1700;
	callfunc "F_KafraMain",0,0x5f,"aldebaran.gat",167,112,"NULL",0,0,40,800,'@code,'@price;
	end;
}


//--------------------------------------------------------------
// < Comodo Inside A Building >
//--------------------------------------------------------------

cmd_in02.gat,146,180,4	script	Kafra Employee	721,{

	cutin "Kafra_07",2;
	setarray '@code,5,12;
	setarray '@price,1800,1200;
	callfunc "F_KafraMain",2,0x5f,"comodo.gat",209,143,"NULL",0,0,40,800,'@code,'@price;
	end;
}


//--------------------------------------------------------------
// < Comodo Pharos Lighthouse >
//--------------------------------------------------------------

cmd_fild07.gat,136,134,4	script	Kafra Employee	721,{

	cutin "Kafra_07",2;
	setarray '@code,10,5;
	setarray '@price,1200,1200;
	callfunc "F_KafraMain",2,0x5f,"cmd_fild07.gat",127,134,"NULL",0,0,80,1000,'@code,'@price;
	end;
}


//--------------------------------------------------------------
// < Comodo > 
//--------------------------------------------------------------

comodo.gat,195,150,4	script	Kafra Employee	721,{

	cutin "Kafra_07",2;
	setarray '@code,5,12;
	setarray '@price,1800,1200;
	callfunc "F_KafraMain",2,0x5f,"comodo.gat",180,151,"NULL",0,0,80,1000,'@code,'@price;
	end;
}


//--------------------------------------------------------------
// < Orc Dungeon >
//--------------------------------------------------------------

gef_fild10.gat,73,340,5	script	Kafra Employee	117,{

	cutin "Kafra_01",2;
	callfunc "F_KafraMain",0,0x5a,0,0,0,0,0,0,130,800;
	end;
}


//--------------------------------------------------------------
// < Coal Mine (Dead Pit) >
//--------------------------------------------------------------

mjolnir_02.gat,82,362,4	script	Kafra Employee	117,{

	cutin "Kafra_01",2;
	callfunc "F_KafraMain",1,0x5a,0,0,0,0,0,0,100,800;
	end;
}


//--------------------------------------------------------------
// < Yuno > South Entrance
//--------------------------------------------------------------

yuno.gat,152,187,4	script	Kafra Employee	113,{

	cutin "Kafra_05",2;
	set '@code,8;
	set '@price,1200;
	callfunc "F_KafraMain",0,0x7f,"yuno.gat",158,124,"NULL",0,0,40,800,'@code,'@price;
	close2;
	viewpoint 1,327,109,2,0x0000FF;
	viewpoint 1,277,221,3,0x0000FF;
	cutin "Kafra_05",255;
	end;
}


//--------------------------------------------------------------
// < Juno > South East
//--------------------------------------------------------------

yuno.gat,327,109,4	script	Kafra Employee	113,{

	cutin "Kafra_05",2;
	set '@code,8;
	set '@price,1200;
	callfunc "F_KafraMain",0,0x7f,"yuno.gat",327,100,"NULL",0,0,40,800,'@code,'@price;
	close2;
	viewpoint 1,152,187,1,0x0000FF;
	viewpoint 1,277,221,3,0x0000FF;
	cutin "Kafra_05",255;
	end;
}


//--------------------------------------------------------------
// < Juno > North East
//--------------------------------------------------------------

yuno.gat,277,221,4	script	Kafra Employee	113,{

	cutin "Kafra_05",2;
	set '@code,8;
	set '@price,1200;
	callfunc "F_KafraMain",0,0x7f,"yuno.gat",275,229,"NULL",0,0,40,800,'@code,'@price;
	close2;
	viewpoint 1,152,187,1,0x0000FF;
	viewpoint 1,327,109,2,0x0000FF;
	cutin "Kafra_05",255;
	end;
}


//--------------------------------------------------------------
// < Amatsu >
//--------------------------------------------------------------

amatsu.gat,102,149,5	script	Kafra Employee#amatsunomal	116,{

	cutin "Kafra_02",2;
	callfunc "F_KafraMain",1,0x5b,"amatsu.gat",116,94,"NULL",0,0,80,700;
	end;
}


//--------------------------------------------------------------
// < Kunlun >
//--------------------------------------------------------------

gonryun.gat,159,122,4	script	Kafra Employee	116,{

	cutin "Kafra_02",2;
	callfunc "F_KafraMain",1,0x5b,"gonryun.gat",160,62,"NULL",0,0,80,700;
	end;
}


//--------------------------------------------------------------
// < Umbala >
//--------------------------------------------------------------

umbala.gat,87,160,5	script	Kafra Employee	721,{

	cutin "Kafra_07",2;
	set '@code,10;
	set '@price,1800;
	callfunc "F_KafraMain",0,0x57,"umbala.gat",100,154,"NULL",0,0,80,0,'@code,'@price;
	end;
}


//--------------------------------------------------------------
// < Niflheim >
//--------------------------------------------------------------

niflheim.gat,202,180,3	script	Kafra Employee	791,{

	mes "[Kafra Employee]";
	mes "Welcome...";
	mes "Kafra Corporation...";
	mes "We are always near you, even if you're dying.";
	mes "How can I help you?";
	next;
	switch(select("Use storage","Cancel.")) {
	case 1:
		percentheal 0,-10;
		callfunc "F_KafraStorage",150;
		end;
	case 2:
		percentheal 0,-10;
		mes "[Kafra Employee]";
		mes "Kafra Services...";
		mes "anytime and anywhere...";
		mes "Always for customer...";
		mes "Because it is a duty...";
		mes "Please do not forget...";
		break;
	}
	close;
}

//--------------------------------------------------------------
// < Louyang >
//--------------------------------------------------------------

louyang.gat,210,104,4	script	Kafra Employee	116,{

	cutin "Kafra_02",2;
	callfunc "F_KafraMain",1,0x5b,"louyang.gat",218,97,"NULL",0,0,80,700;
	end;
}


//--------------------------------------------------------------
// < Ayothaya >
//--------------------------------------------------------------

ayothaya.gat,212,169,6	script	Kafra Employee	116,{

	cutin "Kafra_02",2;
	callfunc "F_KafraMain",1,0x5b,"ayothaya.gat",217,187,"NULL",0,0,80,700;
	end;
}


//--------------------------------------------------------------
// < Einbroch - Plaza >
//--------------------------------------------------------------

einbroch.gat,242,205,5	script	Kafra Employee#einbroch1	116,{

	cutin "Kafra_02",2;
	callfunc "F_KafraMain",4,0x7f,"einbroch.gat",239,197,"NULL",0,0,40,800;
	close2;
	viewpoint 1,242,205,1,0x0000FF;
	viewpoint 1,59,203,2,0x0000FF;
	cutin "Kafra_02",255;
	end;
}


//--------------------------------------------------------------
// < Einbroch - Airport >
//--------------------------------------------------------------

einbroch.gat,59,203,5	script	Kafra Employee#einbroch2	117,{

	cutin "Kafra_01",2;
	callfunc "F_KafraMain",4,0x7f,"einbroch.gat",239,197,"NULL",0,0,40,800;
	close2;
	viewpoint 1,242,205,1,0x0000FF;
	viewpoint 1,59,203,2,0x0000FF;
	cutin "Kafra_01",255;
	end;
}


//--------------------------------------------------------------
// < Einbech >
//--------------------------------------------------------------

einbech.gat,181,132,5	script	Kafra Employee	115,{

	cutin "Kafra_03",2;
	callfunc "F_KafraMain",4,0x7f,"einbech.gat",181,123,"NULL",0,0,40,850;
	close2;
	viewpoint 1,181,132,1,0x0000FF;
	cutin "Kafra_03",255;
	end;
}


//--------------------------------------------------------------
// < Lighthalzen North >
//--------------------------------------------------------------

lighthalzen.gat,191,320,4	script	Kafra Employee	861,{

	cutin "Kafra_09",2;
	callfunc "F_KafraMain",4,0x5f,"lighthalzen.gat",194,313,"NULL",0,0,40,800;
	end;
}


//--------------------------------------------------------------
// < Lighthalzen South >
//--------------------------------------------------------------

lighthalzen.gat,164,100,4	script	Kafra Employee	860,{

	cutin "Kafra_08",2;
	callfunc "F_KafraMain",4,0x5f,"lighthalzen.gat",158,96,"NULL",0,0,40,800;
	end;
}


//--------------------------------------------------------------
// < Lighthalzen Hotel >
//--------------------------------------------------------------

lhz_in02.gat,237,284,4	script	Kafra Employee	861,{

	cutin "Kafra_09",2;
	callfunc "F_KafraMain",4,0x5f,"lhz_in02.gat",278,214,"NULL",0,0,40,800;
	end;
}


//--------------------------------------------------------------
// < Prontera - Old Swordsman Association >
//--------------------------------------------------------------

prontera.gat,248,42,0	script	Kafra Employee	116,{

	cutin "kafra_02.bmp",2;
	if ((job_merchant_q == 6 || job_merchant_q == 5) && (job_merchant_q2 == 2 || job_merchant_q2 == 1)) {
		mes "[Kafra Employee]";
		mes "Oh! Thank you for";
		mes "traveling such a long";
		mes "way to come over here~";
		close2;
		cutin "kafra_02.bmp",255;
		end;
	} else if (job_merchant_q == 4 || job_merchant_q == 3) {
		mes "[Kafra Employee]";
		mes "A delivery from";
		mes "the Merchant Guild?";
		mes "Oh, yes, please set";
		mes "it down right over there...";
		if (countitem(1081) == 1 || countitem(1082) == 1 || countitem(1091) == 1) {
			mes "You must be really tired";
			mes "after carrying it for so long!";
		} else {
			mes "W-wait. Didn't you bring it?";
			mes "Where's the package?";
			close2;
			cutin "kafra_02.bmp",255;
			end;
		}
		next;
		mes "[Kafra Employee]";
		mes "Now, let me check";
		mes "the serial number...";
		if (job_merchant_q2 == 1 && countitem(1081) != 0) {
			mes "2485741. Right, this is";
			mes "the one we ordered. Oh,";
			mes "and don't forget this receipt!";
			next;
			delitem 1081,1;
			getitem 1073,1;
		} else if (job_merchant_q2 == 2 && countitem(1082) != 0) {
			mes "2328137. Right, this is";
			mes "the one we ordered. Oh,";
			mes "and don't forget this receipt!";
			next;
			delitem 1082,1;
			getitem 1074,1;
		} else {
			mes "Mmmm? Hold on. This is";
			mes "the wrong package. What we";
			if (job_merchant_q2 == 1) {
				mes "ordered had the serial number 2485741. I'm sure it's not this.";
			} else if (job_merchant_q2 == 2) {
				mes "ordered had the serial number 2328137. I'm sure it's not this.";
			} else {
				mes "ordered had the serial number 2328137 or 2328137.";
			}
			next;
			mes "[Kafra Employee]";
			mes "I'm afraid there";
			mes "must be some kind";
			mes "of mistake. Perhaps";
			mes "you should go back to";
			mes "the Merchant Guild to";
			mes "clear up this situation?";
			close2;
			cutin "kafra_02.bmp",255;
			end;
		}
		if (job_merchant_q == 4) {
			set job_merchant_q,6;
		} else if (job_merchant_q == 3) {
			set job_merchant_q,5;
		}
		mes "[Kafra Employee]";
		mes "Thanks again";
		mes "for going through";
		mes "all of that trouble~";
		close2;
		cutin "kafra_02.bmp",255;
		end;
	} else {
		mes "[Kafra Employee]";
		mes "Welcome to the";
		mes "Kafra Corportation,";
		mes "where the service is";
		mes "always on your side~";
		next;
		mes "[Kafra Employee]";
		mes "As you can see, the";
		mes "Swordman Assocation";
		mes "has moved to Izlude, a";
		mes "satellite city of Prontera.";
		mes "Currently, we offer a Teleport";
		mes "Service to Izlude for 600 zeny.";
		next;
		switch(select("Use","Cancel")) {
		case 1:
			if (Zeny < 600) {
				mes "[Kafra Employee]";
				mes "I'm sorry, but you";
				mes "don't have enough zeny";
				mes "for this Teleport Service.";
				close2;
				cutin "kafra_02.bmp",255;
				end;
			}
			set Zeny,Zeny-600;
			set KAFRA_PIT,KAFRA_PIT+60;
			cutin "kafra_02.bmp",255;
			warp "izlude.gat",94,103;
			end;
		case 2:
			close2;
			cutin "kafra_02.bmp",255;
			end;
		}
	}
}


//--------------------------------------------------------------
// < Izlude - Byalan Island >
//--------------------------------------------------------------

izlu2dun.gat,106,58,4	script	Kafra Employee	116,0,0,{

	if (KAFRA_PIT >= 25000) {
		mes "[Kafra Employee]";
		mes "Your Special Reserve Points";
		mes "have exceeded the maximum";
		mes "capacity. Please redeem these";
		mes "points at the Kafra Corportation Headquarters in Al De Baran";
		mes "before using the Kafra Service.";
		close;
	}
	cutin "kafra_02.bmp",2;
	if ((job_merchant_q == 6 || job_merchant_q == 5) && (job_merchant_q2 == 8 || job_merchant_q2 == 7)) {
		mes "[Kafra Employee]";
		mes "Oh hello~";
		mes "Um, is there";
		mes "some special reason";
		mes "as to why you're here?";
		close2;
		cutin "kafra_02.bmp",255;
		end;
	} else if (job_merchant_q == 4 || job_merchant_q == 3) {
		mes "[Kafra Employee]";
		mes "A delivery from";
		mes "the Merchant Guild?";
		mes "Oh, yes, please set";
		mes "it down right over there...";
		if (countitem(1081) == 1 || countitem(1082) == 1 || countitem(1091) == 1) {
			mes "You must be really tired";
			mes "after carrying it for so long!";
		} else {
			mes "W-wait. Didn't you bring it?";
			mes "Where's the package?";
			close2;
			cutin "kafra_02.bmp",255;
			end;
		}
		next;
		mes "[Kafra Employee]";
		mes "Now, let me check";
		mes "the serial number...";
		if (job_merchant_q2 == 7 && countitem(1081) != 0) {
			mes "3318702. Right, this is";
			mes "the one we ordered. Oh,";
			mes "and don't forget this receipt!";
			delitem 1081,1;
			getitem 1079,1;
		} else if (job_merchant_q2 == 8 && countitem(1082) != 0) {
			mes "3543625. Right, this is";
			mes "the one we ordered. Oh,";
			mes "and don't forget this receipt!";
			delitem 1082,1;
			getitem 1080,1;
		} else {
			mes "Mmmm? Hold on. This is";
			mes "the wrong package. What we";
			if (job_merchant_q2 == 7) {
				mes "ordered had the serial number 3318702. I'm sure it's not this.";
			} else if (job_merchant_q2 == 8) {
				mes "ordered had the serial number 3543625. I'm sure it's not this.";
			} else {
				mes "ordered had the serial number 3318702 or 3543625.";
			}
			close2;
			cutin "kafra_02.bmp",255;
			end;
		}
		if (job_merchant_q == 4) {
			set job_merchant_q,6;
		} else if (job_merchant_q == 3) {
			set job_merchant_q,5;
		}
		next;
		if (countitem(1072) != 0 && quest_alb_01 == 0) {
			switch(select("This is from Chief Mahnsoo of the Merchant Guild...")) {
			case 1:
				delitem 1072,1;
				set quest_alb_01,1;
				mes "[Kafra Employee]";
				mes "Oh~! A letter from";
				mes "Mahnsoo! Thank you";
				mes "so much, I've been dying";
				mes "to hear from him. How is";
				mes "he doing, is he alright?";
				mes "I can't wait to read it...";
				next;
				mes "[Kafra Employee]";
				mes "Oh, thank you for";
				mes "going through all the";
				mes "trouble of delivering all";
				mes "of this. This isn't anything";
				mes "special, but please take it.";
				mes "Well, see you again~";
				getitem 513,3;
				close2;
				cutin "kafra_02.bmp",255;
				end;
			}
		} else {
			mes "[Kafra Employee]";
			mes "Thanks again";
			mes "for going through";
			mes "all of that trouble~";
			close2;
			cutin "kafra_02.bmp",255;
			end;
		}
	}
	callfunc "F_KafraMain",0,0x5a,0,0,0,0,0,0,40,800;
	end;
}


//--------------------------------------------------------------
// Aldebaran Kafra Corporation Headquarters - Exchange Kafras
//--------------------------------------------------------------

// Special Reserve Points Kafra
aldeba_in.gat,79,161,6	script	Kafra#04	115,{

	cutin "kafra_03",2;
	mes "[Kafra]";
	mes "Welcome, ^6666FF" + strcharinfo(0) + "^000000.  This is where you can trade in your special reserve points for useful items and cool prizes.";
	next;
	mes "[Kafra]";
	mes "Each Kafra will allow you to trade in reserve points of varying amounts.  I can trade in reserve points starting from ^2222FF100 pts up to 3000 pts^000000.";
	next;
	mes "[Kafra]";
	mes "The amount of special reserve points that you have is: ^FF0000"+RESRVPTS+"^000000 pts.  Please make a choice based on your point total.";
M_Menu:
	next;
	menu 	"100- Sweet Potato 7 ea",M_1a, "200- Sweet Potato 15 ea",M_1b, "300- Sweet Potato 25 ea",M_1c, "400- Sweet Potato 35 ea",M_1d,
		"500- Sweet Potato 50 ea",M_1e, "600- Sweet Potato 60 ea",M_1f, "700- Sweet Potato 75 ea",M_1g, "800- Sweet Potato 85 ea",M_1h,
		"900- Sweet Potato 100 ea",M_1i, "1000- 1st Lottery Chance!",M_1j, "Next items",M_2, "Cancel",M_End;

	M_1a:
		if (RESRVPTS < 100) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 7;
		set RESRVPTS, RESRVPTS - 100;
		close;
	M_1b:
		if (RESRVPTS < 200) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 15;
		set RESRVPTS, RESRVPTS - 200;
		close;
	M_1c:
		if (RESRVPTS < 300) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 25;
		set RESRVPTS, RESRVPTS - 300;
		close;
	M_1d:
		if (RESRVPTS < 400) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 35;
		set RESRVPTS, RESRVPTS - 400;
		close;
	M_1e:
		if (RESRVPTS < 500) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 50;
		set RESRVPTS, RESRVPTS - 500;
		close;
	M_1f:
		if (RESRVPTS < 600) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 60;
		set RESRVPTS, RESRVPTS - 600;
		close;
	M_1g:
		if (RESRVPTS < 700) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 75;
		set RESRVPTS, RESRVPTS - 700;
		close;
	M_1h:
		if (RESRVPTS < 800) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 85;
		set RESRVPTS, RESRVPTS - 800;
		close;
	M_1i:
		if (RESRVPTS < 900) goto sL_LowPts1;
		mes "[Kafra]";
		mes "Here you are.";
		getitem 516, 100;
		set RESRVPTS, RESRVPTS - 900;
		close;
	M_1j:
		if (RESRVPTS < 1000) goto sL_LowPts1;
		set RESRVPTS, RESRVPTS - 1000;
		mes "[Kafra]";
		mes "^0000FF1st Lottery Opportunity!!^000000";
		set @Lotto, 1;
		next;
		callfunc "F_Lottery";
		goto M_End;

	sL_LowPts1:
		mes "[Kafra]";
		mes "I'm sorry but you do not have enough reserve points for that selection.";
		goto M_Menu;

	M_2:
		menu 	"1100- Red Potion 7 ea",M_2a, "1300- Red Potion 15 ea",M_2b, "1500- Red Potion 25 ea",M_2c,
			"1700- Red Potion 35 ea",M_2d, "1900- Red Potion 50 ea",M_2e, "2100- Red Potion 60 ea",M_2f,
			"2300- Red Potion 75 ea",M_2g, "2500- Red Potion 85 ea",M_2h, "2800- Red Potion 100 ea",M_2i,
			"3000- 2nd Lotery Chance!",M_2j, "Previous List",M_Menu, "Cancel",M_End;
		
		M_2a:
			if (RESRVPTS < 1100) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 7;
			set RESRVPTS, RESRVPTS - 1100;
			close;
		M_2b:
			if (RESRVPTS < 1300) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 15;
			set RESRVPTS, RESRVPTS - 1300;
			close;
		M_2c:
			if (RESRVPTS < 1500) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 25;
			set RESRVPTS, RESRVPTS - 1500;
			close;
		M_2d:
			if (RESRVPTS < 1700) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 35;
			set RESRVPTS, RESRVPTS - 1700;
			close;
		M_2e:
			if (RESRVPTS < 1900) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 50;
			set RESRVPTS, RESRVPTS - 1900;
			close;
		M_2f:
			if (RESRVPTS < 2100) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 60;
			set RESRVPTS, RESRVPTS - 2100;
			close;
		M_2g:
			if (RESRVPTS < 2300) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 75;
			set RESRVPTS, RESRVPTS - 2300;
			close;
		M_2h:
			if (RESRVPTS < 2500) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 85;
			set RESRVPTS, RESRVPTS - 2500;
			close;
		M_2i:
			if (RESRVPTS < 2800) goto sL_LowPts2;
			mes "[Kafra]";
			mes "Here you are.";
			getitem 501, 100;
			set RESRVPTS, RESRVPTS - 2800;
			close;
		M_2j:
			if (RESRVPTS < 3000) goto sL_LowPts2;
			set RESRVPTS, RESRVPTS - 3000;
			mes "[Kafra]";
			mes "^0000FF2nd Lottery Opportunity!!^000000";
			set @Lotto, 2;
			next;
			callfunc "F_Lottery";
			goto M_End;

		sL_LowPts2:
			mes "[Kafra]";
			mes "I'm sorry but you do not have enough reserve points for that selection.";
			next;
			goto M_2;

	M_End:
		mes "[Kafra]";
		mes "Please come back anytime when you have more reserve points.";
		cutin "",255;
		close;
}

// Special Reserve Points #2 Kafra
aldeba_in.gat,88,161,3	script	Kafra#05	115,{

	cutin "kafra_03",2;
	mes "[Kafra]";
	mes "Welcome ^5577FF"+strcharinfo(0)+"^000000.  We are currently having a special event for our customers.";
	mes "You can get free gifts by using your ^FF5533special reserve points^000000 in the ^3355FFSpecial Kafra Gift Event^000000!!";
	next;
	mes "[Kafra]";
	mes "Would you like to use your points?";
	next;
	menu "Yes I would.",-, "Maybe next time.",M_End;

		mes "[Kafra]";
		mes "You have the following amount of special reserve points: ^5544FF"+RESRVPTS+"^000000.";
		mes "Make a choice and test your luck!";
		next;
		menu "5000pts = 1st Lottery Chance!",sM_1st, "7000pts = 2nd Lottery Chance!",sM_2nd, "10000pts = 3rd Lottery Chance!",sM_3rd, "Cancel",M_End; 

		sM_1st:
			if (RESRVPTS < 5000) goto sL_NotEnuf;
			set RESRVPTS, RESRVPTS - 5000;
			set @Lotto, 3;
			callfunc "F_Lottery";
			goto M_End;
		sM_2nd:
			if (RESRVPTS < 7000) goto sL_NotEnuf;
			set RESRVPTS, RESRVPTS - 7000;
			set @Lotto, 4;
			callfunc "F_Lottery";
			goto M_End;
		sM_3rd:
			if (RESRVPTS < 10000) goto sL_NotEnuf;
			set RESRVPTS, RESRVPTS - 10000;
			set @Lotto, 5;
			callfunc "F_Lottery";
			goto M_End;

			sL_NotEnuf:
				mes "[Kafra]";
				mes "I'm sorry dear but you do not have enough points for this selection.";
				cutin "",255;
				close;
	M_End:
		mes "[Kafra]";
		mes "No problem.  Collect more and more special reserve points by using the Kafra Services found throughout Rune Midgard.";
		mes "Thank you for using Kafra Corp. services.";
		cutin "",255;
		close;
}

// Kafra Function F_Lottery
function	script	F_Lottery	{

	mes "[Kafra]";
	mes "You have the unique opportunity to win a prize sent down from the heavens themselves!!";
	next;
	mes "[Kafra]";
	mes "Don't miss this one and only chance! Now dear, are you ready?";
	next;
	mes "[Kafra]";
	mes "How many times do you want the Lottery Machine to spin? You can choose up to 5 times.";
	next;
	input @input;
	if (@input < 1 || @input > 5) set @input, rand(1,5); //Lupus's fix
	callsub sF_Spin;
	mes "[Kafra]";
	mes "Ok~ Let me check the results~ guess what it is?";
	next;
	mes "[Kafra]";
	mes "^FF0000Lets see.... This is...!!^000000";
	next;
	if (@temp <  1) goto sL_Prize1;
	if (@temp <  2) goto sL_Prize2;
	if (@temp <  3) goto sL_Prize3;
	if (@temp <=  4) goto sL_Prize4;
	goto sL_Prize5;

	sL_Prize1:
		mes "[Kafra]";
		mes "WOW!!!!..... You win!!! 1st Prize~! Congratulations~~ You got the 1st prize~~";
		if (@Lotto == 1) getitem 2328,1;//Items: Wooden_Mail,
		if (@Lotto == 2) getitem 2307,1;//Items: Mantle,
		if (@Lotto == 3) getitem 657,10;//Items: Berserk_Potion,
		if (@Lotto == 4) getitem 607,2;//Items: Yggdrasilberry,
		if (@Lotto == 4) getitem 608,1;//Items: Yggdrasil_Seed,
		if (@Lotto == 5) getitem 607,3;//Items: Yggdrasilberry,
		return;
	sL_Prize2:
		mes "[Kafra]";
		mes "Oh! WOW! You've won the 2nd prize! Congratulations!!";
		if (@Lotto == 1) getitem 2403,1;//Items: Shoes,
		if (@Lotto == 2) getitem 2226,1;//Items: Cap,
		if (@Lotto == 3) getitem 2201,1;//Items: Sunglasses,
		if (@Lotto == 4) getitem 526,3;//Items: Royal_Jelly,
		if (@Lotto == 5) getitem 608,1;//Items: Yggdrasil_Seed,
		if (@Lotto == 5) getitem 526,10;//Items: Royal_Jelly,
		return;
	sL_Prize3:
		mes "[Kafra]";
		mes "Congratulations! You've won the 3rd prize.";
		if (@Lotto == 1) getitem 602,4;//Items: Butterfly_Wing,
		if (@Lotto == 2) getitem 505,3;//Items: Blue_Potion,
		if (@Lotto == 3) getitem 2203,1;//Items: Glasses,
		if (@Lotto == 4) getitem 504,15;//Items: White_Potion,
		if (@Lotto == 5) getitem 504,30;//Items: White_Potion,
		return;
	sL_Prize4:
		mes "[Kafra]";
		mes "You've won the 4th prize.";
		if (@Lotto == 1) getitem 516,100;//Items: Sweet_Potato,
		if (@Lotto == 2) getitem 501,150;//Items: Red_Potion,
		if (@Lotto == 3) getitem 502,150;//Items: Orange_Potion,
		if (@Lotto == 4) getitem 505,5;//Items: Blue_Potion,
		if (@Lotto == 5) getitem 505,10;//Items: Blue_Potion,
		return;
	sL_Prize5:
		mes "[Kafra]";
		mes "You've won the 5th prize.";
		if (@Lotto == 1) getitem 516,50;//Items: Sweet_Potato,
		if (@Lotto == 2) getitem 501,100;//Items: Red_Potion,
		if (@Lotto == 3) getitem 501,200;//Items: Red_Potion,
		if (@Lotto == 4) getitem 501,250;//Items: Red_Potion,
		if (@Lotto == 5) getitem 501,300;//Items: Red_Potion,
		return;

sF_Spin:
	mes "[Lottery Machine]";
	mes "Number of spins remaining: "+@input;
	next;
	mes "[Lottery Machine]";
	mes "(rumble~rumble~rumble~)...";
	next;
	set @temp, rand(10);
	set @input, @input -1;
	if (@input <= 0) return;
	goto sF_Spin;
}
