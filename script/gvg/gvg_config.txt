//--------------------------------------------------------------
//               (c)2004-2007 Aurora Team Presents:             
//                  ___                                         
//                 /   |_   _ _ __ ___  _ __ __ _               
//                / /| | | | | '__/ _ \| '__/ _` |              
//               / ___ | |_| | | | (_) | | | (_| |              
//              /_/  |_|\____|_|  \___/|_|  \__,_|              
//                 http://aurora.deltaanime.net                 
//--------------------------------------------------------------
// Script Title: GvG Time Set Configuration	Author: jAthena/Yor/Tsuyuki
//--------------------------------------------------------------
// Revision History: v1.2
//--------------------------------------------------------------
//	1.0 First version [jAthena]
//	1.1 Translated into English [Yor]
//	1.2 Updated dialogue to iRO Aegis [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: * Official-certified *
//--------------------------------------------------------------


//==============================================================
// Beginning and End Timers for WoE
//	Time Settings are acquired from const.txt.
//	(GvGWeekDay, GvGTimeST, GvGTimeST, GvGTime*ST, GvGTime*ED)
//
// It will check if WoE begins 60s after the start of the Map Server or not.
//--------------------------------------------------------------

-	script	#AgitConfig	139,{
	end;

OnInit:
	if (GvGWeekDay == 0)
		end;
	// Set flag to 1 for each day of the week that the WoE is activated (depends of bit flag of GvGWeekDay).
	for(set '@i,0; '@i<7; set '@i,'@i+1) {
		set 'open['@i],(GvGWeekDay &(1<<'@i))? 1: 0;
	}
	// When time depends of the day of the week
	if (GvGTimeST == GvGTimeED) {
		setarray 'dbt,GvGTime0ST,GvGTime1ST,GvGTime2ST,GvGTime3ST,GvGTime4ST,GvGTime5ST,GvGTime6ST;
		setarray 'fin,GvGTime0ED,GvGTime1ED,GvGTime2ED,GvGTime3ED,GvGTime4ED,GvGTime5ED,GvGTime6ED;
	}
	// In the case of same time everyday
	else {
		cleararray 'dbt,GvGTimeST,7;
		cleararray 'fin,GvGTimeED,7;
	}
	sleep 60000;
	set '@day,gettime(4);
	set '@min,gettime(3)*100+gettime(2);
	// If today is an activated day and if it is already during the WoE time.
	if ('open['@day] && '@min>='dbt['@day] && '@min<'fin['@day]) {
		debugmes "War of Emperium has restarted [ " +gettimestr("%H:%M",6)+ " ].";
		agitstart;
	}
	sleep (60-gettime(1))*1000;	// Wait until next minute

OnTimer60000:
	initnpctimer;
	set '@day,gettime(4);
	set '@min,gettime(3)*100+gettime(2);

	// Check 24 o'clock and end of WoE
	if ('@min == 0) {
		set '@prev,('@day>0)? '@day-1: 6;
		if ('open['@prev] && 'fin['@prev]==2400) {
			if ('open['@day]==0 || 'dbt['@day]>0) {
				debugmes "The War of Emperium is over [ " +gettimestr("%H:%M",6)+ " ].";
				agitend;
			}
			end;
		}
	}
	if ('open['@day]==0)
		end;
	if ('@min == 'dbt['@day]) {
		debugmes "The War of Emperium has begun [ " +gettimestr("%H:%M",6)+ " ].";
		agitstart;
	}
	else if('@min == 'fin['@day]) {
		debugmes "The War of Emperium is over [ " +gettimestr("%H:%M",6)+ " ].";
		agitend;
	}
	end;
}


//==============================================================
//            ----- Debug mode only for GMs -----
//--------------------------------------------------------------

/*prontera.gat,152,208,0	script	GvG-Control	111,{

	if (getgmlevel(0)==0)
		end;
	mes "[WoE-Control^ff0000(GMs only)^000000]";
	mes "War of Emperium settings";
	next;
	switch(select("Start","Stop","Debug mode","Cancel")) {
	case 1:
		announce "The War of Emperium has begun.",0;
		agitstart;
		break;
	case 2:
		announce "The War of Emperium is over.",0;
		agitend;
		break;
	case 3:
		setarray '@chr$,"Sunday","Monday","Tuesday","Wednesday","Thursday","Friday","Saturday";
		for(set '@i,0; '@i<7; set '@i,'@i+1)
			mes '@chr$['@i]+ ": " +(getelementofarray( getvariableofnpc('open,"#AgitConfig"),'@i ))? "activated": "disabled";
		next;
		if (GvGTimeST != GvGTimeED) {
			mes "Time: from " +GvGTimeST+ " to " +GvGTimeED;
			break;
		}
		for(set '@i,0; '@i<7; set '@i,'@i+1) {
			mes '@chr$['@i]+ "'s time: from " +getelementofarray( getvariableofnpc('dbt,"#AgitConfig"),'@i )+
						" to " +getelementofarray( getvariableofnpc('fin,"#AgitConfig"),'@i );
		}
		break;
	}
	close;
}*/
//==============================================================
