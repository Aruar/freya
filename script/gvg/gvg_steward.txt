//--------------------------------------------------------------
//			   (c)2004-2007 Aurora Team Presents:			 
//				  ___										 
//				 /   |_   _ _ __ ___  _ __ __ _			   
//				/ /| | | | | '__/ _ \| '__/ _` |			  
//			   / ___ | |_| | | | (_) | | | (_| |			  
//			  /_/  |_|\____|_|  \___/|_|  \__,_|			  
//				 http://aurora.deltaanime.net				 
//--------------------------------------------------------------
// Script Title: GvG Steward System	Author: jAthena/Yor
//--------------------------------------------------------------
// Revision History: v1.2
//--------------------------------------------------------------
//	1.0 First version [jAthena]
//	1.1 Translated into English [Yor]
//	1.2 Fixed some issueus with investment [kosai]
//--------------------------------------------------------------
// Additional Notes: * Official-certified *
//--------------------------------------------------------------


//==============================================================
// Steward Package Functions.
//	callfunc "sF_StewardMain","AgitCode",MasterRoomX,MasterRoomY;
//
// * Parse End Processing.
//	Everery thing contains a 'close' or an 'end', but we have add a 'end' after each 'call' function.
//
// * Functions.
//	"Guardian installation" is set with the function "GuardianCall".
//	Function "F_AgitGiveUp" is called when a guild leaves a castle.
//
// * Using strnpcinfo() to get a NPC name, using getmapxy() to know map name of NPC.
//--------------------------------------------------------------

function	script	sF_StewardMain	{

	function sF_StewardSpeech {
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);	// Only '@map$ is used.
		set '@gid,getcastledata('@map$,1);
		if ('@gid <= 0) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "I am waiting for my master.";
			//mes "";
			mes "Brave adventurer, follow your destiny!";
			//mes "";
			close;
		}
		if (getcharid(0) != getguildmasterid('@gid)) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "I am here to follow";
			//mes "";
			mes "^ff0000" +getguildmaster('@gid)+ "^000000's command!";
			mes "Hey! You're not even member of the guild!!";
			mes "Where are the Guardians?";
			mes "Destroy this intruder!";
			close;
		}
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Welcome master ^ff0000" +strcharinfo(0)+ "^000000!";
		mes "I will assist you in any way I can!";
		next;
		return;
	}

	function sF_StewardConf {
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "I will inform you on the state of the Castle:";
		mes " ";
		mes "^0000ffCurrent Commerce Investment is " +getcastledata('@map$,2)+ " point(s).";
		if (getcastledata('@map$,4) != 0)
			mes "- You have invested " +getcastledata('@map$,4)+ " times today.";
			mes "Current Defense Investment is " +getcastledata('@map$,3)+ " point(s).^000000";
		if (getcastledata('@map$,5) != 0)
			mes "^0000ff- You have invested " +getcastledata('@map$,5)+ " times today.^000000";
		mes " ";
		mes "That is about it.";
		return;
	}

	function sF_StewardEco {
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "If you improve your Commerce Investment,";
		mes "the guild's productive power increases to produce more goods.";
		mes "So an investment will be required";
		mes "if you're considering future growth.";
		next;
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Usually, only one investment ispossible.";
		mes "However, you can invest up to two times a day,";
		mes "but the second investment need an additional cost.";
		//mes "";
		next;
		set '@count,getcastledata('@map$,4);
		if ('@count == 2) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "^ff0000You have already invested twice today,";
			mes "and that's the limit.";
			//mes "";
			mes "^000000I'm expecting to see our riches grow";
			mes "at a high level.";
			close;
		}
		set '@val,getcastledata('@map$,2);
		if ('@val == 100) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "^ff0000Castle's commerce investment";
			mes "is already at the maximum of 100 points.";
			mes "You don't have to invest any further.^000000";
			 close;
		}
		set '@tmp,('@val-'@count)/5;
		set '@price,5000*(1+(1+'@tmp)*'@tmp/2)*(1+3*'@count);
		switch('@count) {
		case 0:
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "The needed investment amount is";
			mes "^ff0000" +'@price+ "^000000 Zenys.";
			mes "Would you like to invest?";
			break;
		case 1:
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "You've already invested once today,";
			mes "but you can invest again at";
			mes "^ff0000" +'@price+ "^000000 Zenys";
			//mes "";
			break;
		}
		next;
		if (select("Invest Commerce.","Cancel.")==2) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "I'll wait for you.";
			mes "There's no need to be in a hurry.";
			mes "You can come back anytime.";
			close;
		}
		if (Zeny < '@price) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "Master!";
			mes "You do not have enough money to invest.";
			mes "Investment has been cancelled.";
			close;
		}
		set Zeny,Zeny-'@price;
		setcastledata '@map$,4,'@count+1;
		set '@tmp,getcastledata('@map$,2)+1;
		if (getgdskilllv(getcharid(2),GD_DEVELOPMENT) == 1 && (rand(100) >= 50) && ('@tmp < 100)) {
			set '@tmp,'@tmp+1;
		}
		setcastledata '@map$,2,'@tmp;
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Investment was successful!";
		mes "I hope we will gain more and more goods.";
		mes "The investment will be taken in account from tomorrow on.";
		return;
	}	

	function sF_StewardDef {
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "If you improve investment of defense,";
		mes "the durability of our Guardians";
		mes "and the Emperium will increase.";
		mes "So if you consider our future battles,";
		mes "an investment will be required.";
		next;
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Usually, only one investment ispossible.";
		mes "However, you can invest up to two times a day,";
		mes "but the second investment need an additional cost.";
		next;
		set '@count,getcastledata('@map$,5);
		if ('@count == 2) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "^ff0000You have already invested twice today,";
			mes "and that's the limit.";
			//mes "";
			mes "^000000I'm expecting to see our development grow";
			mes "at a high level.";
			close;
		}
		set '@val,getcastledata('@map$,3);
		if ('@val == 100) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "^ff0000Castle's Defense Investment is already maxed at 100 points.";
			mes "You can not invest any further.^000000";
			close;
		}
		set '@tmp,('@val-'@count)/5;
		set '@price,10000*(1+(1+'@tmp)*'@tmp/2)*(1+3*'@count);
		switch('@count) {
		case 0:
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "The needed investment amount is";
			mes "^ff0000" +'@price+ "^000000 Zenys.";
			mes "Would you like to invest?";
			break;
		case 1:
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "You've already invested once today,";
			mes "but you can invest again at";
			mes "^ff0000" +'@price+ "^000000 Zenys.";
			mes "Would you like to invest again?";
			break;
		}
		next;
		if (select("Invest Defense.","Cancel.")==2) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "I'll wait for you.";
			mes "There's no need to be in a hurry.";
			mes "You can come back anytime.";
			close;
		}
		if (Zeny < '@price) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "Master!";
			mes "You do not have enough money to invest.";
			mes "Investment has been cancelled.";
			close;
		}
		set Zeny,Zeny-'@price;
		setcastledata '@map$,5,'@count+1; //+1 times invested
		set '@tmp,getcastledata('@map$,3)+1; //+1 to Def points (saved in tmp)
		setcastledata '@map$,3,'@tmp; //New Def points value after investment
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Investment was successful!";
		mes "I hope we will gain more and more goods.";
		mes "The investment will be taken in account from tomorrow on.";
		return;
	}

	function sF_StewardGuard {
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Would you like to install a Guardian?";
		mes "Guardians will protect the Castle";
		mes "from enemies.";
		mes "Please choose a Guardian.";
		//mes "";
		next;
		// Actual Menu(B5 reference)
		//menu	 "Install Archer Guardian.",L_MENU_1,"Install Archer Guardian.",L_MENU_2,
		//	"Install Soldier Guardian.",L_MENU_3,"Install Soldier Guardian.",L_MENU_4,
		//	"Install Knight Guardian.",L_MENU_5,"Install Soldier Guardian.",L_MENU_6,
		//	"Install Knight Guardian.",L_MENU_7,"Master Guardian can't be install. (31214/31214)",L_MENU_8;
		//
		// It will check if a Guardian can be installed or not, and will indicate his HP.
		// Moreover, you can install a Guardian even if it is already present, in this case, it will recover his HP.
		set '@ret,select("Guardian 1","Guardian 2","Guardian 3","Guardian 4","Guardian 5","Guardian 6","Guardian 7","Guardian 8")+9;
		if (getcastledata('@map$,'@ret)) {
			mes "[Steward "+strnpcinfo(0)+"]"; // Guardian is already installed.
			mes "Master!";
			mes "This Guardian has already been installed.";
			mes " ";
			mes "Guardian installation has been cancelled.";
			close;
		}
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Would you really install";
		mes "this Guardian? You need";
		mes "^ff000010000^000000Zenys.";
		next;
		if (select("Install the Guardian.","Cancel.")==2) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "I'll wait for you.";
			mes "When some room will be made";
			mes "I strongly recommend";
			mes "to install a maximum of Guardians.";
			close;
		}
		if (getgdskilllv(getcharid(2),GD_GUARDIANRESEARCH) == 0) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "I'm sorry Master but you cannot install";
			mes "any Guardians right now.";
			mes "Your guild must learn the Guild skill";
			//mes "";
			mes "^ff0000Guardian Research^000000 first.";
			mes "Guardian installation has been cancelled.";
			close;
		}
		if (Zeny < 10000) {	// Not enough money
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "Master!";
			mes "You do not have enough money to install this Guardian.";
			mes "Guardian installation has been cancelled.";
			close;
		}
		set Zeny,Zeny-10000;
		setcastledata '@map$,'@ret,1;
		callfunc "F_GuardianCall",getarg(0),'@ret,1;
		return;
	}

	function sF_StewardKafra {
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
		if (getcastledata('@map$,9)==0) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "Would you like to employ";
			mes "the services of a Kafra?";
			mes "You will need ^ff000010000^000000 Zenys to do so...";
			next;
			if (select("Employ Kafra.","Cancel.")==2) {
				mes "[Steward "+strnpcinfo(0)+"]";
				mes "I'll wait for you.";
				mes "But the convenience of";
				mes "the Guild members";
				mes "must not be forgotten.";
				close;
			}
			if (getgdskilllv(getcharid(2),GD_KAFRACONTACT) == 0) {
				mes "[Steward "+strnpcinfo(0)+"]";
				mes "Master... The contract with";
				mes "the Kafra Headquarter hasn't been made yet.";
				mes "If the contract isn't conclude";
				mes "you cannot employ a Kafra.";
				next;
				mes "[Steward "+strnpcinfo(0)+"]";
				mes "In order to hire a Kafra,";
				mes "you must first learn the Guild skill";
				mes "^ff0000Contract with Kafra^000000";
				mes "Kafra installation has been cancelled.";
				close;
			}
			if (Zeny < 10000) {
				mes "[Steward "+strnpcinfo(0)+"]";
				mes "Master! You don't have enough money!";
				close;
			}
			set Zeny,Zeny-10000;
			enablenpc "AgitKafra_"+getarg(0);
			setcastledata '@map$,9,1;
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "You have created a contract with the Kafra Service Company.";
			next;
			cutin "kafra_01",2;
			mes "[Kafra]";
			mes "How do you do?";
			mes "I'm here to provide you with helpful service!";
			mes "I'll do the best I can to serve you.";
			next;
			cutin "kafra_01",255;
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "Every month you have to pay Zeny";
			mes "in order to maintain the contract.";
			mes "(not now)";
		}
		else {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "Would you like to lay off the current Kafra?";
			next;
			if (select("Lay off the Kafra.","Cancel.")==2) {
				mes "[Steward "+strnpcinfo(0)+"]";
				mes "Understood.";
				close;
			}
			cutin "kafra_01",2;
			mes "[Kafra]";
			mes "Have I done anything wrong?";
			mes "If I did, will you please forgive me?";
			//mes "";
			next;
			if (select("Lay off the Kafra.","Cancel.")==2) {
				mes "[Kafra]";
				mes "Thank you Master!";
				mes "I'll do my best!";
				close2;
				cutin "kafra_01",255;
				end;
			}
			mes "[Kafra]";
			mes "It's unfortunate that I won't be able to serve your Guild anymore...";
			next;
			disablenpc "AgitKafra_"+getarg(0);
			setcastledata '@map$,9,0;
			cutin "kafra_01",255;
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "...";
			mes "The Kafra has been layed off...";
		}
		return;
	}

	function sF_StewardRoom {
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Would you like to go to our Treasure Room?";
		//mes "";
		//mes "";
		mes "Only you, the Guild Master,";
		mes "are allowed to enter this room.";
		next;
		if (select("Enter in Treasure Room.","Cancel.")==1) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "I will guide you.";
			mes "Please follow me.";
			mes "You must pull down on the secret switch";
			mes "in order to get out.";
			close2;
			warp '@map$,getarg(1),getarg(2);
			end;
		}
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "The goods are produced everyday.";
		mes "The goods are saved to a certain extend.";
		mes "But after a certain amount the production stops.";
		mes "So you have to come often to pick up the goods.";
		mes "If you want to increase the goods of the Guild, you must not forget.";
		return;
	}

	function sF_StewardGiveup {
		set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Master!";
		mes "Do you really want to abandoned the Castle?";
		mes " ";
		mes "Oh please reconsider Master!";
		next;
		if (select("Abandon the Castle.","Cancel.")==1) {
			mes "[Steward "+strnpcinfo(0)+"]";
			mes "Master!";
			mes "Oh please reconsider one more time.";
			mes "Please I beg you!!!!!!!!";
			next;
			if (select("Cancel.","Abandon the Castle.")==2) {
				mes "[Steward "+strnpcinfo(0)+"]";
				mes "Master!...";
				mes "... it is... it is... the end...";
				close2;
				announce "Castle [" +getcastlename('@map$)+ "] has been abandonned by [" +getguildname(getcharid(2))+ "] guild.",0;
				callfunc "F_AgitGiveUp",getarg(0);
				end;
			}
		}
		mes "[Steward "+strnpcinfo(0)+"]";
		mes "Thank you! Master!";
		mes "Please stop doing such a joke!";
		return;
	}

	sF_StewardSpeech;
	switch(select("Display Castle state","Commercial investment","Defense investment","Install Guardians","Contract/Lay off with kafra","Enter in Treasure Room.","Abandon the Castle.")) {
		case 1:
			sF_StewardConf;
			close;
		case 2:
			sF_StewardEco;
			close;
		case 3:
			sF_StewardDef;
			close;
		case 4:
			sF_StewardGuard getarg(0);
			close;
		case 5:
			sF_StewardKafra getarg(0);
			close;
		case 6:
			sF_StewardRoom 0,getarg(1),getarg(2);
			close;
		case 7:
			sF_StewardGiveup getarg(0);
			close;
	}
	return;
}


//==============================================================
// Aldebaran
//--------------------------------------------------------------

aldeg_cas01.gat,218,175,0	script	Arl Fredo	55,{
	callfunc "sF_StewardMain","L1",113,223;
	end;
}

aldeg_cas02.gat,78,74,0		script	Chen Lee	55,{
	callfunc "sF_StewardMain","L2",134,225;
	end;
}

aldeg_cas03.gat,110,118,0	script	Najarf	55,{
	callfunc "sF_StewardMain","L3",219,268;
	end;
}

aldeg_cas04.gat,67,116,0	script	Wiriot	55,{
	callfunc "sF_StewardMain","L4",79,14;
	end;
}

aldeg_cas05.gat,51,179,0	script	Brimhemsen	55,{
	callfunc "sF_StewardMain","L5",68,13;
	end;
}


//==============================================================
// Geffen
//--------------------------------------------------------------

gefg_cas01.gat,40,48,5		script	Gnaucher	55,{
	callfunc "sF_StewardMain","B1",151,109;
	end;
}

gefg_cas02.gat,12,66,0		script	Esmark	55,{
	callfunc "sF_StewardMain","B2",137,113;
	end;
}

gefg_cas03.gat,106,23,3		script	Jyang	55,{
	callfunc "sF_StewardMain","B3",267,287;
	end;
}

gefg_cas04.gat,73,46,3		script	Kelbany	55,{
	callfunc "sF_StewardMain","B4",113,115;
	end;
}

gefg_cas05.gat,70,52,3		script	Beeor	55,{
	callfunc "sF_StewardMain","B5",141,107;
	end;
}


//==============================================================
// Payon
//--------------------------------------------------------------

payg_cas01.gat,120,58,4		script	Grunday	55,{
	callfunc "sF_StewardMain","C1",287,5;
	end;
}

payg_cas02.gat,22,260,7		script	Cherrios	55,{
	callfunc "sF_StewardMain","C2",141,141;
	end;
}

payg_cas03.gat,50,261,3		script	Garriet	55,{
	callfunc "sF_StewardMain","C3",155,165;
	end;
}

payg_cas04.gat,38,284,3		script	DJ	55,{
	callfunc "sF_StewardMain","C4",143,45;
	end;
}

payg_cas05.gat,277,249,3	script	Najarf	55,{
	callfunc "sF_StewardMain","C5",153,129;
	end;
}


//==============================================================
// Prontera
//--------------------------------------------------------------

prtg_cas01.gat,112,181,0	script	Abrai	55,{
	callfunc "sF_StewardMain","V1",7,205;
	end;
}

prtg_cas02.gat,94,61,4		script	Rhay	55,{
	callfunc "sF_StewardMain","V2",199,225;
	end;
}

prtg_cas03.gat,51,100,4		script	Stick	55,{
	callfunc "sF_StewardMain","V3",185,129;
	end;
}

prtg_cas04.gat,259,265,4	script	Bandred	55,{
	callfunc "sF_StewardMain","V4",267,159;
	end;
}

prtg_cas05.gat,36,37,4		script	Reiner	55,{
	callfunc "sF_StewardMain","V5",273,175;
	end;
}
