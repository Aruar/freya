//--------------------------------------------------------------
//               (c)2004-2007 Aurora Team Presents:             
//                  ___                                         
//                 /   |_   _ _ __ ___  _ __ __ _               
//                / /| | | | | '__/ _ \| '__/ _` |              
//               / ___ | |_| | | | (_) | | | (_| |              
//              /_/  |_|\____|_|  \___/|_|  \__,_|              
//                 http://aurora.deltaanime.net                 
//--------------------------------------------------------------
// Script Title: GvG Core System	Author: jAthena/Yor/Tsuyuki
//--------------------------------------------------------------
// Revision History: v1.2
//--------------------------------------------------------------
//	1.0 First version [jAthena]
//	1.1 Translated into English [Yor]
//	1.2 Updated dialogue to iRO Aegis [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: * Official-certified *
//--------------------------------------------------------------


//==============================================================
// WoE starting and guild changements common functions.
//	callfunc "F_AgitGiveUp","AgitCode";
//
// * Run
//	"::OnFlagEmb_XY"
//
// * Function
//	When it is WoE, function "AgitSummon" is called.
//--------------------------------------------------------------

function	script	F_AgitGiveUp	{
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);	// Only '@map$ is used.
	killmonsterall '@map$;
	disablenpc "AgitKafra_"+getarg(0);
	for(set '@i,1; '@i<=17; set '@i,'@i+1) {
		setcastledata '@map$,'@i,('@i==2 || '@i==3)? 1: 0;
	}
	donpcevent "::OnFlagEmb_"+getarg(0); // Changing of the flag emblems.
	maprespawnguildid '@map$,0,7;
	if (agitcheck())
		callfunc "F_AgitSummon",getarg(0); // Summoning of monsters and the Emperium.
	return;
}


//==============================================================
// Section: execution of attack castle.
//	All processings are performed here!
//
// * The information about position is acquired with strnpcinfo(2).
//   Because it is duplicate function, Map names and NPC names are acquired with specifical functions.
//--------------------------------------------------------------

-	script	AgitExe	139,{

OnAgitInit:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);	// Only '@map$ is used.
	if ('@map$ == "-") // The 'duplicate' has no origin.
		end;
	if (getcastledata('@map$,9) == 0)
		disablenpc "AgitKafra_"+strnpcinfo(2);
	for(set '@i,10; '@i<=17; set '@i,'@i+1) { // Summon Guardians if they exist.
		if (getcastledata('@map$,'@i))
			callfunc "F_GuardianCall",strnpcinfo(2),'@i,0;
	}
	end;

OnAgitStart:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-")
		end;
	maprespawnguildid '@map$,getcastledata('@map$,1),2;
	callfunc "F_AgitSummon",strnpcinfo(2); // Summoning of Emperium.
	gvgon '@map$;
	end;

OnAgitBreak:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-")
		end;
	killmonsterall '@map$;
	set '@gid,getcharid(2);	// Get the Emperium ID.
	if ('@gid <= 0)
		end;
	setcastledata '@map$,1,'@gid;
	for(set '@i,2; '@i<=17; set '@i,'@i+1) {
		if ('@i==2 || '@i==3) {
			set '@val,getcastledata('@map$,'@i)-5;
			setcastledata '@map$,'@i,('@val<1)? 1: '@val;
		}
		else
			setcastledata '@map$,'@i,0;
	}
	announce "The [" +getcastlename('@map$)+ "] castle has been conquered by the [" +getguildname('@gid)+ "] guild.",0;
	announce "The Emperium has been destroyed.",9,0x00ff00;
	donpcevent "::OnFlagEmb_"+strnpcinfo(2); // Changing of the flag emblems.
	end;

OnAgitEliminate:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-")
		end;
	maprespawnguildid '@map$,getcastledata('@map$,1),6;
	callfunc "F_AgitSummon",strnpcinfo(2); // Only the Emperium is resummoned.
	end;

OnGuildBreak:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-")
		end;
	callfunc "F_AgitGiveUp",strnpcinfo(2);
	end;

OnAgitEnd:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-")
		end;
	maprespawnguildid '@map$,getcastledata('@map$,1),4;
	killmonster '@map$,strnpcinfo(0)+"::OnAgitBreak"; // Only the Emperium is removed.
	gvgoff '@map$;
	end;
}

// Aldebaran
aldeg_cas01.gat,0,0,0	duplicate(AgitExe)	AgitExe#L1	-1
aldeg_cas02.gat,0,0,0	duplicate(AgitExe)	AgitExe#L2	-1
aldeg_cas03.gat,0,0,0	duplicate(AgitExe)	AgitExe#L3	-1
aldeg_cas04.gat,0,0,0	duplicate(AgitExe)	AgitExe#L4	-1
aldeg_cas05.gat,0,0,0	duplicate(AgitExe)	AgitExe#L5	-1

// Geffen
gefg_cas01.gat,0,0,0	duplicate(AgitExe)	AgitExe#B1	-1
gefg_cas02.gat,0,0,0	duplicate(AgitExe)	AgitExe#B2	-1
gefg_cas03.gat,0,0,0	duplicate(AgitExe)	AgitExe#B3	-1
gefg_cas04.gat,0,0,0	duplicate(AgitExe)	AgitExe#B4	-1
gefg_cas05.gat,0,0,0	duplicate(AgitExe)	AgitExe#B5	-1

// Payon
payg_cas01.gat,0,0,0	duplicate(AgitExe)	AgitExe#C1	-1
payg_cas02.gat,0,0,0	duplicate(AgitExe)	AgitExe#C2	-1
payg_cas03.gat,0,0,0	duplicate(AgitExe)	AgitExe#C3	-1
payg_cas04.gat,0,0,0	duplicate(AgitExe)	AgitExe#C4	-1
payg_cas05.gat,0,0,0	duplicate(AgitExe)	AgitExe#C5	-1

// Prontera
prtg_cas01.gat,0,0,0	duplicate(AgitExe)	AgitExe#V1	-1
prtg_cas02.gat,0,0,0	duplicate(AgitExe)	AgitExe#V2	-1
prtg_cas03.gat,0,0,0	duplicate(AgitExe)	AgitExe#V3	-1
prtg_cas04.gat,0,0,0	duplicate(AgitExe)	AgitExe#V4	-1
prtg_cas05.gat,0,0,0	duplicate(AgitExe)	AgitExe#V5	-1
