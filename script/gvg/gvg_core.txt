//--------------------------------------------------------------
//               (c)2004-2007 Aurora Team Presents:             
//                  ___                                         
//                 /   |_   _ _ __ ___  _ __ __ _               
//                / /| | | | | '__/ _ \| '__/ _` |              
//               / ___ | |_| | | | (_) | | | (_| |              
//              /_/  |_|\____|_|  \___/|_|  \__,_|              
//                 http://aurora.deltaanime.net                 
//--------------------------------------------------------------
// Script Title: GvG Core System	Author: jAthena/Tsuyuki
//--------------------------------------------------------------
// Revision History: v1.1
//--------------------------------------------------------------
//	1.0 First version [jAthena]
//	1.1 Updated to iRO Aegis/official [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: * Official-certified / Aegis-certified *
//--------------------------------------------------------------


//--------------------------------------------------------------
// Documentation:
//--------------------------------------------------------------
// Time settings are acquired from "db/const.txt".
// (GvGWeekDay, GvGTimeST, GvGTimeST, GvGTime*ST, GvGTime*ED)
//--------------------------------------------------------------


//--------------------------------------------------------------
// GvG System Activation/Deactivation Clock - [jAthena]
//--------------------------------------------------------------

-	script	#AgitConfig	139,{

OnInit:
	if (GvGWeekDay == 0)
		end;
	for(set '@i,0; '@i < 7; set '@i,'@i + 1) {
		set 'open['@i],(GvGWeekDay & (1 << '@i)) ? 1 : 0;
	}
	if (GvGTimeST == GvGTimeED) {
		setarray 'dbt,GvGTime0ST,GvGTime1ST,GvGTime2ST,GvGTime3ST,GvGTime4ST,GvGTime5ST,GvGTime6ST;
		setarray 'fin,GvGTime0ED,GvGTime1ED,GvGTime2ED,GvGTime3ED,GvGTime4ED,GvGTime5ED,GvGTime6ED;
	} else {
		cleararray 'dbt,GvGTimeST,7;
		cleararray 'fin,GvGTimeED,7;
	}
	sleep 60000;
	set '@day,gettime(4);
	set '@min,gettime(3) * 100 + gettime(2);
	if ('open['@day] && '@min >= 'dbt['@day] && '@min < 'fin['@day]) {
		debugmes "War of Emperium has restarted [ "+gettimestr("%H:%M",6)+" ].";
		agitstart;
	}
	sleep (60 - gettime(1)) * 1000;

OnTimer60000:
	initnpctimer;
	set '@day,gettime(4);
	set '@min,gettime(3) * 100 + gettime(2);
	if ('@min == 0) {
		set '@prev,('@day > 0) ? '@day - 1 : 6;
		if ('open['@prev] && 'fin['@prev] == 2400) {
			if ('open['@day] == 0 || 'dbt['@day] > 0) {
				debugmes "The War of Emperium is over [ "+gettimestr("%H:%M",6)+" ].";
				agitend;
			}
			end;
		}
	}
	if ('open['@day] == 0)
		end;
	if ('@min == 'dbt['@day]) {
		debugmes "The War of Emperium has begun [ "+gettimestr("%H:%M",6)+" ].";
		agitstart;
	} else if ('@min == 'fin['@day]) {
		debugmes "The War of Emperium is over [ "+gettimestr("%H:%M",6)+" ].";
		agitend;
	}
	end;
}


//--------------------------------------------------------------
// GvG System Core Execution - [jAthena]
//--------------------------------------------------------------

-	script	AgitExe	139,{

OnAgitInit:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-")
		end;
	if (getcastledata('@map$,9) == 0)
		disablenpc "AgitKafra_"+strnpcinfo(2);
	for(set '@i,10; '@i <= 17; set '@i,'@i + 1) {
		if (getcastledata('@map$,'@i))
			callfunc "F_GuardianCall",strnpcinfo(2),'@i,0;
	}
	end;

OnAgitStart:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-") {
		end;
	}
	maprespawnguildid '@map$,getcastledata('@map$,1),2;
	callfunc "F_AgitSummon",strnpcinfo(2);
	gvgon '@map$;
	end;

OnAgitBreak:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-") {
		end;
	}
	killmonsterall '@map$;
	set '@gid,getcharid(2);
	if ('@gid <= 0) {
		end;
	}
	setcastledata '@map$,1,'@gid;
	for(set '@i,2; '@i <= 17; set '@i,'@i + 1) {
		if ('@i == 2 || '@i == 3) {
			set '@val,getcastledata('@map$,'@i) - 5;
			setcastledata '@map$,'@i,('@val < 1) ? 1 : '@val;
		} else {
			setcastledata '@map$,'@i,0;
		}
	}
	announce "The ["+getcastlename('@map$)+"] castle has been conquered by the ["+getguildname('@gid)+"] guild.",0;
	announce "The Emperium has been destroyed.",9,0x00ff00;
	donpcevent "::OnFlagEmb_"+strnpcinfo(2);
	end;

OnAgitEliminate:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-") {
		end;
	}
	maprespawnguildid '@map$,getcastledata('@map$,1),6;
	callfunc "F_AgitSummon",strnpcinfo(2);
	end;

OnGuildBreak:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-") {
		end;
	}
	killmonsterall '@map$;
	disablenpc "AgitKafra_"+strnpcinfo(2);
	for(set '@i,1; '@i <= 17; set '@i,'@i + 1) {
		setcastledata '@map$,'@i,('@i == 2 || '@i == 3) ? 1 : 0;
	}
	donpcevent "::OnFlagEmb_"+strnpcinfo(2);
	maprespawnguildid '@map$,0,7;
	if (agitcheck()) {
		callfunc "F_AgitSummon",getarg(0);
	}
	end;

OnAgitEnd:
	set '@dummy,getmapxy('@map$,'@dummy,'@dummy,1);
	if ('@map$ == "-") {
		end;
	}
	maprespawnguildid '@map$,getcastledata('@map$,1),4;
	killmonster '@map$,strnpcinfo(0)+"::OnAgitBreak";
	gvgoff '@map$;
	end;
}


//--------------------------------------------------------------
// GvG System Core Castle Duplicates - [jAthena]
//--------------------------------------------------------------

aldeg_cas01.gat,0,0,0	duplicate(AgitExe)	AgitExe#L1	139
aldeg_cas02.gat,0,0,0	duplicate(AgitExe)	AgitExe#L2	139
aldeg_cas03.gat,0,0,0	duplicate(AgitExe)	AgitExe#L3	139
aldeg_cas04.gat,0,0,0	duplicate(AgitExe)	AgitExe#L4	139
aldeg_cas05.gat,0,0,0	duplicate(AgitExe)	AgitExe#L5	139

gefg_cas01.gat,0,0,0	duplicate(AgitExe)	AgitExe#B1	139
gefg_cas02.gat,0,0,0	duplicate(AgitExe)	AgitExe#B2	139
gefg_cas03.gat,0,0,0	duplicate(AgitExe)	AgitExe#B3	139
gefg_cas04.gat,0,0,0	duplicate(AgitExe)	AgitExe#B4	139
gefg_cas05.gat,0,0,0	duplicate(AgitExe)	AgitExe#B5	139

payg_cas01.gat,0,0,0	duplicate(AgitExe)	AgitExe#C1	139
payg_cas02.gat,0,0,0	duplicate(AgitExe)	AgitExe#C2	139
payg_cas03.gat,0,0,0	duplicate(AgitExe)	AgitExe#C3	139
payg_cas04.gat,0,0,0	duplicate(AgitExe)	AgitExe#C4	139
payg_cas05.gat,0,0,0	duplicate(AgitExe)	AgitExe#C5	139

prtg_cas01.gat,0,0,0	duplicate(AgitExe)	AgitExe#V1	139
prtg_cas02.gat,0,0,0	duplicate(AgitExe)	AgitExe#V2	139
prtg_cas03.gat,0,0,0	duplicate(AgitExe)	AgitExe#V3	139
prtg_cas04.gat,0,0,0	duplicate(AgitExe)	AgitExe#V4	139
prtg_cas05.gat,0,0,0	duplicate(AgitExe)	AgitExe#V5	139
